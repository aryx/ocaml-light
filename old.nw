\ifallcode
<<copyright header2>>=
(***********************************************************************)
(*                                                                     *)
(*                           Objective Caml                            *)
(*                                                                     *)
(* Xavier Leroy and Jerome Vouillon, projet Cristal, INRIA Rocquencourt*)
(*                                                                     *)
(*  Copyright 1996 Institut National de Recherche en Informatique et   *)
(*  Automatique.  Distributed only by permission.                      *)
(*                                                                     *)
(***********************************************************************)
@
\fi



\section{Naming errors}

<<type Env.error>>=
(* Error report *)

type error =
    Not_an_interface of string
  | Corrupted_interface of string
  | Illegal_renaming of string * string
  | Inconsistent_import of string * string * string
@

<<exception Env.Error>>=
exception Error of error
@

<<signature Env.report_error>>=
val report_error: error -> unit
@

<<function Env.report_error>>=
(* Error report *)

let report_error = function
    Not_an_interface filename ->
      print_string filename; print_space();
      print_string "is not a compiled interface."
  | Corrupted_interface filename ->
      print_string "Corrupted compiled interface"; print_space();
      print_string filename
  | Illegal_renaming(modname, filename) ->
      print_string "Wrong file naming:"; print_space();
      print_string filename; print_space();
      print_string "contains the compiled interface for"; print_space();
      print_string modname
  | Inconsistent_import(name, source1, source2) ->
      open_hvbox 0;
      print_string "The compiled interfaces for "; print_string source1;
      print_string " and "; print_string source2; print_space();
      print_string "make inconsistent assumptions over interface ";
      print_string name;
      close_box()
@


\section{Typing errors}

\subsection{Types}

<<type Typetexp.error>>=
type error =
    Unbound_type_variable of string
  | Unbound_type_constructor of Longident.t
  | Type_arity_mismatch of Longident.t * int * int
  | Bound_type_variable of string
  | Type_mismatch of (Types.type_expr * Types.type_expr) list
  | Alias_type_mismatch of (Types.type_expr * Types.type_expr) list
@

<<exception Typetexp.Error>>=
exception Error of Location.t * error
@

<<signature Typetexp.report_error>>=
val report_error: error -> unit
@


<<constant Typetexp.report_error>>=
let report_error = function
    Unbound_type_variable name ->
      print_string "Unbound type parameter "; print_string name
  | Unbound_type_constructor lid ->
      print_string "Unbound type constructor "; longident lid
  | Type_arity_mismatch(lid, expected, provided) ->
      open_box 0;
      print_string "The type constructor "; longident lid;
      print_space(); print_string "expects "; print_int expected;
      print_string " argument(s),"; print_space();
      print_string "but is here applied to "; print_int provided;
      print_string " argument(s)";
      close_box()
  | Bound_type_variable name ->
      print_string "Already bound type parameter "; print_string name
  | Type_mismatch trace ->
      Printtyp.unification_error trace
        (function () ->
           print_string "This type parameter")
        (function () ->
           print_string "should be an instance of type")
  | Alias_type_mismatch trace ->
      Printtyp.unification_error trace
        (function () ->
           print_string "This alias is bound to type")
        (function () ->
           print_string "but is used as an instance of type")
@

\subsection{Modules}

<<type Typemod.error>>=
type error =
    Unbound_module of Longident.t
  | Unbound_modtype of Longident.t
  | Not_included of Includemod.error list
  | Signature_expected
  | Structure_expected of module_type
  | With_no_component of Longident.t
  | With_mismatch of Longident.t * Includemod.error list
  | Repeated_name of string * string
  | Non_generalizable of type_expr
  | Non_generalizable_module of module_type
@

<<exception Typemod.Error>>=
exception Error of Location.t * error
@

<<signature Typemod.report_error>>=
val report_error: error -> unit
@


<<constant Typemod.report_error>>=
let report_error = function
    Unbound_module lid ->
      print_string "Unbound module "; longident lid
  | Unbound_modtype lid ->
      print_string "Unbound module type "; longident lid
  | Not_included errs ->
      open_vbox 0;
      print_string "Signature mismatch:"; print_space();
      Includemod.report_error errs;
      close_box()
  | Signature_expected ->
      print_string "This module type is not a signature"
  | Structure_expected mty ->
      open_box 0;
      print_string "This module is not a structure; it has type";
      print_space(); modtype mty;
      close_box()
  | With_no_component lid ->
      open_box 0;
      print_string "The signature constrained by `with' has no component named";
      print_space(); longident lid;
      close_box()
  | With_mismatch(lid, explanation) ->
      open_vbox 0;
      open_box 0;
      print_string "In this `with' constraint, the new definition of";
      print_space(); longident lid; print_space();
      print_string "does not match its original definition";
      print_space(); print_string "in the constrained signature:";
      close_box();
      print_space();
      Includemod.report_error explanation;
      close_box()
  | Repeated_name(kind, name) ->
      open_box 0;
      print_string "Multiple definition of the "; print_string kind;
      print_string " name "; print_string name; print_string ".";
      print_space();
      print_string "Names must be unique in a given structure.";
      close_box()
  | Non_generalizable typ ->
      open_box 0;
      print_string "The type of this expression,"; print_space();
      type_scheme typ; print_string ","; print_space();
      print_string "contains type variables that cannot be generalized";
      close_box()
  | Non_generalizable_module mty ->
      open_box 0;
      print_string "The type of this module,"; print_space();
      modtype mty; print_string ","; print_space();
      print_string "contains type variables that cannot be generalized";
      close_box()
@





<<type Includemod.error>>=
type error =
    Missing_field of Ident.t
  | Value_descriptions of Ident.t * value_description * value_description
  | Type_declarations of Ident.t * type_declaration * type_declaration
  | Exception_declarations of
      Ident.t * exception_declaration * exception_declaration
  | Module_types of module_type * module_type
  | Modtype_permutation
  | Interface_mismatch of string * string
@

<<exception Includemod.Error>>=
exception Error of error list
@

<<signature Includemod.report_error>>=
val report_error: error list -> unit
@


<<constant Includemod.include_err>>=
let include_err = function
    Missing_field id ->
      print_string "The field `"; ident id; 
      print_string "' is required but not provided"
  | Value_descriptions(id, d1, d2) ->
      open_hvbox 2;
      print_string "Values do not match:"; print_space();
      value_description id d1; 
      print_break 1 (-2);
      print_string "is not included in"; print_space();
      value_description id d2;
      close_box()
  | Type_declarations(id, d1, d2) ->
      open_hvbox 2;
      print_string "Type declarations do not match:"; print_space();
      type_declaration id d1; 
      print_break 1 (-2);
      print_string "is not included in"; print_space();
      type_declaration id d2;
      close_box()
  | Exception_declarations(id, d1, d2) ->
      open_hvbox 2;
      print_string "Exception declarations do not match:"; print_space();
      exception_declaration id d1; 
      print_break 1 (-2);
      print_string "is not included in"; print_space();
      exception_declaration id d2;
      close_box()
  | Module_types(mty1, mty2)->
      open_hvbox 2;
      print_string "Modules do not match:"; print_space();
      modtype mty1;
      print_break 1 (-2);
      print_string "is not included in"; print_space();
      modtype mty2;
      close_box()
  | Modtype_permutation ->
      print_string "Illegal permutation of structure fields"
  | Interface_mismatch(impl_name, intf_name) ->
      open_box 0;
      print_string "The implementation "; print_string impl_name;
      print_space(); print_string "does not match the interface ";
      print_string intf_name;
      print_string ":";
      close_box()
@

<<function Includemod.report_error>>=
let report_error errlist =
  match errlist with
    [] -> ()
  | err :: rem ->
      open_vbox 0;
      include_err err;
      List.iter (fun err -> print_space(); include_err err) rem;
      close_box()
@

\subsection{Declarations}

<<type Typedecl.error>>=
type error =
    Repeated_parameter
  | Duplicate_constructor of string
  | Too_many_constructors
  | Duplicate_label of string
  | Recursive_abbrev of string
  | Definition_mismatch of type_expr
  | Unconsistent_constraint
  | Type_clash of (type_expr * type_expr) list
  | Null_arity_external
@

<<exception Typedecl.Error>>=
exception Error of Location.t * error
@

<<signature Typedecl.report_error>>=
val report_error: error -> unit
@


<<constant Typedecl.report_error>>=
let report_error = function
    Repeated_parameter ->
      print_string "A type parameter occurs several times"
  | Duplicate_constructor s ->
      print_string "Two constructors are named "; print_string s
  | Too_many_constructors ->
      print_string "Too many constructors -- maximum is ";
      print_int Config.max_tag; print_string " constructors"
  | Duplicate_label s ->
      print_string "Two labels are named "; print_string s
  | Recursive_abbrev s ->
      print_string "The type abbreviation "; print_string s;
      print_string " is cyclic" (* " expands to itself" *)
  | Definition_mismatch ty ->
      Printtyp.reset ();
      Printtyp.mark_loops ty;
      print_string
        "The variant or record definition does not match that of type";
      print_space(); Printtyp.type_expr ty
  | Unconsistent_constraint ->
      print_string "The type constraints are not consistent"
  | Type_clash trace ->
      Printtyp.unification_error trace
        (function () ->
           print_string "This type constructor expands to type")
        (function () ->
           print_string "but is here used with type")
  | Null_arity_external ->
      print_string "External identifiers must be functions"
@


\subsection{Expressions}

<<type Typecore.error>>=
type error =
    Unbound_value of Longident.t
  | Unbound_constructor of Longident.t
  | Unbound_label of Longident.t
  | Constructor_arity_mismatch of Longident.t * int * int
  | Label_mismatch of Longident.t * (type_expr * type_expr) list
  | Pattern_type_clash of (type_expr * type_expr) list
  | Multiply_bound_variable
  | Orpat_not_closed
  | Expr_type_clash of (type_expr * type_expr) list
  | Apply_non_function of type_expr
  | Label_multiply_defined of Longident.t
  | Label_missing
  | Label_not_mutable of Longident.t
  | Bad_format of string
  | Not_subtype of (type_expr * type_expr) list * (type_expr * type_expr) list
  | Coercion_failure of type_expr * type_expr * (type_expr * type_expr) list
  | Too_many_arguments
@

<<exception Typecore.Error>>=
exception Error of Location.t * error
@

<<signature Typecore.report_error>>=
val report_error: error -> unit
@


<<constant Typecore.report_error>>=
let report_error = function
    Unbound_value lid ->
      print_string "Unbound value "; longident lid
  | Unbound_constructor lid ->
      print_string "Unbound constructor "; longident lid
  | Unbound_label lid ->
      print_string "Unbound label "; longident lid
  | Constructor_arity_mismatch(lid, expected, provided) ->
      open_box 0;
      print_string "The constructor "; longident lid;
      print_space(); print_string "expects "; print_int expected;
      print_string " argument(s),"; print_space();
      print_string "but is here applied to "; print_int provided;
      print_string " argument(s)";
      close_box()
  | Label_mismatch(lid, trace) ->
      unification_error trace
        (function () ->
           print_string "The label "; longident lid;
           print_space(); print_string "belongs to the type")
        (function () ->
           print_string "but is here mixed with labels of type")
  | Pattern_type_clash trace ->
      unification_error trace
        (function () ->
           print_string "This pattern matches values of type")
        (function () ->
           print_string "but is here used to match values of type")
  | Multiply_bound_variable ->
      print_string "This variable is bound several times in this matching"
  | Orpat_not_closed ->
      print_string "A pattern with | must not bind variables"
  | Expr_type_clash trace ->
      unification_error trace
        (function () ->
           print_string "This expression has type")
        (function () ->
           print_string "but is here used with type")
  | Apply_non_function typ ->
      begin match (repr typ).desc with
        Tarrow(_, _) ->
          print_string "This function is applied to too many arguments"
      | _ ->
          print_string
            "This expression is not a function, it cannot be applied"
      end
  | Label_multiply_defined lid ->
      print_string "The label "; longident lid;
      print_string " is defined several times"
  | Label_missing ->
      print_string "Some labels are undefined"
  | Label_not_mutable lid ->
      print_string "The label "; longident lid;
      print_string " is not mutable"
  | Bad_format s ->
      print_string "Bad format `"; print_string s; print_string "'"
  | Not_subtype(tr1, tr2) ->
      reset ();
      List.iter
        (function (t, t') -> mark_loops t; if t != t' then mark_loops t')
        tr1;
      List.iter
        (function (t, t') -> mark_loops t; if t != t' then mark_loops t')
        tr2;
      trace true (fun _ -> print_string "is not a subtype of type") tr1;
      trace false (fun _ -> print_string "is not compatible with type") tr2
  | Coercion_failure (ty, ty', trace) ->
      unification_error trace
        (function () ->
           mark_loops ty; if ty' != ty then mark_loops ty';
           print_string "This expression cannot be coerced to type";
           print_break 1 2;
           type_expansion ty ty';
           print_string ";";
           print_space ();
           print_string "it has type")
        (function () ->
           print_string "but is here used with type")
  | Too_many_arguments ->
      print_string "This function has too many arguments"
@



\section{Dumpers, [[-dxxx]]}


\subsection{Typed structures}

\subsubsection{Primitives}

% primitives
<<signature Primitive.print_description>>=
val print_description: description -> unit
@

<<function Primitive.print_quoted>>=
let print_quoted s = print_char '"'; print_string s; print_char '"'
@

<<function Primitive.print_description>>=
let print_description p =
  print_quoted p.prim_name;
  if not p.prim_alloc then
    (print_space(); print_quoted "noalloc");
  if p.prim_native_name <> "" then
    (print_space(); print_quoted p.prim_native_name);
  if p.prim_native_float then
    (print_space(); print_quoted "float")
@

\subsubsection{Names}

<<signature Printtyp.longident>>=
val longident: Longident.t -> unit
@

<<signature Printtyp.ident>>=
val ident: Ident.t -> unit
@

<<signature Printtyp.path>>=
val path: Path.t -> unit
@






<<constant Printtyp.longident>>=
(* Print a long identifier *)

let rec longident = function
    Lident s -> print_string s
  | Ldot(p, s) -> longident p; print_string "."; print_string s
@

<<function Printtyp.ident>>=
(* Print an identifier *)

let ident id =
  print_string(Ident.name id)
@

<<constant Printtyp.ident_pervasive>>=
(* Print a path *)

let ident_pervasive = Ident.create_persistent "Pervasives"
@

<<constant Printtyp.path>>=
let rec path = function
    Pident id ->
      ident id
  | Pdot(Pident id, s, pos) when Ident.same id ident_pervasive ->
      print_string s
  | Pdot(p, s, pos) ->
      path p; print_string "."; print_string s
@


\subsubsection{Types}

<<signature Printtyp.type_expr>>=
val type_expr: type_expr -> unit
@

<<function Printtyp.type_expr>>=
let type_expr ty =
  typexp false 0 ty
@


<<function Printtyp.typeexp>>=
let rec typexp sch prio0 ty =
  let ty = repr ty in
  try
    List.assq ty !names;
    if (ty.desc = Tvar) && sch && (ty.level <> generic_level)
    then print_string "'_"
    else print_string "'";
    print_string (name_of_type ty)
  with Not_found ->
    let alias = List.memq ty !aliased in
    if alias then begin
      name_of_type ty;
      if prio0 >= 1 then begin open_box 1; print_string "(" end
      else open_box 0
    end;
    let prio = if alias then 0 else prio0 in
    begin match ty.desc with
      Tvar ->
        if (not sch) or ty.level = generic_level
        then print_string "'"
        else print_string "'_";
        print_string(name_of_type ty)
    | Tarrow(ty1, ty2) ->
        if prio >= 2 then begin open_box 1; print_string "(" end
                     else open_box 0;
        typexp sch 2 ty1;
        print_string " ->"; print_space();
        typexp sch 1 ty2;
        if prio >= 2 then print_string ")";
        close_box()
    | Ttuple tyl ->
        if prio >= 3 then begin open_box 1; print_string "(" end
                     else open_box 0;
        typlist sch 3 " *" tyl;
        if prio >= 3 then print_string ")";
        close_box()
    | Tconstr(p, tyl, abbrev) ->
        open_box 0;
        begin match tyl with
          [] -> ()
        | [ty1] ->
            typexp sch 3 ty1; print_space()
        | tyl ->
            open_box 1; print_string "("; typlist sch 0 "," tyl;
            print_string ")"; close_box(); print_space()
        end;
        path p;
        close_box()
(*
| Tfield _ -> typobject sch ty ty (ref None)
| Tnil -> typobject sch ty ty (ref None)
*)
    | _ ->
        fatal_error "Printtyp.typexp"
    end;
    if alias then begin
      print_string " as ";
      print_string "'";
      print_string (name_of_type ty);
      (* if not (opened_object ty) then *)
      remove_name_of_type ty;
      if prio0 >= 1 then print_string ")";
      close_box()
    end
(* ; print_string "["; print_int ty.level; print_string "]" *)

and typlist sch prio sep = function
    [] -> ()
  | [ty] -> typexp sch prio ty
  | ty::tyl ->
      typexp sch prio ty; print_string sep; print_space();
      typlist sch prio sep tyl

and typfields sch rest =
  function
    [] ->
      begin match rest.desc with
        Tvar -> if sch & rest.level <> generic_level then
                  print_string "_";
                print_string ".."
      | Tnil -> ()
      | _    -> fatal_error "typfields (1)"
      end
  | [(s, t)] ->
      print_string s;
      print_string " : ";
      typexp sch 0 t;
      begin match rest.desc with
        Tvar -> print_string ";"; print_space ()
      | Tnil -> ()
      | _    -> fatal_error "typfields (2)"
      end;
      typfields sch rest []
  | (s, t)::l ->
      print_string s;
      print_string " : ";
      typexp sch 0 t;
      print_string ";"; print_space ();
      typfields sch rest l
@


%\subsubsection{Helpers}

<<signature Printtyp.reset>>=
val reset: unit -> unit
@


<<signature Printtyp.mark_loops>>=
val mark_loops: type_expr -> unit
@

<<signature Printtyp.type_scheme>>=
val type_scheme: type_expr -> unit
@


<<signature Printtyp.type_expansion>>=
val type_expansion: type_expr -> type_expr -> unit
@


<<signature Printtyp.trace>>=
val trace: bool -> (unit -> unit) -> (type_expr * type_expr) list -> unit
@

<<signature Printtyp.unification_error>>=
val unification_error:
        (type_expr * type_expr) list -> (unit -> unit) -> (unit -> unit) ->
        unit
@








<<constant Printtyp.names>>=
(* Print a type expression *)

let names = ref ([] : (type_expr * string) list)
@

<<constant Printtyp.name_counter>>=
let name_counter = ref 0
@

<<function Printtyp.reset_names>>=
let reset_names () = names := []; name_counter := 0
@

<<function Printtyp.new_name>>=
let new_name () =
  let name =
    if !name_counter < 26
    then String.make 1 (Char.chr(97 + !name_counter)) 
    else String.make 1 (Char.chr(97 + !name_counter mod 26)) ^
           string_of_int(!name_counter / 26)
  in
    incr name_counter;
    name
@

<<function Printtyp.name_of_type>>=
let name_of_type t =
  try List.assq t !names with Not_found ->
    let name = new_name () in
    names := (t, name) :: !names;
    name
@

<<function Printtyp.list_removeq>>=
let rec list_removeq a =
  function
    [] ->
      []
  | (b, _) as e::l ->
      if a == b then l else e::list_removeq a l
@

<<function Printtyp.remove_name_of_type>>=
let remove_name_of_type t =
  names := list_removeq t !names
@

<<constant Printtyp.visited_objects>>=
let visited_objects = ref ([] : type_expr list)
@

<<constant Printtyp.aliased>>=
let aliased = ref ([] : type_expr list)
@

<<function Printtyp.mark_loops_rec>>=
let rec mark_loops_rec visited ty =
  let ty = repr ty in
  if List.memq ty visited then begin
    if not (List.memq ty !aliased) then
      aliased := ty :: !aliased
  end else
    let visited = ty :: visited in
    match ty.desc with
      Tvar                -> ()
    | Tarrow(ty1, ty2)    ->
        mark_loops_rec visited ty1; mark_loops_rec visited ty2
    | Ttuple tyl          -> List.iter (mark_loops_rec visited) tyl
    | Tconstr(_, tyl, _)  ->
        List.iter (mark_loops_rec visited) tyl
    | Tnil                -> ()
    | Tlink _             -> fatal_error "Printtyp.mark_loops_rec (2)"
@

<<function Printtyp.mark_loops>>=
let mark_loops ty = mark_loops_rec [] ty
@

<<function Printtyp.reset_loop_marks>>=
let reset_loop_marks () =
  visited_objects := []; aliased := []
@

<<function Printtyp.reset>>=
let reset () =
  reset_names (); reset_loop_marks ()
@

<<function Printtyp.constrain>>=
(* Print one type declaration *)

let constrain ty =
  let ty' = unalias ty in
  if ty != ty' then begin
    print_space ();
    open_box 2;
    print_string "constraint ";
    type_sch ty;
    print_string " =";
    print_space();
    type_sch ty';
    close_box()
  end
@


<<function Printtyp.type_expansion>>=
(* Print an unification error *)

let type_expansion t t' =
  if t == t' then
    type_expr t
  else begin
    open_box 2;
    type_expr t;
    print_space (); print_string "="; print_space ();
    type_expr t';
    close_box ()
  end
@

<<function Printtyp.trace>>=
let rec trace fst txt =
  function
    (t1, t1')::(t2, t2')::rem ->
      if not fst then
        print_cut ();
      open_box 0;
      print_string "Type"; print_break 1 2;
      type_expansion t1 t1'; print_space ();
      txt (); print_break 1 2;
      type_expansion t2 t2';
      close_box ();
      trace false txt rem
  | _ ->
      ()
@

<<function Printtyp.unification_error>>=
let unification_error tr txt1 txt2 =
  reset ();
  List.iter
    (function (t, t') -> mark_loops t; if t != t' then mark_loops t')
    tr;
  open_box 0;
  let (t1, t1') = List.hd tr in
  let (t2, t2') = List.hd (List.tl tr) in
  txt1 (); print_break 1 2;
  type_expansion t1 t1'; print_space();
  txt2 (); print_break 1 2;
  type_expansion t2 t2';
  close_box();
  trace false (fun _ -> print_string "is not compatible with type")
        (List.tl (List.tl tr))
@


\subsubsection{Expressions}

<<signature Printtyp.value_description>>=
val value_description: Ident.t -> value_description -> unit
@

<<function Printtyp.value_description>>=
(* Print a value declaration *)

let value_description id decl =
  open_box 2;
  print_string (if decl.val_kind = Val_reg then "val " else "external ");
  ident id; print_string " :"; print_space();
  type_scheme decl.val_type;
  begin match decl.val_kind with
    Val_prim p ->
      print_space(); print_string "= "; Primitive.print_description p
  | _ -> ()
  end;
  close_box()
@

\subsubsection{Type declarations}

<<signature Printtyp.type_declaration>>=
val type_declaration: Ident.t -> type_declaration -> unit
@

<<signature Printtyp.exception_declaration>>=
val exception_declaration: Ident.t -> exception_declaration -> unit
@


<<function Printtyp.type_declaration>>=
let rec type_declaration id decl =
  reset();

  let params = List.map repr decl.type_params in

  aliased := params @ !aliased;
  List.iter mark_loops params;
  List.iter (fun x -> name_of_type x; ()) params;
  begin match decl.type_manifest with
    None    -> ()
  | Some ty -> mark_loops ty
  end;
  begin match decl.type_kind with
    Type_abstract -> ()
  | Type_variant [] -> ()
  | Type_variant cstrs ->
      List.iter (fun (_, args) -> List.iter mark_loops args) cstrs
  | Type_record (lbl1 :: lbls as l) ->
      List.iter (fun (_, _, ty) -> mark_loops ty) l
  | _ -> assert false
  end;

  open_hvbox 2;
  print_string "type ";
  type_expr {desc = Tconstr(Pident id, params, ref Mnil);
             level = generic_level};
  begin match decl.type_manifest with
    None -> ()
  | Some ty ->
      print_string " ="; print_space(); type_expr ty
  end;
  begin match decl.type_kind with
    Type_abstract -> ()
  | Type_variant [] -> ()
      (* A fatal error actually, except when printing type exn... *)
  | Type_variant cstrs ->
      print_string " =";
      List.iter
        (fun cstr -> print_space(); print_string "| "; constructor cstr)
        cstrs
  | Type_record (lbl1 :: lbls as l) ->
      print_string " ="; print_space();
      print_string "{ "; label lbl1;
      List.iter
        (fun lbl -> print_string ";"; print_break 1 2; label lbl)
        lbls;
      print_string " }"
  | _ -> assert false
  end;
  List.iter constrain params;
  close_box()

and constructor (name, args) =
  print_string name;
  match args with
    [] -> ()
  | _  -> print_string " of ";
          open_box 2; typlist false 3 " *" args; close_box()

and label (name, mut, arg) =
  begin match mut with
      Immutable -> ()
    | Mutable -> print_string "mutable "
  end;
  print_string name;
  print_string ": ";
  type_expr arg

@


<<function Printtyp.exception_declaration>>=
(* Print an exception declaration *)

let exception_declaration id decl =
  print_string "exception "; constructor (Ident.name id, decl)
@

\subsubsection{Modules}

<<signature Printtyp.modtype>>=
val modtype: module_type -> unit
@

<<signature Printtyp.signature>>=
val signature: signature -> unit
@

<<signature Printtyp.signature_body>>=
val signature_body: bool -> signature -> unit
@


<<function Printtyp.modtype>>=
let rec modtype = function
    Tmty_ident p ->
      path p
  | Tmty_signature sg ->
      open_hvbox 2;
      print_string "sig"; signature_body true sg; 
      print_break 1 (-2); print_string "end";
      close_box()

and signature_body spc = function
    [] -> ()
  | item :: rem ->
      if spc then print_space();
      let cont =
        match item with
          Tsig_value(id, decl) ->
            value_description id decl; rem
        | Tsig_type(id, decl)  ->
            type_declaration id decl; rem
        | Tsig_exception(id, decl)  ->
            exception_declaration id decl; rem
        | Tsig_module(id, mty)  ->
            open_box 2; print_string "module "; ident id; print_string " :";
            print_space(); modtype mty; close_box(); rem
      in signature_body true cont
@


<<function Printtyp.signature>>=
(* Print a signature body (used when compiling a .mli and printing results
   in interactive use). *)

let signature sg =
  open_vbox 0;
  signature_body false sg;
  close_box()
@

\chapter{Checking}

\section{Interface/Implementation consistency}
%includemod.ml

\section{Partial pattern matches}
% parmatch.ml

<<signature Parmatch.check_partial>>=
val check_partial: 
      Location.t -> (Typedtree.pattern * Typedtree.expression) list -> unit
@

\section{Unused pattern matches}

<<signature Parmatch.check_unused>>=
val check_unused: (Typedtree.pattern * Typedtree.expression) list -> unit
@



\chapter{Typing}

% note that this phase build a global environment,
% and will load .cmi of other files!

% see types.mli, Link

<<signature Btype.newgenty>>=
val newgenty: type_desc -> type_expr
        (* Create a generic type *)
@

<<signature Btype.newgenvar>>=
val newgenvar: unit -> type_expr
        (* Return a fresh generic variable *)
@

<<signature Ctype.newvar>>=
val newvar: unit -> type_expr
        (* Return a fresh variable *)
@

% in ctype.ml before unify

%   1. When unifying two non-abbreviated types, one type is made a link
%      to the other. When unifying an abbreviated type with a
%      non-abbreviated type, the non-abbreviated type is made a link to
%      the other one. When unifying to abbreviated types, these two
%      types are kept distincts, but they are made to (temporally)
%      expand to the same type.
%   2. Abbreviations with at least one parameter are systematically
%      expanded. The overhead does not seem to high, and that way
%      abbreviations where some parameters does not appear in the
%      expansion, such as ['a t = int], are correctly handled. In
%      particular, for this example, unifying ['a t] with ['b t] keeps
%      ['a] and ['b] distincts. (Is it really important ?)
%   3. Unifying an abbreviation ['a t = 'a] with ['a] should not yield
%      ['a t as 'a]. Indeed, the type variable would otherwise be lost.
%      This problem occurs for abbreviations expanding to a type
%      variable, but also to many other constrained abbreviations (for
%      instance, [(< x : 'a > -> unit) t = <x : 'a>]). The solution is
%      that, if an abbreviation is unified with some subpart of its
%      parameters, then the parameter actually does not get
%      abbreviated.  It would be possible to check whether some
%      information is indeed lost, but it probably does not worth it.


<<constant Typecore.type_constant>>=
(* Typing of constants *)

let type_constant = function
    Const_int _ -> instance Predef.type_int
  | Const_char _ -> instance Predef.type_char
  | Const_string _ -> instance Predef.type_string
  | Const_float _ -> instance Predef.type_float
@

\section{Resolving names}
%Naming? A special section? nast

% resolving? fully qualifying every idents?

\subsection{[[Ident]]}

<<signature type t>>=
type t
@
<<type Ident.t>>=
type t = { stamp: int; name: string; mutable global: bool }
@

<<signature Ident.create>>=
val create: string -> t
@

<<signature Ident.name>>=
val name: t -> string
@


\subsection{[[Path]]}

<<type Path.t>>=
(* Access paths *)

type t =
    Pident of Ident.t
  | Pdot of t * string * int
@
% int?

\section{Types}

%todo? put earlier? in header of the chapter?
%todo? the code of the dumper helps to understand things?

<<type Types.type_expr>>=
type type_expr =
  { mutable desc: type_desc; 
    mutable level: int }
@
% level??
% todo: look previous implem? in caml light? or just before objects?

<<type Types.type_desc>>=
and type_desc =
    Tvar
  | Tarrow of type_expr * type_expr
  | Ttuple of type_expr list
  | Tconstr of Path.t * type_expr list * abbrev_memo ref

  | Tnil
  | Tlink of type_expr
@
%Tlink? Tnil? Tvar of nothing?
% ref? use globals?

<<type Types.abbrev_memo>>=
and abbrev_memo =
    Mnil
  | Mcons of Path.t * type_expr * abbrev_memo
  | Mlink of abbrev_memo ref
@
% ???

\section{The typed AST}

% could factorize?

<<type Typedtree.pattern>>=
type pattern =
  { pat_desc: pattern_desc;
    pat_loc: Location.t;
    pat_type: type_expr }
@
% note that pat_type now!

<<type Typedtree.pattern_desc>>=
and pattern_desc =
    Tpat_any
  | Tpat_var of Ident.t
  | Tpat_alias of pattern * Ident.t
  | Tpat_constant of constant
  | Tpat_tuple of pattern list
  | Tpat_construct of constructor_description * pattern list
  | Tpat_record of (label_description * pattern) list
  | Tpat_or of pattern * pattern
@

<<type Typedtree.expression>>=
type expression =
  { exp_desc: expression_desc;
    exp_loc: Location.t;
    exp_type: type_expr;
    exp_env: Env.t }
@
% env?

<<type Typedtree.expression_desc>>=
and expression_desc =
    Texp_ident of Path.t * value_description
  | Texp_constant of constant
  | Texp_let of rec_flag * (pattern * expression) list * expression
  | Texp_function of (pattern * expression) list
  | Texp_apply of expression * expression list
  | Texp_match of expression * (pattern * expression) list
  | Texp_try of expression * (pattern * expression) list
  | Texp_tuple of expression list
  | Texp_construct of constructor_description * expression list
  | Texp_record of (label_description * expression) list
  | Texp_field of expression * label_description
  | Texp_setfield of expression * label_description * expression
  | Texp_array of expression list
  | Texp_ifthenelse of expression * expression * expression option
  | Texp_sequence of expression * expression
  | Texp_while of expression * expression
  | Texp_for of
      Ident.t * expression * expression * direction_flag * expression
  | Texp_when of expression * expression
@

<<type Typedtree.module_expr>>=
type module_expr =
  { mod_desc: module_expr_desc;
    mod_loc: Location.t;
    mod_type: module_type;
    mod_env: Env.t }
@

<<type Typedtree.module_expr_desc>>=
and module_expr_desc =
    Tmod_ident of Path.t
  | Tmod_structure of structure
  | Tmod_constraint of module_expr * module_type * module_coercion
@

<<type Typedtree.structure>>=
and structure = structure_item list
@

<<type Typedtree.structure_item>>=
and structure_item =
    Tstr_eval of expression
  | Tstr_value of rec_flag * (pattern * expression) list
  | Tstr_primitive of Ident.t * value_description
  | Tstr_type of (Ident.t * type_declaration) list
  | Tstr_exception of Ident.t * exception_declaration
  | Tstr_module of Ident.t * module_expr
  | Tstr_open of Path.t
@

<<type Typedtree.module_coercion>>=
and module_coercion =
    Tcoerce_none
  | Tcoerce_structure of (int * module_coercion) list
  | Tcoerce_primitive of Primitive.description
@


\section{Builtin types}
% and exceptions

%(* Predefined type constructors (with special typing rules in typecore) *)

<<signatures Predef.type_xxx>>=
val type_int: type_expr
val type_char: type_expr
val type_string: type_expr
val type_float: type_expr
val type_bool: type_expr
val type_unit: type_expr

val type_list: type_expr -> type_expr
val type_array: type_expr -> type_expr
@


<<signatures Predef.path_xxx>>=
val path_int: Path.t
val path_char: Path.t
val path_string: Path.t
val path_float: Path.t
val path_bool: Path.t
val path_unit: Path.t

val path_list: Path.t
val path_array: Path.t
@


<<constants Predef.ident_xxx>>=
let ident_int = Ident.create "int"
and ident_char = Ident.create "char"
and ident_string = Ident.create "string"
and ident_float = Ident.create "float"
and ident_bool = Ident.create "bool"
and ident_unit = Ident.create "unit"

and ident_exn = Ident.create "exn"

and ident_array = Ident.create "array"
and ident_list = Ident.create "list"
@
% no module
% subtle: don't change the order here, otherwise you may
%  have problem in otherlibs/unix, and get error like
%  "type exn is used where expect exn"

<<constants Predef.path_xxx>>=
let path_int = Pident ident_int
and path_char = Pident ident_char
and path_string = Pident ident_string
and path_float = Pident ident_float
and path_bool = Pident ident_bool
and path_unit = Pident ident_unit

and path_list = Pident ident_list
and path_array = Pident ident_array
@

<<constants Predef.type_xxx>>=
let type_int     = newgenty (Tconstr(path_int, [], ref Mnil))
and type_char    = newgenty (Tconstr(path_char, [], ref Mnil))
and type_string  = newgenty (Tconstr(path_string, [], ref Mnil))
and type_float   = newgenty (Tconstr(path_float, [], ref Mnil))
and type_bool    = newgenty (Tconstr(path_bool, [], ref Mnil))
and type_unit    = newgenty (Tconstr(path_unit, [], ref Mnil))

and type_array t = newgenty (Tconstr(path_array, [t], ref Mnil))
and type_list t  = newgenty (Tconstr(path_list, [t], ref Mnil))
@

\section{Builtin values}
% different from primitives
% different from pervasives

<<signature Predef.build_initial_env>>=
(* To build the initial environment. Since there is a nasty mutual
   recursion between predef and env, we break it by parameterizing
   over Env.t, Env.add_type and Env.add_exception. *)

val build_initial_env:
  (Ident.t -> Types.type_declaration -> 'a -> 'a) ->
  (Ident.t -> Types.exception_declaration -> 'a -> 'a) ->
  'a -> 'a
@



% typed tree?
<<function Predef.build_initial_env>>=
let build_initial_env add_type add_exception empty_env =
  let decl_abstr =
    {type_params = [];
     type_arity = 0;
     type_kind = Type_abstract;
     type_manifest = None}
  and decl_bool =
    {type_params = [];
     type_arity = 0;
     type_kind = Type_variant["false",[]; "true",[]];
     type_manifest = None}
  and decl_unit =
    {type_params = []; 
     type_arity = 0;
     type_kind = Type_variant["()",[]];
     type_manifest = None}
  and decl_exn =
    {type_params = [];
     type_arity = 0;
     type_kind = Type_variant [];
     type_manifest = None}
  and decl_array =
    let tvar = newgenvar() in
    {type_params = [tvar];
     type_arity = 1;
     type_kind = Type_abstract;
     type_manifest = None}
  and decl_list =
    let tvar = newgenvar() in
    {type_params = [tvar];
     type_arity = 1;
     type_kind = Type_variant["[]", []; "::", [tvar; type_list tvar]];
     type_manifest = None}
  and decl_format =
    {type_params = [newgenvar(); newgenvar(); newgenvar()];
     type_arity = 3;
     type_kind = Type_abstract;
     type_manifest = None} in

  <<[[Predef.build_initial_env()]] adding exceptions>>

  add_type ident_format decl_format (
  add_type ident_list decl_list (
  add_type ident_array decl_array (
  add_type ident_exn decl_exn (
  add_type ident_unit decl_unit (
  add_type ident_bool decl_bool (
  add_type ident_float decl_abstr (
  add_type ident_string decl_abstr (
  add_type ident_char decl_abstr (
  add_type ident_int decl_abstr (
  empty_env
  )))))))))))))))))))
@


\section{Primitives}

<<type Primitive.description>>=
(* Description of primitive functions *)

type description =
  { prim_name: string;         (* Name of primitive  or C function *)
    prim_arity: int;           (* Number of arguments *)
    prim_alloc: bool;          (* Does it allocates or raise? *)
    prim_native_name: string;  (* Name of C function for the nat. code gen. *)
    prim_native_float: bool }  (* Does the above operate on unboxed floats? *)
@

<<signature Primitive.parse_declaration>>=
val parse_declaration: int -> string list -> description
@

<<function Primitive.parse_declaration>>=
let parse_declaration arity decl =
  match decl with
    name :: "noalloc" :: name2 :: "float" :: _ ->
      {prim_name = name; prim_arity = arity; prim_alloc = false;
       prim_native_name = name2; prim_native_float = true}
  | name :: "noalloc" :: name2 :: _ ->
      {prim_name = name; prim_arity = arity; prim_alloc = false;
       prim_native_name = name2; prim_native_float = false}
  | name :: name2 :: "float" :: _ ->
      {prim_name = name; prim_arity = arity; prim_alloc = true;
       prim_native_name = name2; prim_native_float = true}
  | name :: "noalloc" :: _ ->
      {prim_name = name; prim_arity = arity; prim_alloc = false;
       prim_native_name = ""; prim_native_float = false}
  | name :: name2 :: _ ->
      {prim_name = name; prim_arity = arity; prim_alloc = true;
       prim_native_name = name2; prim_native_float = false}
  | name :: _ ->
      {prim_name = name; prim_arity = arity; prim_alloc = true;
       prim_native_name = ""; prim_native_float = false}
  | [] ->
      fatal_error "Primitive.parse_declaration"
@


\section{The (typed, named) symbol table, [[Env.t]]}
% The typed, resolved symbol table

<<signature type Env.t>>=
type t
@

<<type Env.t>>=
type t = {
  values     : (Path.t * Types.value_description)   Ident.tbl;
  constrs    : Types.constructor_description        Ident.tbl;
  labels     : Types.label_description              Ident.tbl;
  types      : (Path.t * Types.type_declaration)    Ident.tbl;
  modules    : (Path.t * Types.module_type)         Ident.tbl;
  components : (Path.t * module_components)   Ident.tbl;
  summary: summary
}
@
% no Path for constructor and record?
% components??

<<signature Env.empty>>=
val empty: t
@

<<constant Env.empty>>=
let empty = {
  values = Ident.empty; 
  constrs = Ident.empty;
  labels = Ident.empty; 
  types = Ident.empty;
  modules = Ident.empty;
  components = Ident.empty; 
  summary = Env_empty }
@



<<signature Env.initial>>=
val initial: t
@

% by Path
<<signature Env.find_value>>=
val find_value: Path.t -> t -> Types.value_description
@

<<signature Env.find_type>>=
val find_type: Path.t -> t -> Types.type_declaration
@

% by Longident

<<signature Env.lookup_value>>=
val lookup_value: Longident.t -> t -> Path.t * Types.value_description
@

<<signature Env.lookup_constructor>>=
val lookup_constructor: Longident.t -> t -> Types.constructor_description
@

<<signature Env.lookup_label>>=
val lookup_label: Longident.t -> t -> Types.label_description
@

<<signature Env.lookup_type>>=
val lookup_type: Longident.t -> t -> Path.t * Types.type_declaration
@

<<signature Env.lookup_module>>=
val lookup_module: Longident.t -> t -> Path.t * Types.module_type
@



\section{Substitutions}

<<type Subst.t>>=
type t = 
  { types: (Ident.t, Path.t) Tbl.t;
    modules: (Ident.t, Path.t) Tbl.t;
    modtypes: (Ident.t, Types.module_type) Tbl.t }
@


\section{Unification}

<<exception Ctype.Unify>>=
(**** Errors ****)

exception Unify of (type_expr * type_expr) list
@

\section{Subtypes}

<<exception Ctype.Subtype>>=
exception Subtype of
        (type_expr * type_expr) list * (type_expr * type_expr) list
@

\section{Typing}

% string -> Ident.t
% Parsetree.xxx -> Types.xxx or Typedtree.xxx

\subsection{Types}

<<signature Typetexp.transl_simple_type>>=
(* Typechecking of type expressions for the core language *)

val transl_simple_type:
        Env.t -> bool -> Parsetree.core_type -> Types.type_expr
@
% bool?

\subsection{Declarations}

<<signature Typedecl.transl_type_decl>>=
val transl_type_decl:
        Env.t -> (string * Parsetree.type_declaration) list ->
                              (Ident.t * Types.type_declaration) list * Env.t
@

<<signature Typedecl.transl_exception>>=
val transl_exception:
        Env.t -> Parsetree.exception_declaration -> Types.exception_declaration
@

<<signature Typedecl.transl_value_decl>>=
val transl_value_decl:
        Env.t -> Parsetree.value_description -> Types.value_description
@

\subsection{Expressions}


<<signature Typecore.type_binding>>=
val type_binding:
        Env.t -> rec_flag ->
          (Parsetree.pattern * Parsetree.expression) list -> 
          (Typedtree.pattern * Typedtree.expression) list * Env.t
@

<<signature Typecore.type_expression>>=
val type_expression:
        Env.t -> Parsetree.expression -> Typedtree.expression
@

<<signature Typecore.type_pattern_list>>=
val type_pattern_list:
        Env.t -> Parsetree.pattern list -> Typedtree.pattern list * Env.t
@

<<signature Typecore.type_expect>>=
val type_expect:
        Env.t -> Parsetree.expression -> type_expr -> Typedtree.expression
@

<<signature Typecore.type_exp>>=
val type_exp:
        Env.t -> Parsetree.expression -> Typedtree.expression
@



<<function Typecore.type_exp>>=
let rec type_exp env sexp =
  match sexp.pexp_desc with
    Pexp_ident lid ->
      begin try
        let (path, desc) = Env.lookup_value lid env in
        { exp_desc =
            begin match (desc.val_kind, lid) with
            | _ ->
                Texp_ident(path, desc)
            end;
          exp_loc = sexp.pexp_loc;
          exp_type = instance desc.val_type;
          exp_env = env }
      with Not_found ->
        raise(Error(sexp.pexp_loc, Unbound_value lid))
      end
  | Pexp_constant cst ->
      { exp_desc = Texp_constant cst;
        exp_loc = sexp.pexp_loc;
        exp_type = type_constant cst;
        exp_env = env }
  | Pexp_let(rec_flag, spat_sexp_list, sbody) ->
      let (pat_exp_list, new_env) = type_let env rec_flag spat_sexp_list in
      let body = type_exp new_env sbody in
      { exp_desc = Texp_let(rec_flag, pat_exp_list, body);
        exp_loc = sexp.pexp_loc;
        exp_type = body.exp_type;
        exp_env = env }
  | Pexp_function caselist ->
      let ty_arg = newvar() and ty_res = newvar() in
      let cases = type_cases env ty_arg ty_res caselist in
      Parmatch.check_unused cases;
      Parmatch.check_partial sexp.pexp_loc cases;
      { exp_desc = Texp_function cases;
        exp_loc = sexp.pexp_loc;
        exp_type = newty (Tarrow(ty_arg, ty_res));
        exp_env = env }
  | Pexp_apply(sfunct, sargs) ->
      let funct = type_exp env sfunct in
      let rec type_args ty_fun = function
        [] ->
          ([], ty_fun)
      | sarg1 :: sargl ->
          let (ty1, ty2) =
            try
              filter_arrow env ty_fun
            with Unify _ ->
              raise(Error(sfunct.pexp_loc,
                          Apply_non_function funct.exp_type)) in
          let arg1 = type_expect env sarg1 ty1 in
          let (argl, ty_res) = type_args ty2 sargl in
          (arg1 :: argl, ty_res) in
      let (args, ty_res) = type_args funct.exp_type sargs in
      { exp_desc = Texp_apply(funct, args);
        exp_loc = sexp.pexp_loc;
        exp_type = ty_res;
        exp_env = env }
  | Pexp_match(sarg, caselist) ->
      let arg = type_exp env sarg in
      let ty_res = newvar() in
      let cases = type_cases env arg.exp_type ty_res caselist in
      Parmatch.check_unused cases;
      Parmatch.check_partial sexp.pexp_loc cases;
      { exp_desc = Texp_match(arg, cases);
        exp_loc = sexp.pexp_loc;
        exp_type = ty_res;
        exp_env = env }
  | Pexp_try(sbody, caselist) ->
      let body = type_exp env sbody in
      let cases =
        type_cases env (instance Predef.type_exn) body.exp_type caselist in
      Parmatch.check_unused cases;
      { exp_desc = Texp_try(body, cases);
        exp_loc = sexp.pexp_loc;
        exp_type = body.exp_type;
        exp_env = env }
  | Pexp_tuple sexpl ->
      let expl = List.map (type_exp env) sexpl in
      { exp_desc = Texp_tuple expl;
        exp_loc = sexp.pexp_loc;
        exp_type = newty (Ttuple(List.map (fun exp -> exp.exp_type) expl));
        exp_env = env }
  | Pexp_construct(lid, sarg, explicit_arity) ->
      let constr =
        try
          Env.lookup_constructor lid env
        with Not_found ->
          raise(Error(sexp.pexp_loc, Unbound_constructor lid)) in
      let sargs =
        match sarg with
          None -> []
        | Some {pexp_desc = Pexp_tuple sel} when explicit_arity -> sel
        | Some {pexp_desc = Pexp_tuple sel} when constr.cstr_arity > 1 -> sel
        | Some se -> [se] in
      if List.length sargs <> constr.cstr_arity then
        raise(Error(sexp.pexp_loc, Constructor_arity_mismatch(lid,
                                       constr.cstr_arity, List.length sargs)));
      let (ty_args, ty_res) = instance_constructor constr in
      let args = List.map2 (type_expect env) sargs ty_args in
      { exp_desc = Texp_construct(constr, args);
        exp_loc = sexp.pexp_loc;
        exp_type = ty_res;
        exp_env = env }
  | Pexp_record lid_sexp_list ->
      let ty = newvar() in
      let num_fields = ref 0 in
      let type_label_exp (lid, sarg) =
        let label =
          try
            Env.lookup_label lid env
          with Not_found ->
            raise(Error(sexp.pexp_loc, Unbound_label lid)) in
        let (ty_arg, ty_res) = instance_label label in
        begin try
          unify env ty_res ty
        with Unify trace ->
          raise(Error(sexp.pexp_loc, Label_mismatch(lid, trace)))
        end;
        let arg = type_expect env sarg ty_arg in
        num_fields := Array.length label.lbl_all;
        (label, arg) in
      let lbl_exp_list = List.map type_label_exp lid_sexp_list in
      let rec check_duplicates = function
        [] -> ()
      | (lid, sarg) :: remainder ->
          if List.mem_assoc lid remainder
          then raise(Error(sexp.pexp_loc, Label_multiply_defined lid))
          else check_duplicates remainder in
      check_duplicates lid_sexp_list;
      if List.length lid_sexp_list <> !num_fields then
        raise(Error(sexp.pexp_loc, Label_missing));
      { exp_desc = Texp_record lbl_exp_list;
        exp_loc = sexp.pexp_loc;
        exp_type = ty;
        exp_env = env }
  | Pexp_field(sarg, lid) ->
      let arg = type_exp env sarg in
      let label =
        try
          Env.lookup_label lid env
        with Not_found ->
          raise(Error(sexp.pexp_loc, Unbound_label lid)) in
      let (ty_arg, ty_res) = instance_label label in
      unify_exp env arg ty_res;
      { exp_desc = Texp_field(arg, label);
        exp_loc = sexp.pexp_loc;
        exp_type = ty_arg;
        exp_env = env }
  | Pexp_setfield(srecord, lid, snewval) ->
      let record = type_exp env srecord in
      let label =
        try
          Env.lookup_label lid env
        with Not_found ->
          raise(Error(sexp.pexp_loc, Unbound_label lid)) in
      if label.lbl_mut = Immutable then
        raise(Error(sexp.pexp_loc, Label_not_mutable lid));
      let (ty_arg, ty_res) = instance_label label in
      unify_exp env record ty_res;
      let newval = type_expect env snewval ty_arg in
      { exp_desc = Texp_setfield(record, label, newval);
        exp_loc = sexp.pexp_loc;
        exp_type = instance Predef.type_unit;
        exp_env = env }
  | Pexp_array(sargl) ->
      let ty = newvar() in
      let argl = List.map (fun sarg -> type_expect env sarg ty) sargl in
      { exp_desc = Texp_array argl;
        exp_loc = sexp.pexp_loc;
        exp_type = instance (Predef.type_array ty);
        exp_env = env }
  | Pexp_ifthenelse(scond, sifso, sifnot) ->
      let cond = type_expect env scond (instance Predef.type_bool) in
      begin match sifnot with
        None ->
          let ifso = type_expect env sifso (instance Predef.type_unit) in
          { exp_desc = Texp_ifthenelse(cond, ifso, None);
            exp_loc = sexp.pexp_loc;
            exp_type = instance Predef.type_unit;
            exp_env = env }
      | Some sifnot ->
          let ifso = type_exp env sifso in
          let ifnot = type_expect env sifnot ifso.exp_type in
          { exp_desc = Texp_ifthenelse(cond, ifso, Some ifnot);
            exp_loc = sexp.pexp_loc;
            exp_type = ifso.exp_type;
            exp_env = env }
      end
  | Pexp_sequence(sexp1, sexp2) ->
      let exp1 = type_statement env sexp1 in
      let exp2 = type_exp env sexp2 in
      { exp_desc = Texp_sequence(exp1, exp2);
        exp_loc = sexp.pexp_loc;
        exp_type = exp2.exp_type;
        exp_env = env }
  | Pexp_while(scond, sbody) ->
      let cond = type_expect env scond (instance Predef.type_bool) in
      let body = type_statement env sbody in
      { exp_desc = Texp_while(cond, body);
        exp_loc = sexp.pexp_loc;
        exp_type = instance Predef.type_unit;
        exp_env = env }
  | Pexp_for(param, slow, shigh, dir, sbody) ->
      let low = type_expect env slow (instance Predef.type_int) in
      let high = type_expect env shigh (instance Predef.type_int) in
      let (id, new_env) =
        Env.enter_value param {val_type = instance Predef.type_int;
                                val_kind = Val_reg} env in
      let body = type_statement new_env sbody in
      { exp_desc = Texp_for(id, low, high, dir, body);
        exp_loc = sexp.pexp_loc;
        exp_type = instance Predef.type_unit;
        exp_env = env }
  | Pexp_constraint(sarg, sty, sty') ->
      let (arg, ty') =
        match (sty, sty') with
          (None, None) ->               (* Case actually unused *)
            let arg = type_exp env sarg in
            (arg, arg.exp_type)
        | (Some sty, None) ->
            let ty = Typetexp.transl_simple_type env false sty in
            (type_expect env sarg ty, ty)
        | (None, Some sty') ->
            let (ty', force) =
              Typetexp.transl_simple_type_delayed env sty'
            in
            let ty = enlarge_type env ty' in
            force ();
            let arg = type_exp env sarg in
            begin try Ctype.unify env arg.exp_type ty with Unify trace ->
              raise(Error(sarg.pexp_loc,
                    Coercion_failure(ty', full_expand env ty', trace)))
            end;
            (arg, ty')
        | (Some sty, Some sty') ->
            let (ty, force) =
              Typetexp.transl_simple_type_delayed env sty
            and (ty', force') =
              Typetexp.transl_simple_type_delayed env sty'
            in
            begin try
              let force'' = subtype env ty ty' in
              force (); force' (); force'' ()
            with Subtype (tr1, tr2) ->
              raise(Error(sexp.pexp_loc, Not_subtype(tr1, tr2)))
            end;
            (type_expect env sarg ty, ty')
      in
      { exp_desc = arg.exp_desc;
        exp_loc = arg.exp_loc;
        exp_type = ty';
        exp_env = env }
  | Pexp_when(scond, sbody) ->
      let cond = type_expect env scond (instance Predef.type_bool) in
      let body = type_exp env sbody in
      { exp_desc = Texp_when(cond, body);
        exp_loc = sexp.pexp_loc;
        exp_type = body.exp_type;
        exp_env = env }

(* Typing of an expression with an expected type.
   Some constructs are treated specially to provide better error messages. *)

and type_expect env sexp ty_expected =
  match sexp.pexp_desc with
    Pexp_constant(Const_string s as cst) ->
      let exp =
        { exp_desc = Texp_constant cst;
          exp_loc = sexp.pexp_loc;
          exp_type =
            (* Terrible hack for format strings *)
            begin match (Ctype.repr ty_expected).desc with
              Tconstr(path, _, _) when Path.same path Predef.path_format ->
                type_format sexp.pexp_loc s
            | _ -> instance Predef.type_string
            end;
          exp_env = env } in
      unify_exp env exp ty_expected;
      exp
  | Pexp_let(rec_flag, spat_sexp_list, sbody) ->
      let (pat_exp_list, new_env) = type_let env rec_flag spat_sexp_list in
      let body = type_expect new_env sbody ty_expected in
      { exp_desc = Texp_let(rec_flag, pat_exp_list, body);
        exp_loc = sexp.pexp_loc;
        exp_type = body.exp_type;
        exp_env = env }
  | Pexp_sequence(sexp1, sexp2) ->
      let exp1 = type_statement env sexp1 in
      let exp2 = type_expect env sexp2 ty_expected in
      { exp_desc = Texp_sequence(exp1, exp2);
        exp_loc = sexp.pexp_loc;
        exp_type = exp2.exp_type;
        exp_env = env }
  | _ ->
      let exp = type_exp env sexp in
      unify_exp env exp ty_expected;
      exp

(* Typing of statements (expressions whose values are discarded) *)

and type_statement env sexp =
    let exp = type_exp env sexp in
    match (Ctype.repr exp.exp_type).desc with
      Tarrow(_, _) ->
        Location.print_warning sexp.pexp_loc
          "this function application is partial,\n\
           maybe some arguments are missing.";
        exp
    | _ -> exp

(* Typing of match cases *)

and type_cases env ty_arg ty_res caselist =
  List.map
    (fun (spat, sexp) ->
      let (pat, ext_env) = type_pattern env spat in
      unify_pat env pat ty_arg;
      let exp = type_expect ext_env sexp ty_res in
      (pat, exp))
    caselist

(* Typing of let bindings *)

and type_let env rec_flag spat_sexp_list =
  begin_def();
  let (pat_list, new_env) =
    type_pattern_list env (List.map (fun (spat, sexp) -> spat) spat_sexp_list)
  in
  let exp_env =
    match rec_flag with Nonrecursive -> env | Recursive -> new_env in
  let exp_list =
    List.map2
      (fun (spat, sexp) pat -> type_expect exp_env sexp pat.pat_type)
      spat_sexp_list pat_list in
  List.iter2
    (fun pat exp -> Parmatch.check_partial pat.pat_loc [pat, exp])
    pat_list exp_list;
  end_def();
  List.iter2
    (fun pat exp ->
       if not (is_nonexpansive exp) then
         iter_pattern (fun pat ->make_nongen pat.pat_type) pat)
    pat_list exp_list;
  List.iter
    (fun pat -> iter_pattern (fun pat -> generalize pat.pat_type) pat)
    pat_list;
  (List.combine pat_list exp_list, new_env)
@

\subsection{Modules}

<<signature Typemod.transl_signature>>=
val transl_signature:
        Env.t -> Parsetree.signature -> Types.signature
@

<<signature Typemod.type_structure>>=
val type_structure:
        Env.t -> Parsetree.structure -> 
          Typedtree.structure * Types.signature * Env.t
@


\section{Advanced typing issues}

% those are the reasons the typechecker got so complicated?

\subsection{References}
% _a

\subsection{Exceptions}

% builtin exceptions

<<signatures Predef.type_xxx>>=
val type_exn: type_expr
@

<<signatures Predef.path_xxx>>=
val path_exn: Path.t
@

<<constants Predef.path_xxx>>=
and path_exn = Pident ident_exn
@

<<constants Predef.type_xxx>>=
and type_exn     = newgenty (Tconstr(path_exn, [], ref Mnil))
@


<<signature Predef.path_match_failure>>=
val path_match_failure: Path.t
@
% why a path for this one?
<<constant Predef.path_match_failure>>=
let path_match_failure = Pident ident_match_failure
@

<<constants Predef.ident_exn_xxx>>=
let ident_match_failure = Ident.create "Match_failure"
and ident_out_of_memory = Ident.create "Out_of_memory"
and ident_invalid_argument = Ident.create "Invalid_argument"
and ident_failure = Ident.create "Failure"
and ident_not_found = Ident.create "Not_found"
and ident_sys_error = Ident.create "Sys_error"
and ident_end_of_file = Ident.create "End_of_file"
and ident_division_by_zero = Ident.create "Division_by_zero"
and ident_stack_overflow = Ident.create "Stack_overflow"
@


<<[[Predef.build_initial_env()]] adding exceptions>>=
add_exception ident_match_failure
                       [newgenty (Ttuple[type_string; type_int; type_int])] (
add_exception ident_out_of_memory [] (
add_exception ident_stack_overflow [] (
add_exception ident_invalid_argument [type_string] (
add_exception ident_failure [type_string] (
add_exception ident_not_found [] (
add_exception ident_sys_error [type_string] (
add_exception ident_end_of_file [] (
add_exception ident_division_by_zero [] (
@

<<signature Predef.builtin_values>>=
(* To initialize linker tables *)

val builtin_values: (string * Ident.t) list
@

<<constant Predef.builtin_values>>=
let builtin_values =
  List.map (fun id -> Ident.make_global id; (Ident.name id, id))
      [ident_match_failure; ident_out_of_memory; ident_stack_overflow;
       ident_invalid_argument;
       ident_failure; ident_not_found; ident_sys_error; ident_end_of_file;
       ident_division_by_zero]
@

\subsection{Abstract types}

\subsection{Type aliases}

\subsection{Printf}

<<constants Predef.ident_xxx>>=
and ident_format = Ident.create "format"
@

<<signatures Predef.path_xxx>>=
val path_format: Path.t
@

<<constants Predef.path_xxx>>=
and path_format = Pident ident_format
@

% special format typechecking

<<function Typecore.type_format>>=
(* Typing of printf formats *)

let type_format loc fmt =
  let len = String.length fmt in
  let ty_input = newvar()
  and ty_result = newvar() in
  let rec skip_args j =
    if j >= len then j else
      match fmt.[j] with
        '0' .. '9' | ' ' | '.' | '-' -> skip_args (j+1)
      | _ -> j in
  let rec scan_format i =
    if i >= len then ty_result else
    match fmt.[i] with
      '%' ->
        let j = skip_args(i+1) in
        begin match String.unsafe_get fmt j with
        (* We're using unsafe_get here so that if j = String.length fmt,
           we'll fall in the catch-all case of the match *)
          '%' ->
            scan_format (j+1)
        | 's' ->
            newty (Tarrow(instance Predef.type_string, scan_format (j+1)))
        | 'c' ->
            newty (Tarrow(instance Predef.type_char, scan_format (j+1)))
        | 'd' | 'o' | 'x' | 'X' | 'u' ->
            newty (Tarrow(instance Predef.type_int, scan_format (j+1)))
        | 'f' | 'e' | 'E' | 'g' | 'G' ->
            newty (Tarrow(instance Predef.type_float, scan_format (j+1)))
        | 'b' ->
            newty (Tarrow(instance Predef.type_bool, scan_format (j+1)))
        | 'a' ->
            let ty_arg = newvar() in
            newty (Tarrow (newty (Tarrow(ty_input,
                                         newty (Tarrow (ty_arg, ty_result)))),
                           newty (Tarrow (ty_arg, scan_format (j+1)))))
        | 't' ->
            newty (Tarrow(newty (Tarrow(ty_input, ty_result)),
                          scan_format (j+1)))
        | c ->
            raise(Error(loc, Bad_format(String.sub fmt i (j-i+1))))
        end
    | _ -> scan_format (i+1) in
  newty
    (Tconstr(Predef.path_format, [scan_format 0; ty_input; ty_result], ref Mnil))
@




\section{[[./typing/]]}
typing of ocaml 1.07

\subsection*{[[./typing/ident.mli]]}


<<signature Ident.create_persistent>>=
val create_persistent: string -> t
@


<<signature Ident.unique_name>>=
val unique_name: t -> string
@

<<signature Ident.persistent>>=
val persistent: t -> bool
@

<<signature Ident.equal>>=
val equal: t -> t -> bool
        (* Compare identifiers by name. *)      
@

<<signature Ident.same>>=
val same: t -> t -> bool
        (* Compare identifiers by binding location.
           Two identifiers are the same either if they are both
           non-persistent and have been created by the same call to
           [new], or if they are both persistent and have the same
           name. *)
@

<<signature Ident.hide>>=
val hide: t -> t
        (* Return an identifier with same name as the given identifier,
           but stamp different from any stamp returns by new.
           When put in a 'a tbl, this identifier can only be looked
           up by name. *)
@

<<signature Ident.make_global>>=
val make_global: t -> unit
@

<<signature Ident.global>>=
val global: t -> bool
@

<<signature Ident.binding_time>>=
val binding_time: t -> int
@

<<signature Ident.current_time>>=
val current_time: unit -> int
@

<<signature Ident.print>>=
val print: t -> unit
@





<<signature Ident.empty>>=
val empty: 'a tbl
@

<<signature Ident.add>>=
val add: t -> 'a -> 'a tbl -> 'a tbl
@

<<signature Ident.find_same>>=
val find_same: t -> 'a tbl -> 'a
@

<<signature Ident.find_name>>=
val find_name: string -> 'a tbl -> 'a
@

<<signature Ident.iter>>=
val iter: (t -> 'a -> unit) -> 'a tbl -> unit
@

<<signature Ident.print_tbl>>=
val print_tbl: ('a -> unit) -> 'a tbl -> unit
@


%-------------------------------------------------------------

<<./typing/ident.mli>>=
<<copyright header>>

(* Identifiers (unique names) *)

<<signature type t>>

<<signature Ident.create>>
<<signature Ident.create_persistent>>
<<signature Ident.name>>
<<signature Ident.unique_name>>
<<signature Ident.persistent>>
<<signature Ident.equal>>
<<signature Ident.same>>
<<signature Ident.hide>>

<<signature Ident.make_global>>
<<signature Ident.global>>

<<signature Ident.binding_time>>
<<signature Ident.current_time>>

<<signature Ident.print>>

type 'a tbl
        (* Association tables from identifiers to type 'a. *)

<<signature Ident.empty>>
<<signature Ident.add>>
<<signature Ident.find_same>>
<<signature Ident.find_name>>

<<signature Ident.iter>>
<<signature Ident.print_tbl>>
@


\subsection*{[[./typing/ident.ml]]}


<<constant Ident.currentstamp>>=
(* A stamp of 0 denotes a persistent identifier *)

let currentstamp = ref 0
@

<<function Ident.create>>=
let create s =
  incr currentstamp;
  { name = s; stamp = !currentstamp; global = false }
@

<<function Ident.create_persistent>>=
let create_persistent s =
  { name = s; stamp = 0; global = true }
@

<<function Ident.name>>=
let name i = i.name
@

<<function Ident.unique_name>>=
let unique_name i = i.name ^ "_" ^ string_of_int i.stamp
@

<<function Ident.persistent>>=
let persistent i = (i.stamp = 0)
@

<<function Ident.equal>>=
(* @Scheck: dead but should be used? TODO? *)
let equal i1 i2 = i1.name = i2.name
@

<<function Ident.same>>=
let same i1 i2 = i1 = i2
  (* Possibly more efficient version (with a real compiler, at least):
       if i1.stamp <> 0
       then i1.stamp = i2.stamp
       else i2.stamp = 0 & i1.name = i2.name *)
@

<<function Ident.binding_time>>=
let binding_time i = i.stamp
@

<<function Ident.current_time>>=
let current_time() = !currentstamp
@

<<function Ident.hide>>=
let hide i =
  { stamp = -1; name = i.name; global = i.global }
@

<<function Ident.make_global>>=
let make_global i =
  i.global <- true
@

<<function Ident.global>>=
let global i =
  i.global
@

<<function Ident.print>>=
let print i =
  print_string i.name;
  match i.stamp with
    0 -> print_string "!"
  | -1 -> print_string "#"
  | n -> print_string "/"; print_int n; if i.global then print_string "g"
@

<<type Ident.tbl>>=
type 'a tbl =
    Empty
  | Node of 'a tbl * 'a data * 'a tbl * int
@

<<type Ident.data>>=
and 'a data =
  { ident: t;
    data: 'a;
    previous: 'a data option }
@

<<constant Ident.empty>>=
let empty = Empty
@

<<function Ident.mknode>>=
(* Inline expansion of height for better speed
 * let height = function
 *     Empty -> 0
 *   | Node(_,_,_,h) -> h
 *)

let mknode l d r =
  let hl = match l with Empty -> 0 | Node(_,_,_,h) -> h
  and hr = match r with Empty -> 0 | Node(_,_,_,h) -> h in
  Node(l, d, r, (if hl >= hr then hl + 1 else hr + 1))
@

<<function Ident.balance>>=
let balance l d r =
  let hl = match l with Empty -> 0 | Node(_,_,_,h) -> h
  and hr = match r with Empty -> 0 | Node(_,_,_,h) -> h in
  if hl > hr + 1 then
    match l with
    | Node (ll, ld, lr, _)
      when (match ll with Empty -> 0 | Node(_,_,_,h) -> h) >=
           (match lr with Empty -> 0 | Node(_,_,_,h) -> h) ->
        mknode ll ld (mknode lr d r)
    | Node (ll, ld, Node(lrl, lrd, lrr, _), _) ->
        mknode (mknode ll ld lrl) lrd (mknode lrr d r)
    | _ -> assert false
  else if hr > hl + 1 then
    match r with
    | Node (rl, rd, rr, _)
      when (match rr with Empty -> 0 | Node(_,_,_,h) -> h) >=
           (match rl with Empty -> 0 | Node(_,_,_,h) -> h) ->
        mknode (mknode l d rl) rd rr
    | Node (Node (rll, rld, rlr, _), rd, rr, _) ->
        mknode (mknode l d rll) rld (mknode rlr rd rr)
    | _ -> assert false
  else
    mknode l d r
@

<<function Ident.add>>=
let rec add id data = function
    Empty ->
      Node(Empty, {ident = id; data = data; previous = None}, Empty, 1)
  | Node(l, k, r, h) ->
      let c = compare id.name k.ident.name in
      if c = 0 then
        Node(l, {ident = id; data = data; previous = Some k}, r, h)
      else if c < 0 then
        balance (add id data l) k r
      else
        balance l k (add id data r)
@

<<function Ident.find_stamp>>=
let rec find_stamp s = function
    None ->
      raise Not_found
  | Some k ->
      if k.ident.stamp = s then k.data else find_stamp s k.previous
@

<<function Ident.find_same>>=
let rec find_same id = function
    Empty ->
      raise Not_found
  | Node(l, k, r, _) ->
      let c = compare id.name k.ident.name in
      if c = 0 then
        if id.stamp = k.ident.stamp
        then k.data
        else find_stamp id.stamp k.previous
      else
        find_same id (if c < 0 then l else r)
@

<<function Ident.find_name>>=
let rec find_name name = function
    Empty ->
      raise Not_found
  | Node(l, k, r, _) ->
      let c = compare name k.ident.name in
      if c = 0 then
        k.data
      else
        find_name name (if c < 0 then l else r)
@

<<function Ident.print_tbl>>=
(* @Scheck: dumper *)
let print_tbl print_elt tbl =
  open_box 2;
  print_string "[[";
  iter (fun id data -> 
          open_box 2;
          print id; print_string " ->"; print_space(); print_elt data;
          print_string ";"; close_box(); print_space())
       tbl;
  print_string "]]";
  close_box()
@


%-------------------------------------------------------------

<<./typing/ident.ml>>=
<<copyright header>>

open Format

<<type Ident.t>>

<<constant Ident.currentstamp>>

<<function Ident.create>>

<<function Ident.create_persistent>>

<<function Ident.name>>

<<function Ident.unique_name>>

<<function Ident.persistent>>

<<function Ident.equal>>

<<function Ident.same>>

<<function Ident.binding_time>>

<<function Ident.current_time>>

<<function Ident.hide>>

<<function Ident.make_global>>

<<function Ident.global>>

<<function Ident.print>>

<<type Ident.tbl>>

<<type Ident.data>>

<<constant Ident.empty>>

<<function Ident.mknode>>

<<function Ident.balance>>

<<function Ident.add>>

<<function Ident.find_stamp>>

<<function Ident.find_same>>

<<function Ident.find_name>>

let rec iter fn = function
    Empty -> ()
  | Node(l, k, r, _) ->
      iter fn l; iter_node fn k; iter fn r
and iter_node fn k =
  fn k.ident k.data;
  match k.previous with None -> () | Some prev_k -> iter_node fn prev_k

<<function Ident.print_tbl>>

@


\subsection*{[[./typing/primitive.mli]]}





%-------------------------------------------------------------

<<./typing/primitive.mli>>=
<<copyright header>>

<<type Primitive.description>>

<<signature Primitive.parse_declaration>>
<<signature Primitive.print_description>>
@


\subsection*{[[./typing/primitive.ml]]}


%-------------------------------------------------------------

<<./typing/primitive.ml>>=
<<copyright header>>

(* Description of primitive functions *)

open Misc
open Format

<<type Primitive.description>>

<<function Primitive.parse_declaration>>

<<function Primitive.print_quoted>>

<<function Primitive.print_description>>
@


\subsection*{[[./typing/path.mli]]}


<<signature Path.same>>=
val same: t -> t -> bool
@

<<signature Path.isfree>>=
val isfree: Ident.t -> t -> bool
@

<<signature Path.binding_time>>=
val binding_time: t -> int
@

<<signature Path.nopos>>=
val nopos: int
@


%-------------------------------------------------------------

<<./typing/path.mli>>=
<<copyright header>>

<<type Path.t>>

<<signature Path.same>>
<<signature Path.isfree>>
<<signature Path.binding_time>>

<<signature Path.nopos>>
@


\subsection*{[[./typing/path.ml]]}

<<constant Path.nopos>>=
let nopos = -1
@

<<function Path.same>>=
let rec same p1 p2 =
  match (p1, p2) with
    (Pident id1, Pident id2) -> Ident.same id1 id2
  | (Pdot(p1, s1, pos1), Pdot(p2, s2, pos2)) -> s1 = s2 & same p1 p2
  | (_, _) -> false
@

<<function Path.isfree>>=
let rec isfree id = function
    Pident id' -> Ident.same id id'
  | Pdot(p, s, pos) -> isfree id p
@

<<constant Path.binding_time>>=
let rec binding_time = function
    Pident id -> Ident.binding_time id
  | Pdot(p, s, pos) -> binding_time p
@


%-------------------------------------------------------------

<<./typing/path.ml>>=
<<copyright header>>
<<type Path.t>>

<<constant Path.nopos>>

<<function Path.same>>

<<function Path.isfree>>

<<constant Path.binding_time>>
@


\subsection*{[[./typing/types.mli]]}


<<type Types.value_description>>=
type value_description =
  { val_type: type_expr;                (* Type of the value *)
    val_kind: value_kind }
@

<<type Types.value_kind>>=
and value_kind =
    Val_reg                             (* Regular value *)
  | Val_prim of Primitive.description   (* Primitive *)
@

<<type Types.constructor_description>>=
type constructor_description =
  { cstr_res: type_expr;                (* Type of the result *)
    cstr_args: type_expr list;          (* Type of the arguments *)
    cstr_arity: int;                    (* Number of arguments *)
    cstr_tag: constructor_tag;          (* Tag for heap blocks *)
    cstr_consts: int;                   (* Number of constant constructors *)
    cstr_nonconsts: int }               (* Number of non-const constructors *)
@

<<type Types.constructor_tag>>=
and constructor_tag =
    Cstr_constant of int                (* Constant constructor (an int) *)
  | Cstr_block of int                   (* Regular constructor (a block) *)
  | Cstr_exception of Path.t            (* Exception constructor *)
@

<<type Types.label_description>>=
type label_description =
  { lbl_res: type_expr;                 (* Type of the result *)
    lbl_arg: type_expr;                 (* Type of the argument *)
    lbl_mut: mutable_flag;              (* Is this a mutable field? *)
    lbl_pos: int;                       (* Position in block *)
    lbl_all: label_description array;   (* All the labels in this type *)
    lbl_repres: record_representation } (* Representation for this record *)
@

<<type Types.record_representation>>=
and record_representation =
    Record_regular                      (* All fields are boxed / tagged *)
  | Record_float                        (* All fields are floats *)
@

<<type Types.type_declaration>>=
type type_declaration =
  { type_params: type_expr list;
    type_arity: int;
    type_kind: type_kind;
    type_manifest: type_expr option }
@

<<type Types.type_kind>>=
and type_kind =
    Type_abstract
  | Type_variant of (string * type_expr list) list
  | Type_record of (string * mutable_flag * type_expr) list
@

<<type Types.exception_declaration>>=
type exception_declaration = type_expr list
@

<<type Types.module_type>>=
type module_type =
    Tmty_ident of Path.t
  | Tmty_signature of signature
@

<<type Types.signature>>=
and signature = signature_item list
@

<<type Types.signature_item>>=
and signature_item =
    Tsig_value of Ident.t * value_description
  | Tsig_type of Ident.t * type_declaration
  | Tsig_exception of Ident.t * exception_declaration
  | Tsig_module of Ident.t * module_type
@


%-------------------------------------------------------------

<<./typing/types.mli>>=
<<copyright header>>

(* Representation of types and declarations *)

open Asttypes

(* Type expressions for the core language *)

<<type Types.type_expr>>

<<type Types.type_desc>>

<<type Types.abbrev_memo>>

(* Value descriptions *)

<<type Types.value_description>>

<<type Types.value_kind>>

(* Constructor descriptions *)

<<type Types.constructor_description>>

<<type Types.constructor_tag>>

(* Record label descriptions *)

<<type Types.label_description>>

<<type Types.record_representation>>

(* Type definitions *)

<<type Types.type_declaration>>

<<type Types.type_kind>>

<<type Types.exception_declaration>>

(* Type expressions for the module language *)

<<type Types.module_type>>

<<type Types.signature>>

<<type Types.signature_item>>
@


\subsection*{[[./typing/btype.mli]]}

<<signature Btype.generic_level>>=
val generic_level: int
@


<<signature Btype.newmarkedgenvar>>=
val newmarkedgenvar: unit -> type_expr
        (* Return a fresh marked generic variable *)
@

<<signature Btype.repr>>=
val repr: type_expr -> type_expr
        (* Return the canonical representative of a type. *)
@

<<signature Btype.iter_type_expr>>=
(**** Utilities for type traversal ****)

val iter_type_expr: (type_expr -> unit) -> type_expr -> unit
        (* Iteration on types *)
@

<<signature Btype.save_desc>>=
val save_desc: type_expr -> type_desc -> unit
        (* Save a type description *)
@

<<signature Btype.cleanup_types>>=
val cleanup_types: unit -> unit
        (* Restore type descriptions *)
@

<<signature Btype.lowest_level>>=
val lowest_level: int
        (* Marked type: ty.level < lowest_level *)
@

<<signature Btype.pivot_level>>=
val pivot_level: int
        (* Type marking: ty.level <- pivot_level - ty.level *)
@

<<signature Btype.unmark_type>>=
val unmark_type: type_expr -> unit
        (* Remove marks from a type *)
@

<<signature Btype.cleanup_abbrev>>=
(**** Memorization of abbreviation expansion ****)

val cleanup_abbrev: unit -> unit
        (* Flush the cache of abbreviation expansions.
           When some types are saved (using [output_value]), this
           function MUST be called just before. *)
@

<<signature Btype.memorize_abbrev>>=
val memorize_abbrev:
        abbrev_memo ref -> Path.t -> type_expr -> unit
        (* Add an expansion in the cache *)
@


%-------------------------------------------------------------

<<./typing/btype.mli>>=
<<copyright header>>

(* Basic operations on core types *)

open Types

<<signature Btype.generic_level>>

<<signature Btype.newgenty>>
<<signature Btype.newgenvar>>
<<signature Btype.newmarkedgenvar>>

<<signature Btype.repr>>

<<signature Btype.iter_type_expr>>

<<signature Btype.save_desc>>
<<signature Btype.cleanup_types>>

<<signature Btype.lowest_level>>
<<signature Btype.pivot_level>>
<<signature Btype.unmark_type>>

<<signature Btype.cleanup_abbrev>>
<<signature Btype.memorize_abbrev>>
@


\subsection*{[[./typing/btype.ml]]}

<<constant Btype.generic_level>>=
(**** Type level management ****)

let generic_level = 100000000
@

<<constant Btype.lowest_level>>=
(* Used to mark a type during a traversal. *)
let lowest_level = 0
@

<<constant Btype.pivot_level>>=
let pivot_level = 2 * lowest_level - 1
    (* pivot_level - lowest_level < lowest_level *)
@

<<function Btype.newgenty>>=
(**** Some type creators ****)

let newgenty desc      = { desc = desc; level = generic_level }
@

<<function Btype.newgenvar>>=
let newgenvar ()       = newgenty Tvar
@

<<function Btype.newmarkedgenvar>>=
let newmarkedgenvar () = { desc = Tvar; level = pivot_level - generic_level }
@

<<constant Btype.repr>>=
(**** Representative of a type ****)

let rec repr =
  function
    {desc = Tlink t'} ->
      (* 
         We do no path compression. Path compression does not seem to
         improve notably efficiency, and it prevents from changing a
         [Tlink] into another type (for instance, for undoing a
         unification).
      *)
      repr t'
  | t -> t
@

<<function Btype.iter_type_expr>>=
let iter_type_expr f ty =
  match ty.desc with
    Tvar               -> ()
  | Tarrow (ty1, ty2) -> f ty1; f ty2
  | Ttuple l           -> List.iter f l
  | Tconstr (_, l, _)          -> List.iter f l
  | Tnil               -> ()
  | Tlink ty           -> f ty
@

<<constant Btype.saved_desc>>=
let saved_desc = ref []
  (* Saved association of generic nodes with their description. *)
@

<<function Btype.save_desc>>=
let save_desc ty desc = 
  saved_desc := (ty, desc)::!saved_desc
@

<<function Btype.cleanup_types>>=
(* Restored type descriptions *)
let cleanup_types () =
  List.iter (fun (ty, desc) -> ty.desc <- desc) !saved_desc;
  saved_desc := []
@

<<function Btype.unmark_type>>=
(* Remove marks from a type. *)
let rec unmark_type ty =
  let ty = repr ty in
  if ty.level < lowest_level then begin
    ty.level <- pivot_level - ty.level;
    iter_type_expr unmark_type ty
  end
@

<<constant Btype.memo>>=
let memo = ref []
        (* Contains the list of saved abbreviation expansions. *)
@

<<function Btype.cleanup_abbrev>>=
let cleanup_abbrev () =
        (* Remove all memorized abbreviation expansions. *)
  List.iter (fun abbr -> abbr := Mnil) !memo;
  memo := []
@

<<function Btype.memorize_abbrev>>=
let memorize_abbrev mem path v =
        (* Memorize the expansion of an abbreviation. *)
  mem := Mcons (path, v, !mem);
  memo := mem :: !memo
@


%-------------------------------------------------------------

<<./typing/btype.ml>>=
<<copyright header2>>

(* Basic operations on core types *)

open Types

<<constant Btype.generic_level>>

<<constant Btype.lowest_level>>
<<constant Btype.pivot_level>>

<<function Btype.newgenty>>
<<function Btype.newgenvar>>
<<function Btype.newmarkedgenvar>>

<<constant Btype.repr>>

                  (**********************************)
                  (*  Utilities for type traversal  *)
                  (**********************************)

<<function Btype.iter_type_expr>>

<<constant Btype.saved_desc>>

<<function Btype.save_desc>>

<<function Btype.cleanup_types>>

<<function Btype.unmark_type>>

                  (*******************************************)
                  (*  Memorization of abbreviation expansion *)
                  (*******************************************)

<<constant Btype.memo>>

<<function Btype.cleanup_abbrev>>

<<function Btype.memorize_abbrev>>
@


\subsection*{[[./typing/subst.mli]]}

<<signature Subst.identity>>=
(*
   Substitutions are used to translate a type from one context to
   another.  This requires substituing paths for identifiers, and
   possibly also lowering the level of non-generic variables so that
   it be inferior to the maximum level of the new context.

   Substitutions can also be used to create a "clean" copy of a type.
   Indeed, non-variable node of a type are duplicated, with their
   levels set to generic level.  That way, the resulting type is
   well-formed (decreasing levels), even if the original one was not.
*)

val identity: t
@

<<signature Subst.add_type>>=
val add_type: Ident.t -> Path.t -> t -> t
@

<<signature Subst.add_module>>=
val add_module: Ident.t -> Path.t -> t -> t
@


<<signature Subst.type_expr>>=
val type_expr: t -> type_expr -> type_expr
@

<<signature Subst.value_description>>=
val value_description: t -> value_description -> value_description
@

<<signature Subst.type_declaration>>=
val type_declaration: t -> type_declaration -> type_declaration
@

<<signature Subst.exception_declaration>>=
val exception_declaration:
        t -> exception_declaration -> exception_declaration
@

<<signature Subst.modtype>>=
val modtype: t -> module_type -> module_type
@

<<signature Subst.signature>>=
val signature: t -> signature -> signature
@



%-------------------------------------------------------------

<<./typing/subst.mli>>=
<<copyright header>>

(* Substitutions *)

open Types

type t

<<signature Subst.identity>>

<<signature Subst.add_type>>
<<signature Subst.add_module>>

<<signature Subst.type_expr>>
<<signature Subst.value_description>>
<<signature Subst.type_declaration>>
<<signature Subst.exception_declaration>>
<<signature Subst.modtype>>
<<signature Subst.signature>>
@


\subsection*{[[./typing/subst.ml]]}


<<constant Subst.identity>>=
let identity =
  { types = Tbl.empty; modules = Tbl.empty; modtypes = Tbl.empty }
@

<<function Subst.add_type>>=
let add_type id p s =
  { types = Tbl.add id p s.types;
    modules = s.modules;
    modtypes = s.modtypes }
@

<<function Subst.add_module>>=
let add_module id p s =
  { types = s.types;
    modules = Tbl.add id p s.modules;
    modtypes = s.modtypes }
@


<<function Subst.remove_type>>=
let remove_type id s =
  { types = Tbl.remove id s.types;
    modules = s.modules;
    modtypes = s.modtypes }
@

<<function Subst.remove_module>>=
let remove_module id s =
  { types = s.types;
    modules = Tbl.remove id s.modules;
    modtypes = s.modtypes }
@


<<function Subst.module_path>>=
let rec module_path s = function
    Pident id as p ->
      begin try Tbl.find id s.modules with Not_found -> p end
  | Pdot(p, n, pos) ->
      Pdot(module_path s p, n, pos)
@

<<function Subst.type_path>>=
let type_path s = function
    Pident id as p ->
      begin try Tbl.find id s.types with Not_found -> p end
  | Pdot(p, n, pos) ->
      Pdot(module_path s p, n, pos)
@

<<function Subst.typexp>>=
(* Similar to [Ctype.nondep_type_rec]. *)
let rec typexp s ty =
  let ty = repr ty in
  if (ty.desc = Tvar) || (ty.level < lowest_level) then
    ty
  else begin
    let desc = ty.desc in
    save_desc ty desc;
    let ty' = newmarkedgenvar () in     (* Stub *)
    ty.desc <- Tlink ty';
    ty'.desc <-
      begin match desc with
        Tvar | Tlink _ ->
          fatal_error "Subst.typexp"
      | Tarrow(t1, t2) ->
          Tarrow(typexp s t1, typexp s t2)
      | Ttuple tl ->
          Ttuple(List.map (typexp s) tl)
      | Tconstr(p, tl, abbrev) ->
          Tconstr(type_path s p, List.map (typexp s) tl, ref Mnil)
      | Tnil ->
          Tnil
      end;
    ty'
  end
@

<<function Subst.type_expr>>=
(*
   Always make a copy of the type. If this is not done, type levels
   might not be correct.
*)
let type_expr s ty =
  let ty' = typexp s ty in
  cleanup_types ();
  unmark_type ty';
  ty'
@

<<function Subst.type_declaration>>=
let type_declaration s decl =
  let decl =
    { type_params = List.map (typexp s) decl.type_params;
      type_arity = decl.type_arity;
      type_kind =
        begin match decl.type_kind with
          Type_abstract -> Type_abstract
        | Type_variant cstrs ->
            Type_variant(
              List.map (fun (n, args) -> (n, List.map (typexp s) args))
                       cstrs)
        | Type_record lbls ->
            Type_record(
              List.map (fun (n, mut, arg) -> (n, mut, typexp s arg))
                       lbls)
        end;
      type_manifest =
        begin match decl.type_manifest with
          None -> None
        | Some ty -> Some(typexp s ty)
        end
    }
  in
  cleanup_types ();
  List.iter unmark_type decl.type_params;
  begin match decl.type_kind with
    Type_abstract -> ()
  | Type_variant cstrs ->
      List.iter (fun (c, tl) -> List.iter unmark_type tl) cstrs
  | Type_record lbls ->
      List.iter (fun (c, mut, t) -> unmark_type t) lbls
  end;
  begin match decl.type_manifest with
    None    -> ()
  | Some ty -> unmark_type ty
  end;
  decl
@

<<function Subst.value_description>>=
let value_description s descr =
  { val_type = type_expr s descr.val_type;
    val_kind = descr.val_kind }
@

<<function Subst.exception_declaration>>=
let exception_declaration s tyl =
  List.map (type_expr s) tyl
@


%-------------------------------------------------------------

<<./typing/subst.ml>>=
<<copyright header>>

(* Substitutions *)

open Misc
open Path
open Types
open Btype

<<type Subst.t>>

<<constant Subst.identity>>

<<function Subst.add_type>>

<<function Subst.add_module>>

<<function Subst.remove_type>>

<<function Subst.remove_module>>

<<function Subst.module_path>>

<<function Subst.type_path>>

<<function Subst.typexp>>

<<function Subst.type_expr>>

<<function Subst.type_declaration>>


<<function Subst.value_description>>

<<function Subst.exception_declaration>>

let rec modtype s = function
    Tmty_ident p as mty ->
      begin match p with
        Pident id ->
          begin try Tbl.find id s.modtypes with Not_found -> mty end
      | Pdot(p, n, pos) ->
          Tmty_ident(Pdot(module_path s p, n, pos))
      end
  | Tmty_signature sg ->
      Tmty_signature(signature s sg)

and signature s = function
    [] -> []
  | Tsig_value(id, d) :: sg ->
      Tsig_value(id, value_description s d) :: signature s sg
  | Tsig_type(id, d) :: sg ->
      Tsig_type(id, type_declaration s d) :: signature (remove_type id s) sg
  | Tsig_exception(id, d) :: sg ->
      Tsig_exception(id, exception_declaration s d) :: signature s sg
  | Tsig_module(id, mty) :: sg ->
      Tsig_module(id, modtype s mty) :: signature (remove_module id s) sg

@


\subsection*{[[./typing/predef.mli]]}


%-------------------------------------------------------------

<<./typing/predef.mli>>=
<<copyright header>>

(* Predefined type constructors (with special typing rules in typecore) *)

open Types

<<signatures Predef.type_xxx>>

<<signatures Predef.path_xxx>>

<<signature Predef.path_match_failure>>

<<signature Predef.build_initial_env>>

<<signature Predef.builtin_values>>
@


\subsection*{[[./typing/predef.ml]]}



%-------------------------------------------------------------

<<./typing/predef.ml>>=
<<copyright header>>

(* Predefined type constructors (with special typing rules in typecore) *)

open Path
open Types
open Btype

<<constants Predef.ident_xxx>>

<<constants Predef.path_xxx>>

<<constants Predef.type_xxx>>

<<constants Predef.ident_exn_xxx>>


<<constant Predef.path_match_failure>>

<<function Predef.build_initial_env>>

<<constant Predef.builtin_values>>
@


\subsection*{[[./typing/datarepr.mli]]}

<<signature Datarepr.constructor_descrs>>=
val constructor_descrs:
  type_expr -> (string * type_expr list) list ->
    (string * constructor_description) list
@

<<signature Datarepr.exception_descr>>=
val exception_descr:
  Path.t -> type_expr list -> constructor_description
@

<<signature Datarepr.label_descrs>>=
val label_descrs:
  type_expr -> (string * mutable_flag * type_expr) list ->
    (string * label_description) list
@


%-------------------------------------------------------------

<<./typing/datarepr.mli>>=
<<copyright header>>

(* Compute constructor and label descriptions from type declarations,
   determining their representation. *)

open Asttypes
open Types

<<signature Datarepr.constructor_descrs>>
<<signature Datarepr.exception_descr>>
<<signature Datarepr.label_descrs>>
@


\subsection*{[[./typing/datarepr.ml]]}

<<function Datarepr.constructor_descrs>>=
let constructor_descrs ty_res cstrs =
  let num_consts = ref 0 and num_nonconsts = ref 0 in
  List.iter
    (function (name, []) -> incr num_consts
            | (name, _)  -> incr num_nonconsts)
    cstrs;
  let rec describe_constructors idx_const idx_nonconst = function
      [] -> []
    | (name, ty_args) :: rem ->
        let (tag, descr_rem) =
          match ty_args with
            [] -> (Cstr_constant idx_const,
                   describe_constructors (idx_const+1) idx_nonconst rem)
          | _  -> (Cstr_block idx_nonconst,
                   describe_constructors idx_const (idx_nonconst+1) rem) in
        let cstr =
          { cstr_res = ty_res;
            cstr_args = ty_args;
            cstr_arity = List.length ty_args;
            cstr_tag = tag;
            cstr_consts = !num_consts;
            cstr_nonconsts = !num_nonconsts } in
        (name, cstr) :: descr_rem in
  describe_constructors 0 0 cstrs
@

<<function Datarepr.exception_descr>>=
let exception_descr path_exc decl =
  { cstr_res = Predef.type_exn;
    cstr_args = decl;
    cstr_arity = List.length decl;
    cstr_tag = Cstr_exception path_exc;
    cstr_consts = -1;
    cstr_nonconsts = -1 }
@

<<constant Datarepr.none>>=
let none = {desc = Ttuple []; level = -1}
                                        (* Clearly ill-formed type *)
@

<<constant Datarepr.dummy_label>>=
let dummy_label =
  { lbl_res = none; lbl_arg = none; lbl_mut = Immutable;
    lbl_pos = (-1); lbl_all = [||]; lbl_repres = Record_regular }
@

<<constant Datarepr.is_float>>=
(* Cannot call ctype.repres here *)

let rec is_float = 
  function
    {desc = Tlink ty} -> is_float ty
  | {desc = Tconstr(p, _, _)} -> Path.same p Predef.path_float
  | _ -> false
@

<<function Datarepr.label_descrs>>=
let label_descrs ty_res lbls =
  let all_labels = Array.create (List.length lbls) dummy_label in
  let repres =
    if List.for_all (fun (name, flag, ty) -> is_float ty) lbls
    then Record_float
    else Record_regular in
  let rec describe_labels num = function
      [] -> []
    | (name, mut_flag, ty_arg) :: rest ->
        let lbl =
          { lbl_res = ty_res;
            lbl_arg = ty_arg;
            lbl_mut = mut_flag;
            lbl_pos = num;
            lbl_all = all_labels;
            lbl_repres = repres } in
        all_labels.(num) <- lbl;
        (name, lbl) :: describe_labels (num+1) rest in
  describe_labels 0 lbls
@


%-------------------------------------------------------------

<<./typing/datarepr.ml>>=
<<copyright header>>

(* Compute constructor and label descriptions from type declarations,
   determining their representation. *)

open Misc
open Asttypes
open Types

<<function Datarepr.constructor_descrs>>

<<function Datarepr.exception_descr>>

<<constant Datarepr.none>>
<<constant Datarepr.dummy_label>>

<<constant Datarepr.is_float>>

<<function Datarepr.label_descrs>>
@


\subsection*{[[./typing/env.mli]]}


<<signature Env.find_module>>=
val find_module: Path.t -> t -> module_type
@


<<signature Env.find_type_expansion>>=
val find_type_expansion: Path.t -> t -> type_expr list * type_expr
@

<<signature Env.find_modtype_expansion>>=
@



<<signature Env.add_value>>=
(* Insertion by identifier *)

val add_value: Ident.t -> value_description -> t -> t
@

<<signature Env.add_type>>=
val add_type: Ident.t -> type_declaration -> t -> t
@

<<signature Env.add_exception>>=
val add_exception: Ident.t -> exception_declaration -> t -> t
@

<<signature Env.add_module>>=
val add_module: Ident.t -> module_type -> t -> t
@


<<signature Env.add_item>>=
(* Insertion of all fields of a signature. *)

val add_item: signature_item -> t -> t
@

<<signature Env.add_signature>>=
val add_signature: signature -> t -> t
@

<<signature Env.open_signature>>=
(* Insertion of all fields of a signature, relative to the given path.
   Used to implement open. *)

val open_signature: Path.t -> signature -> t -> t
@

<<signature Env.open_pers_signature>>=
val open_pers_signature: string -> t -> t
@

<<signature Env.enter_value>>=
(* Insertion by name *)

val enter_value: string -> value_description -> t -> Ident.t * t
@

<<signature Env.enter_exception>>=
val enter_exception: string -> exception_declaration -> t -> Ident.t * t
@

<<signature Env.enter_module>>=
val enter_module: string -> module_type -> t -> Ident.t * t
@


<<signature Env.reset_cache>>=
(* Reset the cache of in-core module interfaces.
   To be called in particular when load_path changes. *)

val reset_cache: unit -> unit
@

<<signature Env.read_signature>>=
(* Read, save a signature to/from a file *)

val read_signature: string -> string -> signature
        (* Arguments: module name, file name. Results: signature. *)
@

<<signature Env.save_signature>>=
val save_signature: signature -> string -> string -> unit
        (* Arguments: signature, module name, file name. *)
@

<<signature Env.imported_units>>=
(* Return the set of compilation units imported, with their CRC *)

val imported_units: unit -> (string * Digest.t) list
@

<<type Env.summary>>=
(* Summaries -- compact representation of an environment, to be
   exported in debugging information. *)

type summary =
    Env_empty
  | Env_value of summary * Ident.t * value_description
  | Env_type of summary * Ident.t * type_declaration
  | Env_exception of summary * Ident.t * exception_declaration
  | Env_module of summary * Ident.t * module_type
  | Env_open of summary * Path.t
@

<<signature Env.summary>>=
val summary: t -> summary
@


<<signature Env.check_modtype_inclusion>>=
(* Forward declaration to break mutual recursion with includemod. *)

val check_modtype_inclusion: (t -> module_type -> module_type -> unit) ref
@


%-------------------------------------------------------------

<<./typing/env.mli>>=
<<copyright header>>

(* Environment handling *)

open Types

<<signature type Env.t>>

<<signature Env.empty>>
<<signature Env.initial>>

(* Lookup by paths *)

<<signature Env.find_value>>
<<signature Env.find_type>>
<<signature Env.find_module>>

<<signature Env.find_type_expansion>>
<<signature Env.find_modtype_expansion>>

(* Lookup by long identifiers *)

<<signature Env.lookup_value>>
<<signature Env.lookup_constructor>>
<<signature Env.lookup_label>>
<<signature Env.lookup_type>>
<<signature Env.lookup_module>>

<<signature Env.add_value>>
<<signature Env.add_type>>
<<signature Env.add_exception>>
<<signature Env.add_module>>

<<signature Env.add_item>>
<<signature Env.add_signature>>

<<signature Env.open_signature>>
<<signature Env.open_pers_signature>>

<<signature Env.enter_value>>
<<signature Env.enter_exception>>
<<signature Env.enter_module>>

<<signature Env.reset_cache>>

<<signature Env.read_signature>>
<<signature Env.save_signature>>

<<signature Env.imported_units>>

<<type Env.summary>>

<<signature Env.summary>>

<<type Env.error>>

<<exception Env.Error>>

<<signature Env.report_error>>

<<signature Env.check_modtype_inclusion>>
@


\subsection*{[[./typing/env.ml]]}


<<type Env.module_components>>=
and module_components =
    Structure_comps of structure_components
@

<<type Env.structure_components>>=
and structure_components = {
  mutable comp_values     : (string, (value_description * int))       Tbl.t;
  mutable comp_constrs    : (string, (constructor_description * int)) Tbl.t;
  mutable comp_labels     : (string, (label_description * int))       Tbl.t;
  mutable comp_types      : (string, (type_declaration * int))        Tbl.t;
  mutable comp_modules    : (string, (module_type * int))             Tbl.t;
  mutable comp_components : (string, (module_components * int))       Tbl.t;
}
@


<<type Env.pers_struct>>=
(* Persistent structure descriptions *)

type pers_struct =
  { ps_name: string;
    ps_sig: signature;
    ps_comps: module_components;
    ps_crcs: (string * Digest.t) list }
@

<<constant Env.persistent_structures>>=
let persistent_structures =
  (Hashtbl.create 17 : (string, pers_struct) Hashtbl.t)
@

<<function Env.read_pers_struct>>=
let read_pers_struct modname filename =
  let ic = open_in_bin filename in
  try
    let buffer = String.create (String.length cmi_magic_number) in
    really_input ic buffer 0 (String.length cmi_magic_number);
    if buffer <> cmi_magic_number then begin
      close_in ic;
      raise(Error(Not_an_interface filename))
    end;
    let (name, sign, comps) = input_value ic in
    let crcs = input_value ic in
    close_in ic;
    let ps = { ps_name = name;
               ps_sig = sign;
               ps_comps = comps;
               ps_crcs = crcs } in
    if ps.ps_name <> modname then
      raise(Error(Illegal_renaming(ps.ps_name, filename)));
    Hashtbl.add persistent_structures modname ps;
    ps
  with End_of_file | Failure _ ->
    close_in ic;
    raise(Error(Corrupted_interface(filename)))
@

<<function Env.find_pers_struct>>=
let find_pers_struct name =
  try
    Hashtbl.find persistent_structures name
  with Not_found ->
    read_pers_struct name
      (find_in_path !load_path (String.uncapitalize name ^ ".cmi"))
@

<<function Env.reset_cache>>=
let reset_cache() =
  Hashtbl.clear persistent_structures
@

<<constant Env.check_modtype_inclusion>>=
(* Forward declarations *)

let check_modtype_inclusion =
  (* to be filled with includemod.check_modtype_inclusion *)
  ref ((fun env mty1 mty2 -> fatal_error "Env.include_modtypes") :
       t -> module_type -> module_type -> unit)
@

<<function Env.find_module_descr>>=
(* Lookup by identifier *)

let rec find_module_descr path env =
  match path with
    Pident id ->
      begin try
        let (p, desc) = Ident.find_same id env.components
        in desc
      with Not_found ->
        if Ident.persistent id
        then (find_pers_struct (Ident.name id)).ps_comps
        else raise Not_found
      end
  | Pdot(p, s, pos) ->
      begin match find_module_descr p env with
        Structure_comps c ->
          let (descr, pos) = Tbl.find s c.comp_components in
          descr
      end
@

<<function Env.find>>=
let find proj1 proj2 path env =
  match path with
    Pident id ->
      let (p, data) = Ident.find_same id (proj1 env)
      in data
  | Pdot(p, s, pos) ->
      begin match find_module_descr p env with
        Structure_comps c ->
          let (data, pos) = Tbl.find s (proj2 c) in data
      end
@

<<function Env.find_type_expansion>>=
let find_type_expansion path env =
  let decl = find_type path env in
  match decl.type_manifest with
    None      -> raise Not_found
  | Some body -> (decl.type_params, body)
@

<<function Env.find_modtype_expansion>>=
@

<<function Env.find_module>>=
(* @Scheck: used by the debugger *)
let find_module path env =
  match path with
    Pident id ->
      begin try
        let (p, data) = Ident.find_same id env.modules
        in data
      with Not_found ->
        if Ident.persistent id then
          let ps = find_pers_struct (Ident.name id) in
          Tmty_signature(ps.ps_sig)
        else raise Not_found
      end
  | Pdot(p, s, pos) ->
      begin match find_module_descr p env with
        Structure_comps c ->
          let (data, pos) = Tbl.find s c.comp_modules in data
      end
@

<<function Env.lookup>>=
let lookup proj1 proj2 lid env =
  match lid with
    Lident s ->
      Ident.find_name s (proj1 env)
  | Ldot(l, s) ->
      begin match lookup_module_descr l env with
        (p, Structure_comps c) ->
          let (data, pos) = Tbl.find s (proj2 c) in
          (Pdot(p, s, pos), data)
      end
@

<<function Env.lookup_simple>>=
let lookup_simple proj1 proj2 lid env =
  match lid with
    Lident s ->
      Ident.find_name s (proj1 env)
  | Ldot(l, s) ->
      begin match lookup_module_descr l env with
        (p, Structure_comps c) ->
          let (data, pos) = Tbl.find s (proj2 c) in
          data
      end
@

<<function Env.scrape_modtype>>=
(* Scrape a module type *)

let rec scrape_modtype mty env =
  match mty with
    Tmty_ident path ->
      failwith "TODO? or can remove, find_modtype_expansion"
(*
      begin try
        scrape_modtype (find_modtype_expansion path env) env
      with Not_found ->
        mty
      end
*)
  | _ -> mty
@

<<function Env.constructors_of_type>>=
(* Compute constructor descriptions *)

let constructors_of_type ty_path decl =
  match decl.type_kind with
    Type_variant cstrs ->
      Datarepr.constructor_descrs
        (Btype.newgenty (Tconstr(ty_path, decl.type_params, ref Mnil)))
        cstrs
  | _ -> []
@

<<function Env.labels_of_type>>=
(* Compute label descriptions *)

let labels_of_type ty_path decl =
  match decl.type_kind with
    Type_record labels ->
      Datarepr.label_descrs
        (Btype.newgenty (Tconstr(ty_path, decl.type_params, ref Mnil)))
        labels
  | _ -> []
@

<<function Env.prefix_idents>>=
(* Given a signature and a root path, prefix all idents in the signature
   by the root path and build the corresponding substitution. *)

let rec prefix_idents root pos sub = function
    [] -> ([], sub)
  | Tsig_value(id, decl) :: rem ->
      let p = Pdot(root, Ident.name id, pos) in
      let nextpos = match decl.val_kind with Val_prim _ -> pos | _ -> pos+1 in
      let (pl, final_sub) = prefix_idents root nextpos sub rem in
      (p::pl, final_sub)
  | Tsig_type(id, decl) :: rem ->
      let p = Pdot(root, Ident.name id, nopos) in
      let (pl, final_sub) =
        prefix_idents root pos (Subst.add_type id p sub) rem in
      (p::pl, final_sub)
  | Tsig_exception(id, decl) :: rem ->
      let p = Pdot(root, Ident.name id, pos) in
      let (pl, final_sub) = prefix_idents root (pos+1) sub rem in
      (p::pl, final_sub)
  | Tsig_module(id, mty) :: rem ->
      let p = Pdot(root, Ident.name id, pos) in
      let (pl, final_sub) =
        prefix_idents root (pos+1) (Subst.add_module id p sub) rem in
      (p::pl, final_sub)
@

<<constant Env.funappl_memo>>=
(* Memoized function to compute the components of a functor application
   in a path. *)

let funappl_memo =
@

<<function Env.enter>>=
(* Insertion of bindings by name *)

let enter store_fun name data env =
  let id = Ident.create name in (id, store_fun id (Pident id) data env)
@

<<function Env.add_item>>=
(* Insertion of all components of a signature *)

let add_item comp env =
  match comp with
    Tsig_value(id, decl) -> add_value id decl env
  | Tsig_type(id, decl) -> add_type id decl env
  | Tsig_exception(id, decl) -> add_exception id decl env
  | Tsig_module(id, mty) -> add_module id mty env
@

<<function Env.add_signature>>=
let rec add_signature sg env =
  match sg with
    [] -> env
  | comp :: rem -> add_signature rem (add_item comp env)
@

<<function Env.open_signature>>=
(* Open a signature path *)

let open_signature root sg env =
  (* First build the paths and substitution *)
  let (pl, sub) = prefix_idents root 0 Subst.identity sg in
  (* Then enter the components in the environment after substitution *)
  let newenv =
    List.fold_left2
      (fun env item p ->
        match item with
          Tsig_value(id, decl) ->
            store_value (Ident.hide id) p
                        (Subst.value_description sub decl) env
        | Tsig_type(id, decl) ->
            store_type (Ident.hide id) p
                       (Subst.type_declaration sub decl) env
        | Tsig_exception(id, decl) ->
            store_exception (Ident.hide id) p
                            (Subst.exception_declaration sub decl) env
        | Tsig_module(id, mty) ->
            store_module (Ident.hide id) p (Subst.modtype sub mty) env
      )
      env sg pl in
  { values = newenv.values;
    constrs = newenv.constrs;
    labels = newenv.labels;
    types = newenv.types;
    modules = newenv.modules;
    components = newenv.components;
    summary = Env_open(env.summary, root) }
@

<<function Env.open_pers_signature>>=
(* Open a signature from a file *)

let open_pers_signature name env =
  let ps = find_pers_struct name in
  open_signature (Pident(Ident.create_persistent name)) ps.ps_sig env
@

<<function Env.read_signature>>=
(* Read a signature from a file *)

let read_signature modname filename =
  let ps = read_pers_struct modname filename in ps.ps_sig
@

<<function Env.imported_units>>=
(* Return the list of imported interfaces with their CRCs *)

let imported_units() =
  let imported_units =
    ref ([] : (string * Digest.t) list) in
  let units_xref =
    (Hashtbl.create 13 : (string, Digest.t * string) Hashtbl.t) in
  let add_unit source (name, crc) =
    try
      let (oldcrc, oldsource) = Hashtbl.find units_xref name in
      if oldcrc <> crc then
        raise(Error(Inconsistent_import(name, oldsource, source)))
    with Not_found ->
      Hashtbl.add units_xref name (crc, source);
      imported_units := (name, crc) :: !imported_units in
  Hashtbl.iter
    (fun name ps -> List.iter (add_unit ps.ps_name) ps.ps_crcs)
    persistent_structures;
  !imported_units
@

<<function Env.save_signature>>=
(* Save a signature to a file *)

let save_signature sg modname filename =
  Btype.cleanup_abbrev ();
  let oc = open_out_bin filename in
  output_string oc cmi_magic_number;
  let comps =
    components_of_module empty Subst.identity
       (Pident(Ident.create_persistent modname)) (Tmty_signature sg) in
  output_value oc (modname, sg, comps);
  flush oc;
  let crc = Digest.file filename in
  let crcs = (modname, crc) :: imported_units() in
  output_value oc crcs;
  close_out oc;
  (* Enter signature in persistent table so that imported_unit()
     will also return its crc *)
  let ps =
    { ps_name = modname; ps_sig = sg; ps_comps = comps; ps_crcs = crcs } in
  Hashtbl.add persistent_structures modname ps
@

<<constant Env.initial>>=
(* Make the initial environment *)

let initial = Predef.build_initial_env add_type add_exception empty
@

<<function Env.summary>>=
(* Return the environment summary *)

let summary env = env.summary
@



%-------------------------------------------------------------

<<./typing/env.ml>>=
<<copyright header>>

(* Environment handling *)

open Format
open Config
open Misc
open Asttypes
open Longident
open Path
open Types


<<type Env.error>>

<<exception Env.Error>>

<<type Env.summary>>

<<type Env.t>>

<<type Env.module_components>>


<<type Env.structure_components>>


<<constant Env.empty>>

<<type Env.pers_struct>>

<<constant Env.persistent_structures>>

<<function Env.read_pers_struct>>

<<function Env.find_pers_struct>>

<<function Env.reset_cache>>

<<constant Env.check_modtype_inclusion>>

<<function Env.find_module_descr>>

<<function Env.find>>

let find_value =
  find (fun env -> env.values) (fun sc -> sc.comp_values)
let find_type =
  find (fun env -> env.types) (fun sc -> sc.comp_types)

<<function Env.find_type_expansion>>

<<function Env.find_modtype_expansion>>

<<function Env.find_module>>

(* Lookup by name *)

let rec lookup_module_descr lid env =
  match lid with
    Lident s ->
      begin try
        Ident.find_name s env.components
      with Not_found ->
        let ps = find_pers_struct s in
        (Pident(Ident.create_persistent s), ps.ps_comps)
      end
  | Ldot(l, s) ->
      let (p, descr) = lookup_module_descr l env in
      begin match descr with
        Structure_comps c ->
          let (descr, pos) = Tbl.find s c.comp_components in
          (Pdot(p, s, pos), descr)
      end

and lookup_module lid env =
  match lid with
    Lident s ->
      begin try
        Ident.find_name s env.modules
      with Not_found ->
        let ps = find_pers_struct s in
        (Pident(Ident.create_persistent s), Tmty_signature ps.ps_sig)
      end
  | Ldot(l, s) ->
      let (p, descr) = lookup_module_descr l env in
      begin match descr with
        Structure_comps c ->
          let (data, pos) = Tbl.find s c.comp_modules in
          (Pdot(p, s, pos), data)
      end

<<function Env.lookup>>

<<function Env.lookup_simple>>

let lookup_value =
  lookup (fun env -> env.values) (fun sc -> sc.comp_values)
let lookup_constructor =
  lookup_simple (fun env -> env.constrs) (fun sc -> sc.comp_constrs)
let lookup_label =
  lookup_simple (fun env -> env.labels) (fun sc -> sc.comp_labels)
let lookup_type =
  lookup (fun env -> env.types) (fun sc -> sc.comp_types)
  
<<function Env.scrape_modtype>>

<<function Env.constructors_of_type>>

<<function Env.labels_of_type>>

<<function Env.prefix_idents>>

(* Compute structure descriptions *)

let rec components_of_module env sub path mty =
  match scrape_modtype mty env with
    Tmty_signature sg ->
      let c =
        { comp_values = Tbl.empty; comp_constrs = Tbl.empty;
          comp_labels = Tbl.empty; comp_types = Tbl.empty;
          comp_modules = Tbl.empty;
          comp_components = Tbl.empty; } in
      let (pl, sub) = prefix_idents path 0 sub sg in
      let env = ref env in
      let pos = ref 0 in
      List.iter2 (fun item path ->
        match item with
          Tsig_value(id, decl) ->
            let decl' = Subst.value_description sub decl in
            c.comp_values <-
              Tbl.add (Ident.name id) (decl', !pos) c.comp_values;
            begin match decl.val_kind with
              Val_prim _ -> () | _ -> incr pos
            end
        | Tsig_type(id, decl) ->
            let decl' = Subst.type_declaration sub decl in
            c.comp_types <-
              Tbl.add (Ident.name id) (decl', nopos) c.comp_types;
            List.iter
              (fun (name, descr) ->
                c.comp_constrs <- Tbl.add name (descr, nopos) c.comp_constrs)
              (constructors_of_type path decl');
            List.iter
              (fun (name, descr) ->
                c.comp_labels <- Tbl.add name (descr, nopos) c.comp_labels)
              (labels_of_type path decl')
        | Tsig_exception(id, decl) ->
            let decl' = Subst.exception_declaration sub decl in
            let cstr = Datarepr.exception_descr path decl' in
            c.comp_constrs <-
              Tbl.add (Ident.name id) (cstr, !pos) c.comp_constrs;
            incr pos
        | Tsig_module(id, mty) ->
            let mty' = Subst.modtype sub mty in
            c.comp_modules <-
              Tbl.add (Ident.name id) (mty', !pos) c.comp_modules;
            let comps = components_of_module !env sub path mty in
            c.comp_components <-
              Tbl.add (Ident.name id) (comps, !pos) c.comp_components;
            env := store_components id path comps !env;
            incr pos
        )
        sg pl;
        Structure_comps c
  | Tmty_ident p ->
        Structure_comps {
          comp_values = Tbl.empty; comp_constrs = Tbl.empty;
          comp_labels = Tbl.empty; comp_types = Tbl.empty;
          comp_modules = Tbl.empty;
          comp_components = Tbl.empty; }

(* Insertion of bindings by identifier + path *)

and store_value id path decl env =
  { values = Ident.add id (path, decl) env.values;
    constrs = env.constrs;
    labels = env.labels;
    types = env.types;
    modules = env.modules;
    components = env.components;
    summary = Env_value(env.summary, id, decl) }

and store_type id path info env =
  { values = env.values;
    constrs =
      List.fold_right
        (fun (name, descr) constrs ->
          Ident.add (Ident.create name) descr constrs)
        (constructors_of_type path info)
        env.constrs;
    labels =
      List.fold_right
        (fun (name, descr) labels ->
          Ident.add (Ident.create name) descr labels)
        (labels_of_type path info)
        env.labels;
    types = Ident.add id (path, info) env.types;
    modules = env.modules;
    components = env.components;
    summary = Env_type(env.summary, id, info) }

and store_exception id path decl env =
  { values = env.values;
    constrs = Ident.add id (Datarepr.exception_descr path decl) env.constrs;
    labels = env.labels;
    types = env.types;
    modules = env.modules;
    components = env.components;
    summary = Env_exception(env.summary, id, decl) }

and store_module id path mty env =
  { values = env.values;
    constrs = env.constrs;
    labels = env.labels;
    types = env.types;
    modules = Ident.add id (path, mty) env.modules;
    components =
      Ident.add id (path, components_of_module env Subst.identity path mty)
                   env.components;
    summary = Env_module(env.summary, id, mty) }


and store_components id path comps env =
  { values = env.values;
    constrs = env.constrs;
    labels = env.labels;
    types = env.types;
    modules = env.modules;
    components = Ident.add id (path, comps) env.components;
    summary = env.summary }


<<constant Env.funappl_memo>>
  (Hashtbl.create 17 : (Path.t, module_components) Hashtbl.t)

(* Insertion of bindings by identifier *)

let add_value id desc env =
  store_value id (Pident id) desc env

let add_type id info env =
  store_type id (Pident id) info env

let add_exception id decl env =
  store_exception id (Pident id) decl env

let add_module id mty env =
  store_module id (Pident id) mty env

<<function Env.enter>>

let enter_value = enter store_value
and enter_exception = enter store_exception
and enter_module = enter store_module

<<function Env.add_item>>

<<function Env.add_signature>>

<<function Env.open_signature>>
  
<<function Env.open_pers_signature>>

<<function Env.read_signature>>

<<function Env.imported_units>>

<<function Env.save_signature>>

<<constant Env.initial>>

<<function Env.summary>>

<<function Env.report_error>>
@


\subsection*{[[./typing/typedtree.mli]]}


<<signature Typedtree.let_bound_idents>>=
(* Auxiliary functions over the a.s.t. *)

val let_bound_idents: (pattern * expression) list -> Ident.t list
@

<<signature Typedtree.rev_let_bound_idents>>=
val rev_let_bound_idents: (pattern * expression) list -> Ident.t list
@


%-------------------------------------------------------------

<<./typing/typedtree.mli>>=
<<copyright header>>

(* Abstract syntax tree after typing *)

open Asttypes
open Types

(* Value expressions for the core language *)

<<type Typedtree.pattern>>

<<type Typedtree.pattern_desc>>

<<type Typedtree.expression>>

<<type Typedtree.expression_desc>>

(* Value expressions for the module language *)

<<type Typedtree.module_expr>>

<<type Typedtree.module_expr_desc>>

<<type Typedtree.structure>>

<<type Typedtree.structure_item>>

<<type Typedtree.module_coercion>>

<<signature Typedtree.let_bound_idents>>

<<signature Typedtree.rev_let_bound_idents>>
@


\subsection*{[[./typing/typedtree.ml]]}

<<constant Typedtree.idents>>=
(* List the identifiers bound by a pattern or a let *)
let idents = ref([]: Ident.t list)
@

<<function Typedtree.bound_idents>>=
let rec bound_idents pat =
  match pat.pat_desc with
    Tpat_any -> ()
  | Tpat_var id -> idents := id :: !idents
  | Tpat_alias(p, id) -> bound_idents p; idents := id :: !idents
  | Tpat_constant cst -> ()
  | Tpat_tuple patl -> List.iter bound_idents patl
  | Tpat_construct(cstr, patl) -> List.iter bound_idents patl
  | Tpat_record lbl_pat_list ->
      List.iter (fun (lbl, pat) -> bound_idents pat) lbl_pat_list
  | Tpat_or(p1, p2) -> bound_idents p1; bound_idents p2
@

<<function Typedtree.rev_let_bound_idents>>=
let rev_let_bound_idents pat_expr_list =
  idents := [];
  List.iter (fun (pat, expr) -> bound_idents pat) pat_expr_list;
  let res = !idents in idents := []; res
@

<<function Typedtree.let_bound_idents>>=
let let_bound_idents pat_expr_list =
  List.rev(rev_let_bound_idents pat_expr_list)
@


%-------------------------------------------------------------

<<./typing/typedtree.ml>>=
<<copyright header>>

(* Abstract syntax tree after typing *)

open Misc
open Asttypes
open Types

(* Value expressions for the core language *)

<<type Typedtree.pattern>>

<<type Typedtree.pattern_desc>>

<<type Typedtree.expression>>

<<type Typedtree.expression_desc>>

(* Value expressions for the module language *)

<<type Typedtree.module_expr>>

<<type Typedtree.module_expr_desc>>

<<type Typedtree.structure>>

<<type Typedtree.structure_item>>

<<type Typedtree.module_coercion>>

(* Auxiliary functions over the a.s.t. *)

<<constant Typedtree.idents>>

<<function Typedtree.bound_idents>>

<<function Typedtree.rev_let_bound_idents>>

<<function Typedtree.let_bound_idents>>
@


\subsection*{[[./typing/ctype.mli]]}


<<exception Ctype.Cannot_expand>>=
exception Cannot_expand
@

<<exception Ctype.Cannot_apply>>=
exception Cannot_apply
@

<<exception Ctype.Recursive_abbrev>>=
exception Recursive_abbrev
@

<<signature Ctype.init_def>>=
val init_def: int -> unit
        (* Set the initial variable level *)
@

<<signature Ctype.begin_def>>=
val begin_def: unit -> unit
        (* Raise the variable level by one at the beginning of a definition. *)
@

<<signature Ctype.end_def>>=
val end_def: unit -> unit
        (* Lower the variable level by one at the end of a definition *)
@

<<signature Ctype.reset_global_level>>=
val reset_global_level: unit -> unit
@

<<signature Ctype.newty>>=
val newty: type_desc -> type_expr
@


<<signature Ctype.new_global_var>>=
val new_global_var: unit -> type_expr
        (* Return a fresh variable, bound at toplevel
           (as type variables ['a] in type constraints). *)
@

<<signature Ctype.none>>=
val none: type_expr
        (* A dummy type expression *)
@

<<signature Ctype.repr>>=
val repr: type_expr -> type_expr
        (* Return the canonical representative of a type. *)
@

<<signature Ctype.generalize>>=
val generalize: type_expr -> unit
        (* Generalize in-place the given type *)
@

<<signature Ctype.make_nongen>>=
val make_nongen: type_expr -> unit
        (* Make non-generalizable the given type *)
@

<<signature Ctype.correct_levels>>=
val correct_levels: type_expr -> type_expr
        (* Returns a copy with decreasing levels *)
@

<<signature Ctype.instance>>=
val instance: type_expr -> type_expr
        (* Take an instance of a type scheme *)
@

<<signature Ctype.instance_list>>=
val instance_list: type_expr list -> type_expr list
        (* Take an instance of a list of type schemes *)
@

<<signature Ctype.instance_constructor>>=
val instance_constructor:
        constructor_description -> type_expr list * type_expr
        (* Same, for a constructor *)
@

<<signature Ctype.instance_label>>=
val instance_label: label_description -> type_expr * type_expr
        (* Same, for a label *)
@

<<signature Ctype.instance_parameterized_type>>=
val instance_parameterized_type:
        type_expr list -> type_expr -> type_expr list * type_expr
@

<<signature Ctype.apply>>=
val apply:
        Env.t -> type_expr list -> type_expr -> type_expr list -> type_expr
        (* [apply [p1...pN] t [a1...aN]] match the arguments [ai] to
        the parameters [pi] and returns the corresponding instance of
        [t]. Exception [Cannot_apply] is raised in case of failure. *)
@

<<signature Ctype.expand_head>>=
(*         (* Expand an abbreviation *) *)
val expand_head: Env.t -> type_expr -> type_expr
@

<<signature Ctype.full_expand>>=
val full_expand: Env.t -> type_expr -> type_expr
@

<<signature Ctype.unify>>=
val unify: Env.t -> type_expr -> type_expr -> unit
        (* Unify the two types given. Raise [Unify] if not possible. *)
@

<<signature Ctype.filter_arrow>>=
val filter_arrow: Env.t -> type_expr -> type_expr * type_expr
        (* A special case of unification (with 'a -> 'b). *)
@

<<signature Ctype.moregeneral>>=
val moregeneral: Env.t -> bool -> type_expr -> type_expr -> bool
        (* Check if the first type scheme is more general than the second. *)
@

<<signature Ctype.equal>>=
val equal: Env.t -> bool -> type_expr list -> type_expr list -> bool
        (* [equal env [x1...xn] tau [y1...yn] sigma]
           checks whether the parameterized types
           [/\x1.../\xn.tau] and [/\y1.../\yn.sigma] are equivalent. *)
@

<<signature Ctype.enlarge_type>>=
val enlarge_type: Env.t -> type_expr -> type_expr
        (* Make a type larger *)
@

<<signature Ctype.subtype>>=
val subtype : Env.t -> type_expr -> type_expr -> unit -> unit
        (* [subtype env t1 t2] checks that [t1] is a subtype of [t2].
           It accumulates the constraints the type variables must
           enforce and returns a function that inforce this
           constraints. *)
@

<<signature Ctype.correct_abbrev>>=
val correct_abbrev: Env.t -> Ident.t -> type_expr list -> type_expr -> unit
@

<<signature Ctype.cyclic_abbrev>>=
val cyclic_abbrev: Env.t -> Ident.t -> type_expr -> bool
@

<<type Ctype.closed_schema_result>>=
type closed_schema_result = Var of type_expr | Row_var of type_expr
@

<<signature Ctype.closed_schema>>=
val closed_schema: type_expr -> bool
@

<<signature Ctype.unalias>>=
val unalias: type_expr -> type_expr
@

<<signature Ctype.unroll_abbrev>>=
val unroll_abbrev: Ident.t -> type_expr list -> type_expr -> type_expr
@

<<signature Ctype.arity>>=
val arity: type_expr -> int
        (* Return the arity (as for curried functions) of the given type. *)
@


%-------------------------------------------------------------

<<./typing/ctype.mli>>=
<<copyright header>>

(* Operations on core types *)

open Asttypes
open Types

<<exception Ctype.Unify>>
<<exception Ctype.Subtype>>
<<exception Ctype.Cannot_expand>>
<<exception Ctype.Cannot_apply>>
<<exception Ctype.Recursive_abbrev>>

<<signature Ctype.init_def>>
<<signature Ctype.begin_def>>
<<signature Ctype.end_def>>
<<signature Ctype.reset_global_level>>

<<signature Ctype.newty>>
<<signature Ctype.newvar>>
<<signature Ctype.new_global_var>>
<<signature Ctype.none>>

<<signature Ctype.repr>>

<<signature Ctype.generalize>>
<<signature Ctype.make_nongen>>
<<signature Ctype.correct_levels>>

<<signature Ctype.instance>>
<<signature Ctype.instance_list>>
<<signature Ctype.instance_constructor>>
<<signature Ctype.instance_label>>
<<signature Ctype.instance_parameterized_type>>
<<signature Ctype.apply>>

(* val expand_abbrev: *)
(*         Env.t -> Path.t -> type_expr list -> Types.abbrev_memo ref -> *)
(*         int -> type_expr *)
<<signature Ctype.expand_head>>
<<signature Ctype.full_expand>>

<<signature Ctype.unify>>
<<signature Ctype.filter_arrow>>

<<signature Ctype.moregeneral>>

<<signature Ctype.equal>>

<<signature Ctype.enlarge_type>>
<<signature Ctype.subtype>>

<<signature Ctype.correct_abbrev>>
<<signature Ctype.cyclic_abbrev>>

<<type Ctype.closed_schema_result>>
<<signature Ctype.closed_schema>>

<<signature Ctype.unalias>>
<<signature Ctype.unroll_abbrev>>
<<signature Ctype.arity>>
@


\subsection*{[[./typing/ctype.ml]]}



<<constant Ctype.current_level>>=
(**** Type level management ****)

let current_level = ref 0
@

<<constant Ctype.global_level>>=
let global_level = ref 1
@

<<function Ctype.init_def>>=
let init_def level = current_level := level
@

<<function Ctype.begin_def>>=
let begin_def () = incr current_level
@

<<function Ctype.end_def>>=
let end_def () = decr current_level
@

<<function Ctype.reset_global_level>>=
let reset_global_level () =
  global_level := !current_level + 1
@

<<function Ctype.newty>>=
(* Re-export generic type creators *)

let newty desc         = { desc = desc; level = !current_level }
@

<<function Ctype.new_global_ty>>=
let new_global_ty desc = { desc = desc; level = !global_level }
@

<<function Ctype.newvar>>=
let newvar ()          = { desc = Tvar; level = !current_level }
@

<<function Ctype.newmarkedvar>>=
let newmarkedvar () = { desc = Tvar; level = pivot_level - !current_level }
@

<<function Ctype.new_global_var>>=
let new_global_var ()  = new_global_ty Tvar
@

<<constant Ctype.none>>=
let none = newty (Ttuple [])                (* Clearly ill-formed type *)
@

<<constant Ctype.repr>>=
(* Re-export repr *)
let repr = repr
@

<<constant Ctype.try_expand_head'>>=
let try_expand_head' = (* Forward declaration *)
  ref (fun env ty -> raise Cannot_expand)
@

<<function Ctype.update_level>>=
(*
    The level of a type constructor must be greater than its binding
    time. That way, a type constructor cannot escape the scope of its
    definition, as would be the case in
      let x = ref []
      module M = struct type t let _ = (x : t list ref) end
    (without this constraint, the type system would actually be unsound.)
*)
let rec update_level env level ty =
  let ty = repr ty in
  if ty.level > level then begin
    begin match ty.desc with
      Tconstr(p, tl, abbrev)  when level < Path.binding_time p ->
        (* Try first to replace an abbreviation by its expansion. *)
        begin try
          ty.desc <- Tlink (!try_expand_head' env ty);
          update_level env level ty
        with Cannot_expand ->
          (* +++ Levels should be restored... *)
          raise (Unify [])
        end
    | _ ->
        ty.level <- level;
        iter_type_expr (update_level env level) ty
    end
  end
@

<<function Ctype.make_nongen>>=
(* 
   Function [update_level] will never try to expand an abbreviation in
   this case ([current_level] is greater than the binding time of any
   type constructor path). So, it can be called with the empty
   environnement.
*)
let make_nongen ty = update_level Env.empty !current_level ty
@

<<function Ctype.correct_levels>>=
(* Correct the levels of type [ty]. *)
let correct_levels ty =
  Subst.type_expr Subst.identity ty
@

<<constant Ctype.abbreviations>>=
(*
   Generic nodes are duplicated, while non-generic nodes are left
   as-is.
   During instantiation, the description of a generic node is first
   replaced by a link to a stub ([Tlink (newmarkedvar ())]). Once the
   copy is made, it replaces the stub.
   After instantiation, the description of generic node, which was
   stored by [save_desc], must be put back, using [cleanup_types].
   Marked on the copy are removed by [unmark].
*)

let abbreviations = ref (ref Mnil)
  (* Abbreviation memorized. *)
@

<<function Ctype.copy>>=
let rec copy ty =
  let ty = repr ty in
  if ty.level <> generic_level then
    ty
  else begin
    let desc = ty.desc in
    save_desc ty desc;
    let t = newmarkedvar () in          (* Stub *)
    ty.desc <- Tlink t;
    t.desc <-
      begin match desc with
        Tvar ->
          Tvar
      | Tarrow (t1, t2) ->
          Tarrow (copy t1, copy t2)
      | Ttuple tl ->
          Ttuple (List.map copy tl)
      | Tconstr (p, tl, _) ->
          (*
             One must allocate a new reference, so that abbrevia-
             tions belonging to different branches of a type are
             independent.
             Moreover, a reference containing a [Mcons] must be
             shared, so that the memorized expansion of an abbrevi-
             ation can be released by changing the content of just
             one reference.
          *)
          Tconstr (p, List.map copy tl,
                   ref (match ! !abbreviations with
                          Mcons _ -> Mlink !abbreviations
                        | abbrev  -> abbrev))
      | Tnil ->
          Tnil
      | Tlink t -> (* Actually unused *)
          Tlink (copy t)
      end;
    t
  end
@

<<function Ctype.instance>>=
(**** Variants of instantiations ****)

let instance sch =
  let ty = copy sch in
  cleanup_types ();
  unmark_type ty;
  ty
@

<<function Ctype.instance_list>>=
let instance_list schl =
  let tyl = List.map copy schl in
  cleanup_types ();
  List.iter unmark_type tyl;
  tyl
@

<<function Ctype.instance_constructor>>=
let instance_constructor cstr =
  let ty_res = copy cstr.cstr_res in
  let ty_args = List.map copy cstr.cstr_args in
  cleanup_types ();
  List.iter unmark_type ty_args; unmark_type ty_res;
  (ty_args, ty_res)
@

<<function Ctype.instance_label>>=
let instance_label lbl =
  let ty_res = copy lbl.lbl_res in
  let ty_arg = copy lbl.lbl_arg in
  cleanup_types ();
  unmark_type ty_arg; unmark_type ty_res;
  (ty_arg, ty_res)
@

<<function Ctype.instance_parameterized_type>>=
let instance_parameterized_type sch_args sch =
  let ty_args = List.map copy sch_args in
  let ty = copy sch in
  cleanup_types ();
  List.iter unmark_type ty_args; unmark_type ty;
  (ty_args, ty)
@

<<constant Ctype.unify'>>=
(**** Instantiation with parameter substitution ****)

let unify' = (* Forward declaration *)
  ref (fun env ty1 ty2 -> raise (Unify []))
@

<<function Ctype.subst>>=
let rec subst env level abbrev path params args body =
  let old_level = !current_level in
  current_level := level;
  try
    let body0 = newvar () in          (* Stub *)
    begin match path with
      None      -> ()
    | Some path -> memorize_abbrev abbrev path body0
    end;
    abbreviations := abbrev;
    let (params', body') = instance_parameterized_type params body in
    abbreviations := ref Mnil;
    !unify' env body0 body';
    List.iter2 (!unify' env) params' args;
    current_level := old_level;
    body'
  with Unify _ as exn ->
    current_level := old_level;
    raise exn
@

<<function Ctype.apply>>=
(*
   Only the shape of the type matters, not whether is is generic or
   not. [generic_level] might be somewhat slower, but it ensures
   invariants on types are enforced (decreasing levels.), and we don't
   care about efficiency here.
*)
let apply env params body args =
  try
    subst env generic_level (ref Mnil) None params args body
  with
    Unify _ -> raise Cannot_apply
@

<<function Ctype.find_expans>>=
(* Search whether the expansion has been memorized. *)
let rec find_expans p1 =
  function
    Mnil ->
      None
  | Mcons (p2, ty, _) when Path.same p1 p2 ->
      Some ty
  | Mcons (_, _, rem) ->
      find_expans p1 rem
  | Mlink {contents = rem} ->
      find_expans p1 rem
@

<<constant Ctype.previous_env>>=
let previous_env = ref Env.empty
@

<<function Ctype.expand_abbrev>>=
(*
   An abbreviation expansion will fail in either of these cases:
   1. The type constructor does not correspond to a manifest type.
   2. The type constructor is defined in an external file, and this
      file is not in the path (missing -I options).
   3. The type constructor is not in the "local" environment. This can
      happens when a non-generic type variable has been instantiated
      afterwards to the not yet defined type constructor. (Actually,
      this cannot happen at the moment due to the strong constraints
      between type levels and constructor binding time.)
   4. The expansion requires the expansion of another abbreviation,
      and this other expansion fails.
*)
let expand_abbrev env path args abbrev level =
  (* 
     If the environnement has changed, memorized expansions might not
     be correct anymore, and so we flush the cache. This is safe but
     quite pessimistic: it would be enough to flush the cache at the
     ends of structures and signatures.
     +++ Do it !
  *)
  if env != !previous_env then begin
    cleanup_abbrev ();
    previous_env := env
  end;
  match find_expans path !abbrev with
    Some ty ->
      if level <> generic_level then
        update_level env level ty;
      ty
  | None ->
      let (params, body) =
        try Env.find_type_expansion path env with Not_found ->
          raise Cannot_expand
      in
      try
        subst env level abbrev (Some path) params args body
      with Unify _ -> raise Cannot_expand
@

<<function Ctype.try_expand_head>>=
(* Fully expand the head of a type. Raise an exception if the type
   cannot be expanded. *)
let rec try_expand_head env ty =
  let ty = repr ty in
  match ty.desc with
    Tconstr(p, tl, abbrev) ->
      let ty' = expand_abbrev env p tl abbrev ty.level in
      begin try
        try_expand_head env ty'
      with Cannot_expand ->
        repr ty'
      end
  | _ ->
      raise Cannot_expand
@

<<toplevel Ctype._1>>=
let _ = try_expand_head' := try_expand_head
@

<<function Ctype.expand_head>>=
(* Fully expand the head of a type. *)
let rec expand_head env ty =
  try try_expand_head env ty with Cannot_expand -> repr ty
@

<<function Ctype.full_expand>>=
(* Recursively expand the head of a type.
   Also expand #-types. *)
let rec full_expand env ty =
  let ty = repr (expand_head env ty) in
  match ty.desc with
  | _ ->
      ty
@

<<function Ctype.generic_abbrev>>=
(*
   Check whether the abbreviation expands to a well-defined type.
   During the typing of a class, abbreviations for correspondings
   types expand to non-generic types.
*)
let generic_abbrev env path =
  try
    let (_, body) = Env.find_type_expansion path env in
    (repr body).level = generic_level
  with
    Not_found ->
      false
@

<<exception Ctype.Occur>>=
exception Occur
@

<<constant Ctype.visited>>=
(* The marks are already used by [expand_abbrev]... *)
let visited = ref []
@

<<function Ctype.non_recursive_abbrev>>=
let rec non_recursive_abbrev env ty =
  let ty = repr ty in
  if ty == none then raise Recursive_abbrev;
  if not (List.memq ty !visited) then begin
    let level = ty.level in
    visited := ty :: !visited;
    match ty.desc with
      Tconstr(p, args, abbrev) ->
        begin try
          non_recursive_abbrev env (try_expand_head env ty)
        with Cannot_expand ->
          iter_type_expr (non_recursive_abbrev env) ty
        end
    | _ ->
        iter_type_expr (non_recursive_abbrev env) ty
  end
@

<<function Ctype.correct_abbrev>>=
let correct_abbrev env ident params ty =
  visited := [];
  non_recursive_abbrev env
    (subst env generic_level (ref (Mcons (Path.Pident ident, none, Mnil))) None
       [] [] ty);
  visited := []
@

<<function Ctype.occur_rec>>=
let rec occur_rec env visited ty0 ty =
  if ty == ty0  then raise Occur;
  match ty.desc with
    Tconstr(p, tl, abbrev) ->
      begin try
        if List.memq ty visited then raise Occur;
        iter_type_expr (occur_rec env (ty::visited) ty0) ty
      with Occur -> try
        occur_rec env visited ty0 (try_expand_head env ty)
      with Cannot_expand ->
        raise Occur
      end
  | _ ->
      iter_type_expr (occur_rec env visited ty0) ty
@

<<function Ctype.occur>>=
let occur env ty0 ty =
  try occur_rec env [] ty0 ty with Occur -> raise (Unify [])
@

<<function Ctype.expand_trace>>=
(* +++ Move it to some other place ? *)

let expand_trace env trace =
  List.fold_right
    (fun (t1, t2) rem ->
       (repr t1, full_expand env t1)::(repr t2, full_expand env t2)::rem)
    trace []
@

<<constant Ctype.filter_trace>>=
let rec filter_trace =
  function
    (t1, t1')::(t2, t2')::rem ->
      let rem' = filter_trace rem in
      if (t1 == t1') & (t2 == t2')
      then rem'
      else (t1, t1')::(t2, t2')::rem'
  | _ ->
      []
@

<<function Ctype.deep_occur>>=
(* Return whether [t0] occurs in [ty]. Objects are also traversed. *)
let deep_occur t0 ty =
  let rec occur_rec ty =
    let ty = repr ty in
    if ty.level >= lowest_level then begin
      if ty == t0 then raise Occur;
      ty.level <- pivot_level - ty.level;
      iter_type_expr occur_rec ty
    end
  in
  try
    occur_rec ty; unmark_type ty; false
  with Occur ->
    unmark_type ty; true
@

<<function Ctype.unify>>=
let unify env ty1 ty2 =
  try
    unify env ty1 ty2
  with Unify trace ->
    let trace = expand_trace env trace in
    match trace with
      t1::t2::rem ->
        raise (Unify (t1::t2::filter_trace rem))
    | _ ->
        fatal_error "Ctype.unify"
@

<<toplevel Ctype._2>>=
let _ = unify' := unify
@

<<function Ctype.filter_arrow>>=
(* Unify [t] and ['a -> 'b]. Return ['a] and ['b]. *)
let rec filter_arrow env t =
  let t = expand_head env t in
  match t.desc with
    Tvar ->
      let t1 = newvar () and t2 = newvar () in
      let t' = newty (Tarrow (t1, t2)) in
      update_level env t.level t';
      t.desc <- Tlink t';
      (t1, t2)
  | Tarrow(t1, t2) ->
      (t1, t2)
  | _ ->
      raise (Unify [])
@

<<function Ctype.moregen_occur>>=
(*
   Update the level of [ty]. First check that the levels of variables
   from the subject are not lowered.
*)
let moregen_occur env level ty = 
  let rec occur ty =
    let ty = repr ty in
    if ty.level > level then begin
      if ty.desc = Tvar then raise Occur;
      ty.level <- pivot_level - ty.level;
      iter_type_expr occur ty
    end
  in
  begin try
    occur ty; unmark_type ty
  with Occur ->
    unmark_type ty; raise (Unify [])
  end;
  update_level env level ty
@

<<function Ctype.moregeneral>>=
(*
   Non-generic variable can be instanciated only if [inst_nongen] is
   true. So, [inst_nongen] should be set to false if the subject might
   contain non-generic variables.
   Usually, the subject is given by the user, and the pattern
   is unimportant.  So, no need to propagate abbreviations.
*)
let moregeneral env inst_nongen pat_sch subj_sch =
  let type_pairs = ref [] in

  let rec moregen env t1 t2 =
    if t1 == t2 then () else
    let t1 = repr t1 in
    let t2 = repr t2 in
    if t1 == t2 then () else

    match (t1.desc, t2.desc) with
      (Tvar, _) when if inst_nongen then t1.level <> generic_level - 1
                                    else t1.level =  generic_level ->
        moregen_occur env t1.level t2;
        t1.desc <- Tlink t2
    | (Tconstr (p1, [], _), Tconstr (p2, [], _)) when Path.same p1 p2 ->
        ()
    | _ ->
        let t1' = expand_head env t1 in
        let t2' = expand_head env t2 in
        (* Expansion may have changed the representative of the types... *)
        let t1' = repr t1' and t2' = repr t2' in
        if t1' == t2' then () else
        if
          List.exists (function (t1, t2) -> t1 == t1' && t2 == t2') !type_pairs
        then
          ()
        else begin
          type_pairs := (t1', t2') :: !type_pairs;

          match (t1'.desc, t2'.desc) with
            (Tvar, _) when if inst_nongen then t1'.level <> generic_level - 1
                                          else t1'.level =  generic_level ->
              moregen_occur env t1'.level t2;
              t1'.desc <- Tlink t2
          | (Tarrow (t1, u1), Tarrow (t2, u2)) ->
              moregen env t1 t2; moregen env u1 u2
          | (Ttuple tl1, Ttuple tl2) ->
              moregen_list env tl1 tl2
          | (Tconstr (p1, tl1, _), Tconstr (p2, tl2, _))
                when Path.same p1 p2 ->
              moregen_list env tl1 tl2
          | (Tnil, Tnil) ->
              ()
          | (_, _) ->
              raise (Unify [])
        end

  and moregen_list env tl1 tl2 =
    if List.length tl1 <> List.length tl2 then
      raise (Unify []);
    List.iter2 (moregen env) tl1 tl2

  in
  let old_level = !current_level in
  current_level := generic_level - 1;
  (*
     Generic variables are first duplicated with [instance].  So,
     their levels are lowered to [generic_level - 1].  The subject is
     then copied with [correct_levels].  That way, its levels won't be
     changed.
  *)
  let subj = correct_levels (instance subj_sch) in
  current_level := generic_level;
  (* Duplicate generic variables *)
  let patt = instance pat_sch in
  let res = try moregen env patt subj; true with Unify _ -> false in
  current_level := old_level;
  res
@

<<function Ctype.equal>>=
(* Two modes: with or without renaming of variables *)

let equal env rename tyl1 tyl2 =
  let subst = ref [] in
  let type_pairs = ref [] in

  let rec eqtype t1 t2 =
    if t1 == t2 then true else
    let t1 = repr t1 in
    let t2 = repr t2 in
    if t1 == t2 then true else
    match (t1.desc, t2.desc) with
    | (Tvar, Tvar) when rename ->
        begin try
          List.assq t1 !subst == t2
        with Not_found ->
          subst := (t1, t2) :: !subst;
          true
        end
    | (Tconstr (p1, [], _), Tconstr (p2, [], _)) when Path.same p1 p2 ->
        true
    | _ ->
        let t1 = expand_head env t1 in
        let t2 = expand_head env t2 in
        (* Expansion may have changed the representative of the types... *)
        let t1 = repr t1 and t2 = repr t2 in
        if t1 == t2 then true else
        List.exists (function (t1', t2') -> t1 == t1' & t2 == t2') !type_pairs
              (* XXX Possibly slow... *)
          ||
        begin
          type_pairs := (t1, t2) :: !type_pairs;
          match (t1.desc, t2.desc) with
            (Tvar, Tvar) when rename ->
              begin try
                List.assq t1 !subst == t2
              with Not_found ->
                subst := (t1, t2) :: !subst;
                true
              end
          | (Tarrow (t1, u1), Tarrow (t2, u2)) ->
              eqtype t1 t2 && eqtype u1 u2
          | (Ttuple tl1, Ttuple tl2) ->
              eqtype_list tl1 tl2
          | (Tconstr (p1, tl1, _), Tconstr (p2, tl2, _))
                when Path.same p1 p2 ->
              eqtype_list tl1 tl2
          | (Tnil, Tnil) ->
              true
          | (_, _) ->
              false
        end

  and eqtype_list tl1 tl2 =
    List.length tl1 = List.length tl2
                   &&
    List.for_all2 eqtype tl1 tl2

  in
    eqtype_list tyl1 tyl2
@

<<constant Ctype.subtypes>>=
(**** Build a subtype of a given type. ****)

let subtypes = ref []
@

<<function Ctype.build_subtype>>=
(* XXX Types rcursifs ? *)
let rec build_subtype env t =
  let t = repr t in
  match t.desc with
    Tlink t' ->                         (* Redundant ! *)
      build_subtype env t'
  | Tvar ->
      (t, false)
  | Tarrow(t1, t2) ->
      let (t1', c1) = (t1, false) in
      let (t2', c2) = build_subtype env t2 in
      if c1 or c2 then (new_global_ty (Tarrow(t1', t2')), true)
      else (t, false)
  | Ttuple tlist ->
      let (tlist', clist) =
        List.split (List.map (build_subtype env) tlist)
      in
      if List.exists (function c -> c) clist then
        (new_global_ty (Ttuple tlist'), true)
      else (t, false)
  | Tconstr(p, tl, abbrev) when generic_abbrev env p ->
      let t' = expand_abbrev env p tl abbrev t.level in
      let (t'', c) = build_subtype env t' in
      if c then (t'', true)
      else (t, false)
  | Tconstr(p, tl, abbrev) ->
      (t, false)
  | Tnil ->
      let v = new_global_var () in
      (v, true)
@

<<function Ctype.enlarge_type>>=
let enlarge_type env ty =
  subtypes := [];
  let (ty', _) = build_subtype env ty in
  subtypes := [];
  ty'
@

<<constant Ctype.subtypes (./typing/ctype.ml)>>=
(*
    During the traversal, a trace of visited types is maintained. It
    is printed in case of error.
    Constraints (pairs of types that must be equals) are accumulated
    rather than being enforced straight. Indeed, the result would
    otherwise depend on the order in which these constraints are
    enforced.
    A function enforcing these constraints is returned. That way, type
    variables can be bound to their actual values before this function
    is called (see Typecore).
    Only well-defined abbreviations are expanded (hence the tests
    [generic_abbrev ...]).
*)

let subtypes = ref [];;
@

<<function Ctype.subtype_error>>=
let subtype_error env trace =
  raise (Subtype (expand_trace env (List.rev trace), []))
@

<<function Ctype.subtype>>=
let subtype env ty1 ty2 =
  subtypes := [];
  (* Build constraint set. *)
  let cstrs = subtype_rec env [(ty1, ty2)] ty1 ty2 in
  (* Enforce constraints. *)
  function () ->
    List.iter
      (function (trace0, t1, t2) ->
         try unify env t1 t2 with Unify trace ->
           raise (Subtype (expand_trace env (List.rev trace0),
                           List.tl (List.tl trace))))
      cstrs;
    subtypes := []
@

<<function Ctype.unalias>>=
let unalias ty =
  let ty = repr ty in
  match ty.desc with
    Tvar ->
      ty
  | _ ->
      {desc = ty.desc; level = ty.level}
@

<<function Ctype.unroll_abbrev>>=
let unroll_abbrev id tl ty =
  let ty = repr ty in
  if (ty.desc = Tvar) || (List.exists (deep_occur ty) tl) then
    ty
  else
    let ty' = {desc = ty.desc; level = ty.level} in
    ty.desc <- Tlink {desc = Tconstr (Path.Pident id, tl, ref Mnil);
                      level = ty.level};
    ty'
@

<<function Ctype.arity>>=
(* Return the arity (as for curried functions) of the given type. *)
let rec arity ty =
  match (repr ty).desc with
    Tarrow(t1, t2) -> 1 + arity t2
  | _ -> 0
@

<<function Ctype.cyclic_abbrev>>=
(* Check whether an abbreviation expands to itself. *)
let rec cyclic_abbrev env id ty =
  let ty = repr ty in
  match ty.desc with
    Tconstr (Path.Pident id', _, _) when Ident.same id id' ->
      true
  | Tconstr (p, tl, abbrev) ->
      begin try
        cyclic_abbrev env id (try_expand_head env ty)
      with Cannot_expand ->
        false
      end
  | _ ->
      false
@


<<type Ctype.closed_schema_result (./typing/ctype.ml)>>=
type closed_schema_result = Var of type_expr | Row_var of type_expr
@

<<exception Ctype.Failed>>=
exception Failed of closed_schema_result
@

<<function Ctype.closed_schema_rec>>=
let rec closed_schema_rec row ty =
  let ty = repr ty in
  if ty.level >= lowest_level then begin
    let level = ty.level in
    ty.level <- pivot_level - level;
    match ty.desc with
      Tvar when level <> generic_level ->
        raise (Failed (if row then Row_var ty else Var ty))
    | _ ->
        iter_type_expr (closed_schema_rec false) ty
  end
@

<<function Ctype.closed_schema>>=
(* Return whether all variables of type [ty] are generic. *)
let closed_schema ty =
  try
    closed_schema_rec false ty;
    unmark_type ty;
    true
  with Failed _ ->
    unmark_type ty;
    false
@


%-------------------------------------------------------------

<<./typing/ctype.ml>>=
<<copyright header2>>

(* Operations on core types *)

open Misc
open Asttypes
open Types
open Btype

(*
   Type manipulation after type inference
   ======================================
   If one wants to manipulate a type after type inference (for
   instance, during code generation or in the debugger), one must
   first make sure that the type levels are correct, using the
   function [correct_levels]. Then, this type can be correctely
   manipulated by [apply], [expand_head] and [moregeneral].
*)

(*
   General notes
   =============
   - As much sharing as possible should be kept : it makes types
     smaller and better abbreviated.
     When necessary, some sharing can be lost. Types will still be
     printed correctly (+++ TO DO...), and abbreviations defined by a
     class do not depend on sharing thanks to constrained
     abbreviations. (Of course, even if some sharing is lost, typing
     will still be correct.)
   - All nodes of a type have a level : that way, one know whether a
     node need to be duplicated or not when instantiating a type.
   - Levels of a type are decreasing (generic level being considered
     as greatest).
   - The level of a type constructor is superior to the binding
     time of its path.
   - Recursive types without limitation should be handled (even if
     there is still an occur check). This avoid treating specially the
     case for objects, for instance. Furthermore, the occur check
     policy can then be easily changed.
*)

(*
   A faire
   =======
   - Revoir affichage des types.
   - Etendre la portee d'un alias [... as 'a] a tout le type englobant.
   - #-type implementes comme de vraies abreviations.
   - Niveaux plus fins pour les identificateurs :
       Champ [global] renomme en [level];
       Niveau -1 : global
               0 : module toplevel
               1 : module contenu dans module toplevel
              ...
     En fait, incrementer le niveau a chaque fois que l'on rentre dans
     un module.

       3   4 6
        \ / /
       1 2 5
        \|/
         0

     [Subst] doit ecreter les niveaux (pour qu'un variable non
     generalisable dans un module de niveau 2 ne se retrouve pas
     generalisable lorsque l'on l'utilise au niveau 0).

   - Traitement de la trace de l'unification separe de la fonction
     [unify].
*)

<<exception Ctype.Unify>>

<<exception Ctype.Subtype>>

<<exception Ctype.Cannot_expand>>

<<exception Ctype.Cannot_apply>>

<<exception Ctype.Recursive_abbrev>>

<<constant Ctype.current_level>>
<<constant Ctype.global_level>>

<<function Ctype.init_def>>
<<function Ctype.begin_def>>
<<function Ctype.end_def>>

<<function Ctype.reset_global_level>>

(**** Some type creators ****)

<<function Ctype.newty>>
<<function Ctype.new_global_ty>>

<<function Ctype.newvar>>
<<function Ctype.newmarkedvar>>
<<function Ctype.new_global_var>>

<<constant Ctype.none>>

(**** Representative of a type ****)

<<constant Ctype.repr>>


                         (*****************************)
                         (*  Type level manipulation  *)
                         (*****************************)

(*
   It would be a bit more efficient to remove abbreviation expansions
   rather than generalizing them: these expansions will usually not be
   used anymore. However, this is not possible in the general case, as
   [expand_abbrev] (via [subst]) requires these expansions to be
   preserved. Does it worth duplicating this code ?
*)
let rec generalize ty =
  let ty = repr ty in
  if (ty.level > !current_level) && (ty.level <> generic_level) then begin
    ty.level <- generic_level;
    begin match ty.desc with
      Tconstr (_, _, abbrev) ->
        generalize_expans !abbrev
    | _ -> ()
    end;
    iter_type_expr generalize ty
  end

and generalize_expans =
  function
    Mnil              ->  ()
  | Mcons(_, ty, rem) ->  generalize ty; generalize_expans rem
  | Mlink rem         ->  generalize_expans !rem

<<constant Ctype.try_expand_head'>>

(*
   Lower the levels of a type (assume [level] is not
   [generic_level]).
*)
<<function Ctype.update_level>>

<<function Ctype.make_nongen>>

<<function Ctype.correct_levels>>


                              (*******************)
                              (*  Instantiation  *)
                              (*******************)


<<constant Ctype.abbreviations>>

<<function Ctype.copy>>

<<function Ctype.instance>>

<<function Ctype.instance_list>>

<<function Ctype.instance_constructor>>

<<function Ctype.instance_label>>

<<function Ctype.instance_parameterized_type>>

<<constant Ctype.unify'>>

<<function Ctype.subst>>

<<function Ctype.apply>>


                              (****************************)
                              (*  Abbreviation expansion  *)
                              (****************************)


<<function Ctype.find_expans>>

<<constant Ctype.previous_env>>

(* Expand an abbreviation. The expansion is memorized. *)
(* 
   Assume the level is greater than the path binding time of the
   expanded abbreviation.
*)
<<function Ctype.expand_abbrev>>

<<function Ctype.try_expand_head>>

<<toplevel Ctype._1>>

<<function Ctype.expand_head>>

<<function Ctype.full_expand>>

<<function Ctype.generic_abbrev>>


                              (*****************)
                              (*  Occur check  *)
                              (*****************)


<<exception Ctype.Occur>>

<<constant Ctype.visited>>

<<function Ctype.non_recursive_abbrev>>

<<function Ctype.correct_abbrev>>

<<function Ctype.occur_rec>>

<<function Ctype.occur>>


                              (*****************)
                              (*  Unification  *)
                              (*****************)



(**** Transform error trace ****)
<<function Ctype.expand_trace>>

<<constant Ctype.filter_trace>>

(**** Unification ****)

<<function Ctype.deep_occur>>

(*
   1. When unifying two non-abbreviated types, one type is made a link
      to the other. When unifying an abbreviated type with a
      non-abbreviated type, the non-abbreviated type is made a link to
      the other one. When unifying to abbreviated types, these two
      types are kept distincts, but they are made to (temporally)
      expand to the same type.
   2. Abbreviations with at least one parameter are systematically
      expanded. The overhead does not seem to high, and that way
      abbreviations where some parameters does not appear in the
      expansion, such as ['a t = int], are correctly handled. In
      particular, for this example, unifying ['a t] with ['b t] keeps
      ['a] and ['b] distincts. (Is it really important ?)
   3. Unifying an abbreviation ['a t = 'a] with ['a] should not yield
      ['a t as 'a]. Indeed, the type variable would otherwise be lost.
      This problem occurs for abbreviations expanding to a type
      variable, but also to many other constrained abbreviations (for
      instance, [(< x : 'a > -> unit) t = <x : 'a>]). The solution is
      that, if an abbreviation is unified with some subpart of its
      parameters, then the parameter actually does not get
      abbreviated.  It would be possible to check whether some
      information is indeed lost, but it probably does not worth it.
*)
let rec unify env t1 t2 =
  (* First step: special cases (optimizations) *)
  if t1 == t2 then () else
  let t1 = repr t1 in
  let t2 = repr t2 in
  if t1 == t2 then () else

  try
    match (t1.desc, t2.desc) with
      (Tvar, Tconstr _) when deep_occur t1 t2 ->
        unify2 env t1 t2
    | (Tconstr _, Tvar) when deep_occur t2 t1 ->
        unify2 env t1 t2
    | (Tvar, _) ->
        occur env t1 t2;
        update_level env t1.level t2;
        t1.desc <- Tlink t2
    | (_, Tvar) ->
        occur env t2 t1;
        update_level env t2.level t1;
        t2.desc <- Tlink t1
    | (Tconstr (p1, [], _), Tconstr (p2, [], _)) when Path.same p1 p2 ->
        update_level env t1.level t2;
        t1.desc <- Tlink t2
    | _ ->
        unify2 env t1 t2
  with Unify trace ->
    raise (Unify ((t1, t2)::trace))

and unify2 env t1 t2 =
  (* Second step: expansion of abbreviations *)
  let t1' = expand_head env t1 in
  let t2' = expand_head env t2 in
  (* Expansion may have changed the representative of the types... *)
  let t1' = repr t1' and t2' = repr t2' in
  if t1' == t2' then () else

  let t1 = repr t1 and t2 = repr t2 in
  if (t1 == t1') || (t2 != t2') then
    unify3 env t1 t1' t2 t2'
  else
    try unify3 env t2 t2' t1 t1' with Unify trace ->
      raise (Unify (List.map (fun (x, y) -> (y, x)) trace))

and unify3 env t1 t1' t2 t2' =
  (* Third step: truly unification *)
  (* Assumes either [t1 == t1'] or [t2 != t2'] *)
  let d1 = t1'.desc and d2 = t2'.desc in
  
  if (t2 != t2') && (deep_occur t1' t2) then begin
    (* See point 3. *)
    occur env t1' t2';
    update_level env t1'.level t2';
    t1'.desc <- Tlink t2'
  end else begin
    occur env t1' t2;
    update_level env t1'.level t2;
    t1'.desc <- Tlink t2
  end;

  try
    begin match (d1, d2) with
      (Tvar, _) ->
        ()
    | (_, Tvar) ->
        occur env t2' (newty d1);
        if t1 == t1' then begin
          (* The variable must be instantiated... *)
          let ty = {desc = d1; level = t1'.level} in
          update_level env t2'.level ty;
          t2'.desc <- Tlink ty
        end else begin
          t1'.desc <- d1;
          update_level env t2'.level t1;
          t2'.desc <- Tlink t1
        end
    | (Tarrow (t1, u1), Tarrow (t2, u2)) ->
        unify env t1 t2; unify env u1 u2
    | (Ttuple tl1, Ttuple tl2) ->
        unify_list env tl1 tl2
    | (Tconstr (p1, tl1, _), Tconstr (p2, tl2, _)) when Path.same p1 p2 ->
        unify_list env tl1 tl2
    | (Tnil, Tnil) ->
        ()
    | (_, _) ->
        raise (Unify [])
    end
(*
    (* 
       Can only be done afterwards, once the row variable has
       (possibly) been instantiated.
    *)
    if t1 != t1' (* && t2 != t2' *) then begin
      match (t1.desc, t2.desc) with
        (Tconstr (p, ty::_, _), _)
            when ((repr ty).desc <> Tvar)
              && weak_abbrev p
              && not (deep_occur t1 t2) ->
          update_level env t1.level t2;
          t1.desc <- Tlink t2
      | (_, Tconstr (p, ty::_, _))
            when ((repr ty).desc <> Tvar)
              && weak_abbrev p
              && not (deep_occur t2 t1) ->
          update_level env t2.level t1;
          t2.desc <- Tlink t1;
          t1'.desc <- Tlink t2'
      | _ ->
          ()
    end
*)
  with Unify trace ->
    t1'.desc <- d1;
    raise (Unify trace)

and unify_list env tl1 tl2 =
  if List.length tl1 <> List.length tl2 then
    raise (Unify []);
  List.iter2 (unify env) tl1 tl2



<<function Ctype.unify>>

<<toplevel Ctype._2>>

(**** Special cases of unification ****)

<<function Ctype.filter_arrow>>



                        (***********************************)
                        (*  Matching between type schemes  *)
                        (***********************************)

<<function Ctype.moregen_occur>>

<<function Ctype.moregeneral>>


                 (*********************************************)
                 (*  Equivalence between parameterized types  *)
                 (*********************************************)


<<function Ctype.equal>>


                              (***************)
                              (*  Subtyping  *)
                              (***************)


<<constant Ctype.subtypes>>

<<function Ctype.build_subtype>>

<<function Ctype.enlarge_type>>

(**** Check whether a type is a subtype of another type. ****)

<<constant Ctype.subtypes (./typing/ctype.ml)>>

<<function Ctype.subtype_error>>

let rec subtype_rec env trace t1 t2 =
  let t1 = repr t1 in
  let t2 = repr t2 in
  if t1 == t2 then [] else
  if List.exists (fun (t1', t2') -> t1 == t1' & t2 == t2') !subtypes then
      (* +++ Possibly slow *)
    []
  else begin
    subtypes := (t1, t2) :: !subtypes;
    match (t1.desc, t2.desc) with
      (Tvar, _) | (_, Tvar) ->
        [(trace, t1, t2)]
    | (Tarrow(t1, u1), Tarrow(t2, u2)) ->
        (subtype_rec env ((t2, t1)::trace) t2 t1) @
        (subtype_rec env ((u1, u2)::trace) u1 u2)
    | (Ttuple tl1, Ttuple tl2) ->
        subtype_list env trace tl1 tl2
    | (Tconstr(p1, tl1, abbrev1), Tconstr _) when generic_abbrev env p1 ->
        subtype_rec env trace (expand_abbrev env p1 tl1 abbrev1 t1.level) t2
    | (Tconstr _, Tconstr(p2, tl2, abbrev2)) when generic_abbrev env p2 ->
        subtype_rec env trace t1 (expand_abbrev env p2 tl2 abbrev2 t2.level)
    | (Tconstr _, Tconstr _) ->
        [(trace, t1, t2)]
    | (Tconstr(p1, tl1, abbrev1), _) when generic_abbrev env p1 ->
        subtype_rec env trace (expand_abbrev env p1 tl1 abbrev1 t1.level) t2
    | (_, Tconstr (p2, tl2, abbrev2)) when generic_abbrev env p2 ->
        subtype_rec env trace t1 (expand_abbrev env p2 tl2 abbrev2 t2.level)
    | (_, _) ->
        subtype_error env trace
  end

and subtype_list env trace tl1 tl2 =
  if List.length tl1 <> List.length tl2 then
    subtype_error env trace;
  List.fold_left2
    (fun cstrs t1 t2 -> cstrs @ (subtype_rec env ((t1, t2)::trace) t1 t2))
    [] tl1 tl2


<<function Ctype.subtype>>


                              (*******************)
                              (*  Miscellaneous  *)
                              (*******************)


<<function Ctype.unalias>>

<<function Ctype.unroll_abbrev>>

<<function Ctype.arity>>

<<function Ctype.cyclic_abbrev>>


                    (**************************************)
                    (*  Check genericity of type schemes  *)
                    (**************************************)


<<type Ctype.closed_schema_result (./typing/ctype.ml)>>
<<exception Ctype.Failed>>

<<function Ctype.closed_schema_rec>>

<<function Ctype.closed_schema>>

@


\subsection*{[[./typing/includecore.mli]]}

<<exception Includecore.Dont_match>>=
exception Dont_match
@

<<signature Includecore.value_descriptions>>=
val value_descriptions:
        Env.t -> value_description -> value_description -> module_coercion
@

<<signature Includecore.type_declarations>>=
val type_declarations:
        Env.t -> Ident.t -> type_declaration -> type_declaration -> bool
@

<<signature Includecore.exception_declarations>>=
val exception_declarations:
        Env.t -> exception_declaration -> exception_declaration -> bool
@


%-------------------------------------------------------------

<<./typing/includecore.mli>>=
<<copyright header>>

(* Inclusion checks for the core language *)

open Types
open Typedtree

<<exception Includecore.Dont_match>>

<<signature Includecore.value_descriptions>>
<<signature Includecore.type_declarations>>
<<signature Includecore.exception_declarations>>
@


\subsection*{[[./typing/includecore.ml]]}

<<exception Includecore.Dont_match (./typing/includecore.ml)>>=
(* Inclusion between value descriptions *)

exception Dont_match
@

<<function Includecore.value_descriptions>>=
let value_descriptions env vd1 vd2 =
  if Ctype.moregeneral env true vd1.val_type vd2.val_type then begin
    match (vd1.val_kind, vd2.val_kind) with
        (Val_prim p1, Val_prim p2) ->
          if p1 = p2 then Tcoerce_none else raise Dont_match
      | (Val_prim p, _) -> Tcoerce_primitive p
      | (_, Val_prim p) -> raise Dont_match
      | (_, _) -> Tcoerce_none
  end else
    raise Dont_match
@

<<function Includecore.type_declarations>>=
(* Inclusion between type declarations *)

let type_declarations env id decl1 decl2 =
  decl1.type_arity = decl2.type_arity &
  begin match (decl1.type_kind, decl2.type_kind) with
      (_, Type_abstract) -> true
    | (Type_variant cstrs1, Type_variant cstrs2) ->
        for_all2
          (fun (cstr1, arg1) (cstr2, arg2) ->
            cstr1 = cstr2 &
            for_all2
              (fun ty1 ty2 ->
                Ctype.equal env true (ty1::decl1.type_params)
                                     (ty2::decl2.type_params))
              arg1 arg2)
          cstrs1 cstrs2
    | (Type_record labels1, Type_record labels2) ->
        for_all2
          (fun (lbl1, mut1, ty1) (lbl2, mut2, ty2) ->
            lbl1 = lbl2 & mut1 = mut2 &
            Ctype.equal env true (ty1::decl1.type_params)
                                 (ty2::decl2.type_params))
          labels1 labels2
    | (_, _) -> false
  end &
  begin match (decl1.type_manifest, decl2.type_manifest) with
      (_, None) ->
        Ctype.equal env true decl1.type_params decl2.type_params
    | (Some ty1, Some ty2) ->
        Ctype.equal env true (ty1::decl1.type_params)
                             (ty2::decl2.type_params)
    | (None, Some ty2) ->
        let ty1 =
          Btype.newgenty (Tconstr(Pident id, decl2.type_params, ref Mnil))
        in
        Ctype.equal env true decl1.type_params decl2.type_params
          &
        Ctype.equal env false [ty1] [ty2]
  end
@

<<function Includecore.exception_declarations>>=
(* Inclusion between exception declarations *)

let exception_declarations env ed1 ed2 =
  for_all2 (fun ty1 ty2 -> Ctype.equal env false [ty1] [ty2]) ed1 ed2
@


%-------------------------------------------------------------

<<./typing/includecore.ml>>=
<<copyright header>>

(* Inclusion checks for the core language *)

open Misc
open Path
open Types
open Typedtree

<<exception Includecore.Dont_match (./typing/includecore.ml)>>

<<function Includecore.value_descriptions>>

<<function Includecore.type_declarations>>

<<function Includecore.exception_declarations>>
@


\subsection*{[[./typing/mtype.mli]]}

<<signature Mtype.scrape>>=
val scrape: Env.t -> module_type -> module_type
        (* Expand toplevel module type abbreviations
           till hitting a "hard" module type (signature, functor,
           or abstract module type ident. *)
@

<<signature Mtype.strengthen>>=
val strengthen: Env.t -> module_type -> Path.t -> module_type
        (* Strengthen abstract type components relative to the
           given path. *)
@


%-------------------------------------------------------------

<<./typing/mtype.mli>>=
<<copyright header>>

(* Operations on module types *)

open Types

<<signature Mtype.scrape>>
<<signature Mtype.strengthen>>
@


\subsection*{[[./typing/mtype.ml]]}

<<function Mtype.scrape>>=
let rec scrape env mty =
  match mty with
    Tmty_ident p ->
      failwith "TODO: find_modtype_Expansion"
(*
      begin try
        Env.find_modtype_expansion p env
      with Not_found ->
        mty
      end
*)
  | _ -> mty
@


%-------------------------------------------------------------

<<./typing/mtype.ml>>=
<<copyright header>>

(* Operations on module types *)

open Path
open Types


<<function Mtype.scrape>>

let rec strengthen env mty p =
  match scrape env mty with
    Tmty_signature sg ->
      Tmty_signature(strengthen_sig env sg p)
  | mty ->
      mty

and strengthen_sig env sg p =
  match sg with
    [] -> []
  | (Tsig_value(id, desc) as sigelt) :: rem ->
      sigelt :: strengthen_sig env rem p
  | Tsig_type(id, decl) :: rem ->
      let newdecl =
        match decl.type_manifest with
          None ->
            { type_params = decl.type_params;
              type_arity = decl.type_arity;
              type_kind = decl.type_kind;
              type_manifest = Some(Btype.newgenty(
                                   Tconstr(Pdot(p, Ident.name id, nopos),
                                           decl.type_params,
                                           ref Mnil))) }
        | _ -> decl in
      Tsig_type(id, newdecl) :: strengthen_sig env rem p
  | (Tsig_exception(id, d) as sigelt) :: rem ->
      sigelt :: strengthen_sig env rem p
  | Tsig_module(id, mty) :: rem ->
      Tsig_module(id, strengthen env mty (Pdot(p, Ident.name id, nopos))) ::
      strengthen_sig (Env.add_module id mty env) rem p
      (* Need to add the module in case it defines manifest module types *)
@


\subsection*{[[./typing/printtyp.mli]]}

%-------------------------------------------------------------

<<./typing/printtyp.mli>>=
<<copyright header>>

(* Printing functions *)

open Types

<<signature Printtyp.longident>>
<<signature Printtyp.ident>>
<<signature Printtyp.path>>
<<signature Printtyp.reset>>
<<signature Printtyp.mark_loops>>
<<signature Printtyp.type_expr>>
<<signature Printtyp.type_scheme>>
<<signature Printtyp.value_description>>
<<signature Printtyp.type_declaration>>
<<signature Printtyp.exception_declaration>>
<<signature Printtyp.modtype>>
<<signature Printtyp.signature>>
<<signature Printtyp.signature_body>>
<<signature Printtyp.type_expansion>>
<<signature Printtyp.trace>>
<<signature Printtyp.unification_error>>
@


\subsection*{[[./typing/printtyp.ml]]}



%-------------------------------------------------------------

<<./typing/printtyp.ml>>=
<<copyright header2>>

(* Printing functions *)

open Misc
open Ctype
open Format
open Longident
open Path
open Asttypes
open Types
open Btype

<<constant Printtyp.longident>>

<<function Printtyp.ident>>

<<constant Printtyp.ident_pervasive>>

<<constant Printtyp.path>>

<<constant Printtyp.names>>
<<constant Printtyp.name_counter>>

<<function Printtyp.reset_names>>

<<function Printtyp.new_name>>

<<function Printtyp.name_of_type>>

<<function Printtyp.list_removeq>>

<<function Printtyp.remove_name_of_type>>

<<constant Printtyp.visited_objects>>
<<constant Printtyp.aliased>>

<<function Printtyp.mark_loops_rec>>

<<function Printtyp.mark_loops>>

<<function Printtyp.reset_loop_marks>>

<<function Printtyp.reset>>

<<function Printtyp.typeexp>>

<<function Printtyp.type_expr>>


and type_sch ty =
  typexp true 0 ty

and type_scheme ty =
  reset(); mark_loops ty; typexp true 0 ty

<<function Printtyp.constrain>>

<<function Printtyp.type_declaration>>

<<function Printtyp.exception_declaration>>

<<function Printtyp.value_description>>


(* Print a module type *)
<<function Printtyp.modtype>>
<<function Printtyp.signature>>

<<function Printtyp.type_expansion>>

<<function Printtyp.trace>>

<<function Printtyp.unification_error>>
@


\subsection*{[[./typing/parmatch.mli]]}


%-------------------------------------------------------------

<<./typing/parmatch.mli>>=
<<copyright header>>

(* Detection of partial matches and unused match cases. *)

open Typedtree

<<signature Parmatch.check_partial>>
<<signature Parmatch.check_unused>>
@


\subsection*{[[./typing/parmatch.ml]]}

<<function Parmatch.make_pat>>=
let make_pat desc ty =
  {pat_desc = desc; pat_loc = Location.none; pat_type = ty}
@

<<constant Parmatch.omega>>=
let omega = make_pat Tpat_any Ctype.none
@

<<function Parmatch.omegas>>=
let rec omegas i =
  if i <= 0 then [] else omega :: omegas (i-1)
@

<<function Parmatch.omega_list>>=
let omega_list l = omegas(List.length l)
@

<<function Parmatch.has_guard>>=
let has_guard act =
  match act.exp_desc with
    Texp_when(_, _) -> true
  | _ -> false
@

<<function Parmatch.simple_match>>=
let simple_match p1 p2 = 
  match p1.pat_desc, p2.pat_desc with
    Tpat_construct(c1, _), Tpat_construct(c2, _) ->
      c1.cstr_tag = c2.cstr_tag
  | Tpat_constant(c1), Tpat_constant(c2) ->
      c1 = c2
  | Tpat_tuple(_), Tpat_tuple(_) -> true
  | Tpat_record(_), Tpat_record(_) -> true
  | _, (Tpat_any | Tpat_var(_)) -> true
  | _, _ -> false
@

<<function Parmatch.record_labels>>=
(* Return the set of labels and number of fields for a record pattern. *)

let record_labels p =
  match p.pat_desc with
    Tpat_record((lbl1, pat1) :: rem) -> Array.to_list lbl1.lbl_all
  | _ -> fatal_error "Parmatch.record_labels"
@

<<function Parmatch.record_num_fields>>=
let record_num_fields p =
  match p.pat_desc with
    Tpat_record((lbl1, pat1) :: rem) -> Array.length lbl1.lbl_all
  | _ -> fatal_error "Parmatch.record_num_fields"
@

<<function Parmatch.set_fields>>=
let set_fields size l =
  let v = Array.create size omega in
  let rec change_rec l = match l with
    (lbl,p)::l ->  v.(lbl.lbl_pos) <- p ;  change_rec l 
  | [] -> () in
  change_rec l;
  Array.to_list v
@

<<function Parmatch.simple_match_args>>=
let simple_match_args p1 p2 =
  match p2.pat_desc with
    Tpat_construct(cstr, args) -> args
  | Tpat_tuple(args)  -> args
  | Tpat_record(args) ->  set_fields (record_num_fields p1) args
  | (Tpat_any | Tpat_var(_)) ->
      begin match p1.pat_desc with
        Tpat_construct(_, args) -> omega_list args
      | Tpat_tuple(args) -> omega_list args
      | Tpat_record(args) ->  omega_list args
      | _ -> []
      end
  | _ -> []
@

<<function Parmatch.simple_pat>>=
(*
  Computes the discriminating pattern for matching by the first
  column of pss, that is:
     checks for a tuple or a record when q is a variable.
*)

let rec simple_pat q pss = match pss with
    ({pat_desc = Tpat_alias(p,_)}::ps)::pss -> 
        simple_pat q ((p::ps)::pss)
  | ({pat_desc = Tpat_or(p1,p2)}::ps)::pss ->
        simple_pat q ((p1::ps)::(p2::ps)::pss)
  | ({pat_desc = (Tpat_any | Tpat_var(_))}::_)::pss ->
        simple_pat q pss
  | (({pat_desc = Tpat_tuple(args)} as p)::_)::_ ->
        make_pat (Tpat_tuple(omega_list args)) p.pat_type
  | (({pat_desc = Tpat_record(args)} as p)::_)::pss ->
        make_pat (Tpat_record (List.map (fun lbl -> (lbl,omega)) (record_labels p)))
                 p.pat_type
  | _ -> q
@

<<function Parmatch.filter_one>>=
let filter_one q pss =
  let rec filter_rec = function
      ({pat_desc = Tpat_alias(p,_)}::ps)::pss -> 
        filter_rec ((p::ps)::pss)
    | ({pat_desc = Tpat_or(p1,p2)}::ps)::pss ->
        filter_rec ((p1::ps)::(p2::ps)::pss)
    | (p::ps)::pss ->
        if simple_match q p
        then (simple_match_args q p @ ps) :: filter_rec pss
        else filter_rec pss
    | _ -> [] in
  filter_rec pss
@

<<function Parmatch.filter_extra>>=
let filter_extra pss =
  let rec filter_rec = function
      ({pat_desc = Tpat_alias(p,_)}::ps)::pss -> 
        filter_rec ((p::ps)::pss)
    | ({pat_desc = Tpat_or(p1,p2)}::ps)::pss ->
        filter_rec ((p1::ps)::(p2::ps)::pss)
    | ({pat_desc = (Tpat_any | Tpat_var(_))} :: qs) :: pss ->
        qs :: filter_rec pss
    | _::pss  -> filter_rec pss
    | [] -> [] in
  filter_rec pss
@

<<function Parmatch.filter_all>>=
let filter_all pat0 pss =

  let rec insert q qs env =
    match env with
      [] -> [q, [simple_match_args q q @ qs]]
    | ((p,pss) as c)::env ->
        if simple_match q p
        then (p, ((simple_match_args p q @ qs) :: pss)) :: env
        else c :: insert q qs env in

  let rec filter_rec env = function
    ({pat_desc = Tpat_alias(p,_)}::ps)::pss ->
      filter_rec env ((p::ps)::pss)
  | ({pat_desc = Tpat_or(p1,p2)}::ps)::pss ->
      filter_rec env ((p1::ps)::(p2::ps)::pss)
  | ({pat_desc = (Tpat_any | Tpat_var(_))}::_)::pss ->
      filter_rec env pss
  | (p::ps)::pss ->
      filter_rec (insert p ps env) pss
  | _ -> env

  and filter_omega env = function
    ({pat_desc = Tpat_alias(p,_)}::ps)::pss ->
      filter_omega env ((p::ps)::pss)
  | ({pat_desc = Tpat_or(p1,p2)}::ps)::pss ->
      filter_omega env ((p1::ps)::(p2::ps)::pss)
  | ({pat_desc = (Tpat_any | Tpat_var(_))}::ps)::pss ->
      filter_omega
        (List.map (fun (q,qss) -> (q,(simple_match_args q omega @ ps) :: qss)) env)
        pss
  | _::pss -> filter_omega env pss
  | [] -> env in
        
  filter_omega
    (filter_rec
      (match pat0.pat_desc with
        (Tpat_record(_) | Tpat_tuple(_)) -> [pat0,[]]
      | _ -> [])
      pss)
    pss
@

<<function Parmatch.full_match>>=
let full_match env =
  match env with
    ({pat_desc = Tpat_construct(c,_)},_) :: _ ->
      List.length env = c.cstr_consts + c.cstr_nonconsts
  | ({pat_desc = Tpat_constant(Const_char _)},_) :: _ ->
      List.length env = 256
  | ({pat_desc = Tpat_constant(_)},_) :: _ -> false
  | ({pat_desc = Tpat_tuple(_)},_) :: _ -> true
  | ({pat_desc = Tpat_record(_)},_) :: _ -> true
  | _ -> fatal_error "Parmatch.full_match"
@

<<function Parmatch.satisfiable>>=
(*
  Is the last row of pattern matrix pss + qs satisfiable ?
        That is :
  Does there List.exists at least one value vector, es such that :
   1/ for all ps in pss ps # es (ps and es are not compatible)
   2/ qs <= es                  (es matches qs)
*)

let rec satisfiable pss qs =
  match pss with
    [] -> true
  | _ ->
    match qs with
      [] -> false
    | {pat_desc = Tpat_or(q1,q2)}::qs ->
        satisfiable pss (q1::qs) or satisfiable pss (q2::qs)
    | {pat_desc = Tpat_alias(q,_)}::qs ->
        satisfiable pss (q::qs)
    | {pat_desc = (Tpat_any | Tpat_var(_))}::qs ->
        let q0 = simple_pat omega pss in     
        begin match filter_all q0 pss with
          (* first column of pss is made of variables only *)
          [] -> satisfiable (filter_extra pss) qs 
        | constrs ->          
            let try_non_omega (p,pss) =
              satisfiable pss (simple_match_args p omega @ qs)  in
            if full_match constrs
            then List.exists try_non_omega constrs
            else satisfiable (filter_extra pss) qs or
                 List.exists try_non_omega constrs
        end
    | q::qs ->
        let q0 = simple_pat q pss in
        satisfiable (filter_one q0 pss) (simple_match_args q0 q @ qs)
@

<<constant Parmatch.initial_matrix>>=
let rec initial_matrix = function
    [] -> []
  | (pat, act) :: rem ->
       if has_guard act
       then initial_matrix rem
       else [pat] :: initial_matrix rem
@

<<function Parmatch.get_mins>>=
let get_mins ps =
  let rec select_rec r = function
    [] -> r
  | p::ps ->
      if List.exists (fun p0 -> le_pats p0 p) ps
      then select_rec r ps
      else select_rec (p::r) ps in
  select_rec [] (select_rec [] ps)
@

<<function Parmatch.check_partial>>=
let check_partial loc casel =
  let pss = get_mins (initial_matrix casel) in
  if match pss with
      []     -> if casel = [] then false else true
    | ps::_  -> satisfiable pss (List.map (fun _ -> omega) ps)
  then Location.print_warning loc "this pattern-matching is not exhaustive"
@

<<constant Parmatch.location_of_clause>>=
let location_of_clause = function
    pat :: _ -> pat.pat_loc
  | _ -> fatal_error "Parmatch.location_of_clause"
@

<<function Parmatch.check_unused>>=
let check_unused casel =
  let prefs =   
    List.fold_right
      (fun (pat,act as clause) r ->
         if has_guard act
         then ([], ([pat], act)) :: r
         else ([], ([pat], act)) :: 
              List.map (fun (pss,clause) -> [pat]::pss,clause) r)
      casel [] in
  List.iter
    (fun (pss, ((qs, _) as clause)) ->
      if not (satisfiable pss qs) then
        Location.print_warning (location_of_clause qs)
                                "this match case is unused.")
    prefs
@


%-------------------------------------------------------------

<<./typing/parmatch.ml>>=
<<copyright header>>

(* Detection of partial matches and unused match cases. *)

open Misc
open Asttypes
open Types
open Typedtree

<<function Parmatch.make_pat>>

<<constant Parmatch.omega>>

<<function Parmatch.omegas>>

<<function Parmatch.omega_list>>

<<function Parmatch.has_guard>>

<<function Parmatch.simple_match>>

<<function Parmatch.record_labels>>

<<function Parmatch.record_num_fields>>

<<function Parmatch.set_fields>>

<<function Parmatch.simple_match_args>>

<<function Parmatch.simple_pat>>

<<function Parmatch.filter_one>>

<<function Parmatch.filter_extra>>

<<function Parmatch.filter_all>>

      
<<function Parmatch.full_match>>

<<function Parmatch.satisfiable>>

<<constant Parmatch.initial_matrix>>

let rec le_pat p q =
  match (p.pat_desc, q.pat_desc) with
    (Tpat_var _ | Tpat_any), _ -> true
  | Tpat_alias(p,_), _ -> le_pat p q
  | _, Tpat_alias(q,_) -> le_pat p q
  | Tpat_or(p1,p2), _ -> le_pat p1 q or le_pat p2 q
  | _, Tpat_or(q1,q2) -> le_pat p q1 & le_pat p q2
  | Tpat_constant(c1), Tpat_constant(c2) -> c1 = c2
  | Tpat_construct(c1,ps), Tpat_construct(c2,qs) ->
      c1.cstr_tag = c2.cstr_tag & le_pats ps qs
  | Tpat_tuple(ps), Tpat_tuple(qs) -> le_pats ps qs
  | Tpat_record(l1), Tpat_record(l2) ->
     let size = record_num_fields p in
     le_pats (set_fields size l1) (set_fields size l2)
  | _, _ -> false  

and le_pats ps qs =
  match ps,qs with
    p::ps, q::qs -> le_pat p q & le_pats ps qs
  | _, _         -> true

<<function Parmatch.get_mins>>

<<function Parmatch.check_partial>>

<<constant Parmatch.location_of_clause>>

<<function Parmatch.check_unused>>
@


\subsection*{[[./typing/typetexp.mli]]}


<<signature Typetexp.transl_simple_type_delayed>>=
val transl_simple_type_delayed:
        Env.t -> Parsetree.core_type -> Types.type_expr * (unit -> unit)
        (* Translate a type, but leave type variables unbound. Returns
           the type and a function that binds the type variable. *)
@

<<signature Typetexp.transl_type_scheme>>=
val transl_type_scheme:
        Env.t -> Parsetree.core_type -> Types.type_expr
@

<<signature Typetexp.reset_type_variables>>=
val reset_type_variables: unit -> unit
@

<<signature Typetexp.enter_type_variable>>=
val enter_type_variable: bool -> string -> Types.type_expr
@

<<signature Typetexp.type_variable>>=
val type_variable : Location.t -> string -> Types.type_expr
@

<<exception Typetexp.Already_bound>>=
exception Already_bound
@



%-------------------------------------------------------------

<<./typing/typetexp.mli>>=
<<copyright header>>

<<signature Typetexp.transl_simple_type>>
<<signature Typetexp.transl_simple_type_delayed>>
<<signature Typetexp.transl_type_scheme>>
<<signature Typetexp.reset_type_variables>>
<<signature Typetexp.enter_type_variable>>
<<signature Typetexp.type_variable>>

<<exception Typetexp.Already_bound>>

<<type Typetexp.error>>

<<exception Typetexp.Error>>

<<signature Typetexp.report_error>>
@


\subsection*{[[./typing/typetexp.ml]]}


<<constant Typetexp.type_variables>>=
(* Translation of type expressions *)

let type_variables = ref (Tbl.empty : (string, type_expr) Tbl.t)
@

<<constant Typetexp.aliases>>=
let aliases        = ref (Tbl.empty : (string, type_expr) Tbl.t)
@

<<constant Typetexp.used_variables>>=
let used_variables = ref (Tbl.empty : (string, type_expr) Tbl.t)
@

<<constant Typetexp.bindings>>=
let bindings       = ref ([] : (type_expr * type_expr) list)
        (* These two variables are used for the "delayed" policy. *)
@

<<function Typetexp.reset_type_variables>>=
let reset_type_variables () =
  reset_global_level ();
  type_variables := Tbl.empty
@

<<function Typetexp.enter_type_variable>>=
let enter_type_variable strict name =
  try
    let v = Tbl.find name !type_variables in
    if strict then raise Already_bound;
    v
  with Not_found ->
    let v = new_global_var() in
    type_variables := Tbl.add name v !type_variables;
    v
@

<<function Typetexp.type_variable>>=
let type_variable loc name =
  try
    Tbl.find name !type_variables
  with Not_found ->
    raise(Error(loc, Unbound_type_variable name))
@

<<type Typetexp.policy>>=
type policy = Fixed | Extensible | Delayed
@

<<function Typetexp.transl_type>>=
let rec transl_type env policy styp =
  match styp.ptyp_desc with
    Ptyp_any -> new_global_var()
  | Ptyp_var name ->
      begin try Tbl.find name !aliases with Not_found ->
        match policy with
          Fixed ->
            begin try
              Tbl.find name !type_variables
            with Not_found ->
              raise(Error(styp.ptyp_loc, Unbound_type_variable name))
            end
        | Extensible ->
            begin try
              Tbl.find name !type_variables
            with Not_found ->
              let v = new_global_var () in
              type_variables := Tbl.add name v !type_variables;
              v
            end
        | Delayed ->
            begin try
              Tbl.find name !used_variables
            with Not_found -> try
              let v1 = Tbl.find name !type_variables in
              let v2 = new_global_var () in
              used_variables := Tbl.add name v2 !used_variables;
              bindings := (v1, v2)::!bindings;
              v2
            with Not_found ->
              let v = new_global_var () in
              type_variables := Tbl.add name v !type_variables;
              used_variables := Tbl.add name v !used_variables;
              v
            end
      end
  | Ptyp_arrow(st1, st2) ->
      let ty1 = transl_type env policy st1 in
      let ty2 = transl_type env policy st2 in
      newty (Tarrow(ty1, ty2))
  | Ptyp_tuple stl ->
      newty (Ttuple(List.map (transl_type env policy) stl))
  | Ptyp_constr(lid, stl) ->
      let (path, decl) =
        try
          Env.lookup_type lid env
        with Not_found ->
          raise(Error(styp.ptyp_loc, Unbound_type_constructor lid)) in
      if List.length stl <> decl.type_arity then
        raise(Error(styp.ptyp_loc, Type_arity_mismatch(lid, decl.type_arity,
                                                           List.length stl)));
      let args = List.map (transl_type env policy) stl in
      let cstr = newty (Tconstr(path, args, ref Mnil)) in
      let params = Ctype.instance_list decl.type_params in
      List.iter2
        (fun (sty, ty) ty' ->
           try unify env ty ty' with Unify trace ->
             raise (Error(sty.ptyp_loc, Type_mismatch trace)))
        (List.combine stl args) params;
      cstr

  | Ptyp_alias(st, alias) ->
      begin try
        Tbl.find alias !type_variables;
        raise(Error(styp.ptyp_loc, Bound_type_variable alias))
      with Not_found -> try
        Tbl.find alias !aliases;
        raise(Error(styp.ptyp_loc, Bound_type_variable alias))
      with Not_found ->
        let ty' = newvar () in
        aliases := Tbl.add alias ty' !aliases;
        let ty = transl_type env policy st in
        begin try unify env ty ty' with Unify trace ->
          raise(Error(styp.ptyp_loc, Alias_type_mismatch trace))
        end;
        ty
      end 
@

<<function Typetexp.transl_simple_type>>=
let transl_simple_type env fixed styp =
  aliases := Tbl.empty;
  let typ = transl_type env (if fixed then Fixed else Extensible) styp in
  aliases := Tbl.empty;
  typ
@

<<function Typetexp.transl_simple_type_delayed>>=
let transl_simple_type_delayed env styp =
  aliases := Tbl.empty;
  used_variables := Tbl.empty;
  bindings := [];
  let typ = transl_type env Delayed styp in
  let b = !bindings in
  aliases := Tbl.empty;
  used_variables := Tbl.empty;
  bindings := [];
  (typ,
   function () -> List.iter (function (t1, t2) -> unify env t1 t2) b)
@

<<function Typetexp.transl_type_scheme>>=
let transl_type_scheme env styp =
  reset_type_variables();
  begin_def();
  let typ = transl_simple_type env false styp in
  end_def();
  generalize typ;
  typ
@



%-------------------------------------------------------------

<<./typing/typetexp.ml>>=
<<copyright header>>

(* Typechecking of type expressions for the core language *)

open Misc
open Parsetree
open Types
open Ctype

<<exception Typetexp.Already_bound>>

<<type Typetexp.error>>

<<exception Typetexp.Error>>

<<constant Typetexp.type_variables>>
<<constant Typetexp.aliases>>

<<constant Typetexp.used_variables>>
<<constant Typetexp.bindings>>

<<function Typetexp.reset_type_variables>>

<<function Typetexp.enter_type_variable>>

<<function Typetexp.type_variable>>

<<type Typetexp.policy>>

<<function Typetexp.transl_type>>

<<function Typetexp.transl_simple_type>>

<<function Typetexp.transl_simple_type_delayed>>

<<function Typetexp.transl_type_scheme>>

(* Error report *)

open Format
open Printtyp

<<constant Typetexp.report_error>>
@


\subsection*{[[./typing/includemod.mli]]}

<<signature Includemod.modtypes>>=
val modtypes: Env.t -> module_type -> module_type -> module_coercion
@

<<signature Includemod.signatures>>=
val signatures: Env.t -> signature -> signature -> module_coercion
@

<<signature Includemod.compunit>>=
val compunit: string -> signature -> string -> signature -> module_coercion
@

<<signature Includemod.type_declarations>>=
val type_declarations:
      Env.t -> Ident.t -> type_declaration -> type_declaration -> unit
@



%-------------------------------------------------------------

<<./typing/includemod.mli>>=
<<copyright header>>

(* Inclusion checks for the module language *)

open Types
open Typedtree

<<signature Includemod.modtypes>>
<<signature Includemod.signatures>>
<<signature Includemod.compunit>>
<<signature Includemod.type_declarations>>

<<type Includemod.error>>

<<exception Includemod.Error>>

<<signature Includemod.report_error>>
@


\subsection*{[[./typing/includemod.ml]]}

<<function Includemod.value_descriptions>>=
(* Inclusion between value descriptions *)

let value_descriptions env subst id vd1 vd2 =
  let vd2 = Subst.value_description subst vd2 in
  try
    Includecore.value_descriptions env vd1 vd2
  with Includecore.Dont_match ->
    raise(Error[Value_descriptions(id, vd1, vd2)])
@

<<function Includemod.type_declarations>>=
(* Inclusion between type declarations *)

let type_declarations env subst id decl1 decl2 =
  let decl2 = Subst.type_declaration subst decl2 in
  if Includecore.type_declarations env id decl1 decl2
  then ()
  else raise(Error[Type_declarations(id, decl1, decl2)])
@

<<function Includemod.exception_declarations>>=
(* Inclusion between exception declarations *)

let exception_declarations env subst id decl1 decl2 =
  let decl2 = Subst.exception_declaration subst decl2 in
  if Includecore.exception_declarations env decl1 decl2
  then ()
  else raise(Error[Exception_declarations(id, decl1, decl2)])
@

<<exception Includemod.Dont_match>>=
(* Expand a module type identifier when possible *)

exception Dont_match
@

<<function Includemod.expand_module_path>>=
let expand_module_path env path =
  try
    failwith "TODO: find_modtype_expansion"
(*    Env.find_modtype_expansion path env *)
  with Not_found ->
    raise Dont_match
@

<<type Includemod.field_desc>>=
(* Extract name, kind and ident from a signature item *)

type field_desc =
    Field_value of string
  | Field_type of string
  | Field_exception of string
  | Field_module of string
  | Field_modtype of string
@

<<constant Includemod.item_ident_name>>=
let item_ident_name = function
    Tsig_value(id, _) -> (id, Field_value(Ident.name id))
  | Tsig_type(id, _) -> (id, Field_type(Ident.name id))
  | Tsig_exception(id, _) -> (id, Field_exception(Ident.name id))
  | Tsig_module(id, _) -> (id, Field_module(Ident.name id))
@

<<function Includemod.simplify_structure_coercion>>=
(* Simplify a structure coercion *)

let simplify_structure_coercion cc =
  let pos = ref 0 in
  try
    List.iter
      (fun (n, c) ->
        if n <> !pos or c <> Tcoerce_none then raise Exit;
        incr pos)
      cc;
    Tcoerce_none
  with Exit ->
    Tcoerce_structure cc
@

<<function Includemod.check_modtype_inclusion>>=
(* Simplified inclusion check between module types *)

let check_modtype_inclusion env mty1 mty2 =
  try
    modtypes env Subst.identity mty1 mty2; ()
  with Error reasons ->
    raise Not_found
@

<<toplevel Includemod._1>>=
let _ = Env.check_modtype_inclusion := check_modtype_inclusion
@

<<function Includemod.compunit>>=
(* Check that an implementation of a compilation unit meets its
   interface. *)

let compunit impl_name impl_sig intf_name intf_sig =
  try
    signatures Env.initial Subst.identity impl_sig intf_sig
  with Error reasons ->
    raise(Error(Interface_mismatch(impl_name, intf_name) :: reasons))
@

<<function Includemod.modtypes>>=
(* Hide the substitution parameter to the outside world *)

let modtypes env mty1 mty2 = modtypes env Subst.identity mty1 mty2
@

<<function Includemod.signatures>>=
let signatures env sig1 sig2 = signatures env Subst.identity sig1 sig2
@

<<function Includemod.type_declarations (./typing/includemod.ml)>>=
let type_declarations env id decl1 decl2 =
  type_declarations env Subst.identity id decl1 decl2
@



%-------------------------------------------------------------

<<./typing/includemod.ml>>=
<<copyright header>>

(* Inclusion checks for the module language *)

open Misc
open Path
open Types
open Typedtree

<<type Includemod.error>>

<<exception Includemod.Error>>

(* All functions "blah env x1 x2" check that x1 is included in x2,
   i.e. that x1 is the type of an implementation that fulfills the
   specification x2. If not, Error is raised with a backtrace of the error. *)

<<function Includemod.value_descriptions>>

<<function Includemod.type_declarations>>

<<function Includemod.exception_declarations>>

<<exception Includemod.Dont_match>>

<<function Includemod.expand_module_path>>

<<type Includemod.field_desc>>

<<constant Includemod.item_ident_name>>

<<function Includemod.simplify_structure_coercion>>

(* Inclusion between module types. 
   Return the restriction that transforms a value of the smaller type
   into a value of the bigger type. *)

let rec modtypes env subst mty1 mty2 =
  try
    try_modtypes env subst mty1 mty2
  with 
    Dont_match ->
      raise(Error[Module_types(mty1, Subst.modtype subst mty2)])
  | Error reasons ->
      raise(Error(Module_types(mty1, Subst.modtype subst mty2) :: reasons))

and try_modtypes env subst mty1 mty2 =
  match (mty1, mty2) with
    (_, Tmty_ident p2) ->
      try_modtypes2 env mty1 (Subst.modtype subst mty2)
  | (Tmty_ident p1, _) ->
      try_modtypes env subst (expand_module_path env p1) mty2
  | (Tmty_signature sig1, Tmty_signature sig2) ->
      signatures env subst sig1 sig2
(*
  | (_, _) ->
      raise Dont_match
*)

and try_modtypes2 env mty1 mty2 =
  (* mty2 is an identifier *)
  match (mty1, mty2) with
    (Tmty_ident p1, Tmty_ident p2) when Path.same p1 p2 ->
      Tcoerce_none
  | (_, Tmty_ident p2) ->
      try_modtypes env Subst.identity mty1 (expand_module_path env p2)
  | (_, _) ->
      fatal_error "Includemod.try_modtypes2"

(* Inclusion between signatures *)

and signatures env subst sig1 sig2 =
  (* Environment used to check inclusion of components *)
  let new_env =
    Env.add_signature sig1 env in
  (* Build a table of the components of sig1, along with their positions.
     The table is indexed by kind and name of component *)
  let rec build_component_table pos tbl = function
      [] -> tbl
    | item :: rem ->
        let (id, name) = item_ident_name item in
        let nextpos =
          match item with
            Tsig_value(_,{val_kind = Val_prim _})
          | Tsig_type(_,_)
           -> pos
          | Tsig_value(_,_)
          | Tsig_exception(_,_)
          | Tsig_module(_,_)
           -> pos+1 
        in
        build_component_table nextpos
                              (Tbl.add name (id, item, pos) tbl) rem in
  let comps1 =
    build_component_table 0 Tbl.empty sig1 in
  (* Pair each component of sig2 with a component of sig1,
     identifying the names along the way.
     Return a coercion list indicating, for all run-time components
     of sig2, the position of the matching run-time components of sig1
     and the coercion to be applied to it. *)
  let rec pair_components subst paired unpaired = function
      [] ->
        begin match unpaired with
            [] -> signature_components new_env subst (List.rev paired)
          | _  -> raise(Error unpaired)
        end
    | item2 :: rem ->
        let (id2, name2) = item_ident_name item2 in
        begin try
          let (id1, item1, pos1) = Tbl.find name2 comps1 in
          let new_subst =
            match item2 with
              Tsig_type _ ->
                Subst.add_type id2 (Pident id1) subst
            | Tsig_module _ ->
                Subst.add_module id2 (Pident id1) subst
            | Tsig_value _ | Tsig_exception _  ->
                subst
          in
          pair_components new_subst
            ((item1, item2, pos1) :: paired) unpaired rem
        with Not_found ->
          pair_components subst paired (Missing_field id2 :: unpaired) rem
        end in
  (* Do the pairing and checking, and return the final coercion *)
  simplify_structure_coercion(pair_components subst [] [] sig2)

(* Inclusion between signature components *)

and signature_components env subst = function
    [] -> []
  | (Tsig_value(id1, valdecl1), Tsig_value(id2, valdecl2), pos) :: rem ->
      let cc = value_descriptions env subst id1 valdecl1 valdecl2 in
      begin match valdecl2.val_kind with
        Val_prim p -> signature_components env subst rem
      | _ -> (pos, cc) :: signature_components env subst rem
      end
  | (Tsig_type(id1, tydecl1), Tsig_type(id2, tydecl2), pos) :: rem ->
      type_declarations env subst id1 tydecl1 tydecl2;
      signature_components env subst rem
  | (Tsig_exception(id1, excdecl1), Tsig_exception(id2, excdecl2), pos)
    :: rem ->
      exception_declarations env subst id1 excdecl1 excdecl2;
      (pos, Tcoerce_none) :: signature_components env subst rem
  | (Tsig_module(id1, mty1), Tsig_module(id2, mty2), pos) :: rem ->
      let cc = modtypes env subst mty1 mty2 in
      (pos, cc) :: signature_components env subst rem
  | _ ->
      fatal_error "Includemod.signature_components"

<<function Includemod.check_modtype_inclusion>>

<<toplevel Includemod._1>>

<<function Includemod.compunit>>

<<function Includemod.modtypes>>
<<function Includemod.signatures>>
<<function Includemod.type_declarations (./typing/includemod.ml)>>

(* Error report *)

open Format
open Printtyp

<<constant Includemod.include_err>>

<<function Includemod.report_error>>
@


\subsection*{[[./typing/typedecl.mli]]}


%-------------------------------------------------------------

<<./typing/typedecl.mli>>=
<<copyright header>>

(* Typing of type definitions and primitive definitions *)

open Types

<<signature Typedecl.transl_type_decl>>
<<signature Typedecl.transl_exception>>

<<signature Typedecl.transl_value_decl>>

<<type Typedecl.error>>

<<exception Typedecl.Error>>

<<signature Typedecl.report_error>>
@


\subsection*{[[./typing/typedecl.ml]]}

<<function Typedecl.enter_types>>=
(* Enter all declared types in the environment as abstract types *)

let rec enter_types env = function
    ([], []) ->
      (env, [])
  | ((name, sdecl) :: srem, id :: irem) ->
      let decl =
        { type_params = List.map (fun _ -> Ctype.newvar ()) sdecl.ptype_params;
          type_arity = List.length sdecl.ptype_params;
          type_kind = Type_abstract;
          type_manifest =
            match sdecl.ptype_manifest with
              None -> None
            | Some _ -> Some (Ctype.newvar ()) }
      in
      let extenv = Env.add_type id decl env in
      let (ext_env, decl_rem) = enter_types extenv (srem, irem) in
      (ext_env, (id, decl) :: decl_rem)
  | _ ->
      fatal_error "Typedecl.enter_types"
@

<<function Typedecl.transl_declaration>>=
(* First pass: parameters, constraints and expansion *)
let transl_declaration env (name, sdecl) (id, decl) =
  reset_type_variables();
  begin try
    List.iter2
      (fun ty sty -> Ctype.unify env ty (enter_type_variable true sty))
      decl.type_params sdecl.ptype_params
  with Already_bound ->
    raise(Error(sdecl.ptype_loc, Repeated_parameter))
  end;

  let cstr_params =
    List.map (function (v, _, loc) -> type_variable loc v) sdecl.ptype_cstrs
  in
  List.iter2
    (fun (v, sty, loc) ty' ->
       try
         Ctype.unify env (transl_simple_type env false sty) ty'
       with Ctype.Unify _ ->
         raise(Error(loc, Unconsistent_constraint)))
    sdecl.ptype_cstrs cstr_params;

  let decl' =
    { type_params = decl.type_params;
      type_arity = decl.type_arity;
      type_kind =
        Type_abstract;
      type_manifest =
        begin match (decl.type_manifest, sdecl.ptype_manifest) with
          (None, None) -> None
        | (Some ty, Some sty) ->
            let ty' =
              Ctype.unroll_abbrev id decl.type_params
                (transl_simple_type env true sty)
            in
            if Ctype.cyclic_abbrev env id ty' then
              raise(Error(sdecl.ptype_loc, Recursive_abbrev name));
            begin try Ctype.unify env ty' ty with Ctype.Unify trace ->
              raise(Error(sdecl.ptype_loc, Type_clash trace))
            end;
            Some ty
        | _ ->
            fatal_error "Typedecl.transl_declaration"
        end } in
  (id, decl')
@

<<function Typedecl.transl_declaration2>>=
(* Second pass: representation *)
let transl_declaration2 env (name, sdecl) (id, decl) =
  let (params, typ) =
    match decl.type_manifest with
      None -> (Ctype.instance_list decl.type_params, None)
    | Some typ ->
        let (params, typ) =
          Ctype.instance_parameterized_type decl.type_params typ
        in
        (params, Some typ)
  in

  (* Bind type parameters *)
  reset_type_variables();
  List.iter2
    (fun ty sty -> Ctype.unify env ty (enter_type_variable true sty))
    params sdecl.ptype_params;

  let decl' =
    { type_params = params;
      type_arity = decl.type_arity;
      type_kind =
        begin match sdecl.ptype_kind with
          Ptype_abstract ->
            Type_abstract
        | Ptype_variant cstrs ->
            let all_constrs = ref StringSet.empty in
            List.iter
              (fun (name, args) ->
                if StringSet.mem name !all_constrs then
                  raise(Error(sdecl.ptype_loc, Duplicate_constructor name));
                all_constrs := StringSet.add name !all_constrs)
              cstrs;
            if List.length cstrs > Config.max_tag then
              raise(Error(sdecl.ptype_loc, Too_many_constructors));
            Type_variant(List.map
              (fun (name, args) ->
                      (name, List.map (transl_simple_type env true) args))
              cstrs)
        | Ptype_record lbls ->
            let all_labels = ref StringSet.empty in
            List.iter
              (fun (name, mut, arg) ->
                if StringSet.mem name !all_labels then
                  raise(Error(sdecl.ptype_loc, Duplicate_label name));
                all_labels := StringSet.add name !all_labels)
              lbls;
            Type_record(List.map
              (fun (name, mut, arg) ->
                      (name, mut, transl_simple_type env true arg))
              lbls)
        end;
      type_manifest = typ } in
  (id, decl')
@

<<function Typedecl.generalize_decl>>=
(* Generalize a type declaration *)

let generalize_decl decl =
  List.iter Ctype.generalize decl.type_params;
  begin match decl.type_kind with
    Type_abstract ->
      ()
  | Type_variant v ->
      List.iter (fun (_, tyl) -> List.iter Ctype.generalize tyl) v
  | Type_record r ->
      List.iter (fun (_, _, ty) -> Ctype.generalize ty) r
  end;
  begin match decl.type_manifest with
    None    -> ()
  | Some ty -> Ctype.generalize ty
  end
@

<<function Typedecl.check_abbrev>>=
(*
   If both a variant/record definition and a type equation are given,
   need to check that the equation refers to a type of the same kind
   with the same constructors and labels.
*)
let check_abbrev env (_, sdecl) (id, decl) =
  match decl with
    {type_kind = (Type_variant _ | Type_record _); type_manifest = Some ty} ->
      begin match (Ctype.repr ty).desc with
        Tconstr(path, args, _) ->
          begin try
            let decl' = Env.find_type path env in
            if
              List.length args = List.length decl.type_params
                      &&
              Ctype.equal env false args decl.type_params
                      &&
              Includecore.type_declarations env id
                decl'
                (Subst.type_declaration (Subst.add_type id path Subst.identity)
                                        decl)
            then ()
            else raise(Error(sdecl.ptype_loc, Definition_mismatch ty))
          with Not_found ->
            raise(Error(sdecl.ptype_loc, Definition_mismatch ty))
          end
      | _ -> raise(Error(sdecl.ptype_loc, Definition_mismatch ty))
      end
  | _ -> ()
@

<<function Typedecl.check_recursive_abbrev>>=
(* Occur check *)
let check_recursive_abbrev env (name, sdecl) (id, decl) =
  match decl.type_manifest with
    Some ty ->
      begin try Ctype.correct_abbrev env id decl.type_params ty with
        Ctype.Recursive_abbrev ->
          raise(Error(sdecl.ptype_loc, Recursive_abbrev name))
      end
  | _ ->
      ()
@

<<function Typedecl.transl_type_decl>>=
(* Translate a set of mutually recursive type declarations *)
let transl_type_decl env name_sdecl_list =
  (* Create identifiers. *)
  let id_list =
    List.map (fun (name, _) -> Ident.create name) name_sdecl_list
  in
  (*
     Since we've introduced fresh idents, make sure the definition
     level is at least the binding time of these events. Otherwise,
     passing one of the recursively-defined type constrs as argument
     to an abbreviation may fail.
  *)
  Ctype.init_def(Ident.current_time());
  Ctype.begin_def();
  (* Enter types. *)
  let (temp_env, temp_decl) = enter_types env (name_sdecl_list, id_list) in
  (* Translate each declaration. *)
  let decls =
    List.map2 (transl_declaration temp_env) name_sdecl_list temp_decl in
  (* Generalize intermediate type declarations. *)
  Ctype.end_def();
  List.iter (function (_, decl) -> generalize_decl decl) decls;
  (* Build an env. containing type expansions *)
  let temp_env =
    List.fold_right
      (fun (id, decl) env -> Env.add_type id decl env)
      decls env
  in
  (* Check for recursive abbrevs *)
  List.iter2 (check_recursive_abbrev temp_env) name_sdecl_list decls;
  Ctype.begin_def();
  let decls =
    List.map2 (transl_declaration2 temp_env) name_sdecl_list decls in
  (* Generalize final type declarations. *)
  Ctype.end_def();
  List.iter (function (_, decl) -> generalize_decl decl) decls;
  (* Build the final env. *)
  let newenv =
    List.fold_right
      (fun (id, decl) env -> Env.add_type id decl env)
      decls env
  in
  (* Check re-exportation *)
  List.iter2 (check_abbrev newenv) name_sdecl_list decls;
  (* Done *)
  (decls, newenv)
@

<<function Typedecl.transl_exception>>=
(* Translate an exception declaration *)
let transl_exception env excdecl =
  reset_type_variables();
  Ctype.begin_def();
  let types = List.map (transl_simple_type env true) excdecl in
  Ctype.end_def();
  List.iter Ctype.generalize types;
  types
@

<<function Typedecl.transl_value_decl>>=
(* Translate a value declaration *)
let transl_value_decl env valdecl =
  let ty = Typetexp.transl_type_scheme env valdecl.pval_type in
  match valdecl.pval_prim with
    [] ->
      { val_type = ty; val_kind = Val_reg }
  | decl ->
      let arity = Ctype.arity ty in
      if arity = 0 then
        raise(Error(valdecl.pval_type.ptyp_loc, Null_arity_external));
      let prim = Primitive.parse_declaration arity decl in
      { val_type = ty; val_kind = Val_prim prim }
@


%-------------------------------------------------------------

<<./typing/typedecl.ml>>=
<<copyright header2>>

(**** Typing of type definitions ****)

open Misc
open Parsetree
open Types
open Typedtree
open Typetexp

<<type Typedecl.error>>

<<exception Typedecl.Error>>

<<function Typedecl.enter_types>>

(* Translate one type declaration *)

module StringSet = Set

<<function Typedecl.transl_declaration>>

<<function Typedecl.transl_declaration2>>

<<function Typedecl.generalize_decl>>

<<function Typedecl.check_abbrev>>

(* Check for ill-defined abbrevs *)

<<function Typedecl.check_recursive_abbrev>>

<<function Typedecl.transl_type_decl>>

<<function Typedecl.transl_exception>>

<<function Typedecl.transl_value_decl>>

(**** Error report ****)

open Format

<<constant Typedecl.report_error>>
@


\subsection*{[[./typing/typecore.mli]]}


%-------------------------------------------------------------

<<./typing/typecore.mli>>=
<<copyright header>>

(* Type inference for the core language *)

open Asttypes
open Types

<<signature Typecore.type_binding>>
<<signature Typecore.type_expression>>
<<signature Typecore.type_pattern_list>>
<<signature Typecore.type_expect>>
<<signature Typecore.type_exp>>

<<type Typecore.error>>

<<exception Typecore.Error>>

<<signature Typecore.report_error>>
@


\subsection*{[[./typing/typecore.ml]]}



<<function Typecore.unify_pat>>=
(* Typing of patterns *)

let unify_pat env pat expected_ty =
  try
    unify env pat.pat_type expected_ty
  with Unify trace ->
    raise(Error(pat.pat_loc, Pattern_type_clash(trace)))
@

<<constant Typecore.pattern_variables>>=
let pattern_variables = ref ([]: (Ident.t * type_expr) list)
@

<<function Typecore.enter_variable>>=
let enter_variable loc name ty =
  if List.exists (fun (id, ty) -> Ident.name id = name) !pattern_variables
  then raise(Error(loc, Multiply_bound_variable));
  let id = Ident.create name in
  pattern_variables := (id, ty) :: !pattern_variables;
  id
@

<<function Typecore.type_pat>>=
let rec type_pat env sp =
  match sp.ppat_desc with
    Ppat_any ->
      { pat_desc = Tpat_any;
        pat_loc = sp.ppat_loc;
        pat_type = newvar() }
  | Ppat_var name ->
      let ty = newvar() in
      let id = enter_variable sp.ppat_loc name ty in
      { pat_desc = Tpat_var id;
        pat_loc = sp.ppat_loc;
        pat_type = ty }
  | Ppat_alias(sp, name) ->
      let p = type_pat env sp in
      let id = enter_variable sp.ppat_loc name p.pat_type in
      { pat_desc = Tpat_alias(p, id);
        pat_loc = sp.ppat_loc;
        pat_type = p.pat_type }
  | Ppat_constant cst ->
      { pat_desc = Tpat_constant cst;
        pat_loc = sp.ppat_loc;
        pat_type = type_constant cst }
  | Ppat_tuple spl ->
      let pl = List.map (type_pat env) spl in
      { pat_desc = Tpat_tuple pl;
        pat_loc = sp.ppat_loc;
        pat_type = newty (Ttuple(List.map (fun p -> p.pat_type) pl)) }
  | Ppat_construct(lid, sarg, explicit_arity) ->
      let constr =
        try
          Env.lookup_constructor lid env
        with Not_found ->
          raise(Error(sp.ppat_loc, Unbound_constructor lid)) in
      let sargs =
        match sarg with
          None -> []
        | Some {ppat_desc = Ppat_tuple spl} when explicit_arity -> spl
        | Some {ppat_desc = Ppat_tuple spl} when constr.cstr_arity > 1 -> spl
        | Some({ppat_desc = Ppat_any} as sp) when constr.cstr_arity > 1 ->
            replicate_list sp constr.cstr_arity
        | Some sp -> [sp] in
      if List.length sargs <> constr.cstr_arity then
        raise(Error(sp.ppat_loc, Constructor_arity_mismatch(lid,
                                     constr.cstr_arity, List.length sargs)));
      let args = List.map (type_pat env) sargs in
      let (ty_args, ty_res) = instance_constructor constr in
      List.iter2 (unify_pat env) args ty_args;
      { pat_desc = Tpat_construct(constr, args);
        pat_loc = sp.ppat_loc;
        pat_type = ty_res }
  | Ppat_record lid_sp_list ->
      let ty = newvar() in
      let type_label_pat (lid, sarg) =
        let label =
          try
            Env.lookup_label lid env
          with Not_found ->
            raise(Error(sp.ppat_loc, Unbound_label lid)) in
        let (ty_arg, ty_res) = instance_label label in
        begin try
          unify env ty_res ty
        with Unify trace ->
          raise(Error(sp.ppat_loc, Label_mismatch(lid, trace)))
        end;
        let arg = type_pat env sarg in
        unify_pat env arg ty_arg;
        (label, arg)
      in
      { pat_desc = Tpat_record(List.map type_label_pat lid_sp_list);
        pat_loc = sp.ppat_loc;
        pat_type = ty }
  | Ppat_or(sp1, sp2) ->
      let initial_pattern_variables = !pattern_variables in
      let p1 = type_pat env sp1 in
      let p2 = type_pat env sp2 in
      if !pattern_variables != initial_pattern_variables then
        raise(Error(sp.ppat_loc, Orpat_not_closed));
      unify_pat env p2 p1.pat_type;
      { pat_desc = Tpat_or(p1, p2);
        pat_loc = sp.ppat_loc;
        pat_type = p1.pat_type }
  | Ppat_constraint(sp, sty) ->
      let p = type_pat env sp in
      let ty = Typetexp.transl_simple_type env false sty in
      unify_pat env p ty;
      p
@

<<function Typecore.add_pattern_variables>>=
let add_pattern_variables env =
  let pv = !pattern_variables in
  pattern_variables := [];
  List.fold_right
    (fun (id, ty) env ->
      Env.add_value id {val_type = ty; val_kind = Val_reg} env)
    pv env
@

<<function Typecore.type_pattern>>=
let type_pattern env spat =
  pattern_variables := [];
  let pat = type_pat env spat in
  let new_env = add_pattern_variables env in
  (pat, new_env)
@

<<function Typecore.type_pattern_list>>=
let type_pattern_list env spatl =
  pattern_variables := [];
  let patl = List.map (type_pat env) spatl in
  let new_env = add_pattern_variables env in
  (patl, new_env)
@

<<function Typecore.iter_pattern>>=
let rec iter_pattern f p =
  f p;
  match p.pat_desc with
    Tpat_any | Tpat_var _ | Tpat_constant _ ->
      ()
  | Tpat_alias (p, _) ->
      iter_pattern f p
  | Tpat_tuple pl ->
      List.iter (iter_pattern f) pl
  | Tpat_construct (_, pl) ->
      List.iter (iter_pattern f) pl
  | Tpat_record fl ->
      List.iter (fun (_, p) -> iter_pattern f p) fl
  | Tpat_or (p, p') ->
      iter_pattern f p;
      iter_pattern f p'
@

<<function Typecore.is_nonexpansive>>=
(* Generalization criterion for expressions *)

let rec is_nonexpansive exp =
  match exp.exp_desc with
    Texp_ident(_,_) -> true
  | Texp_constant _ -> true
  | Texp_let(rec_flag, pat_exp_list, body) ->
      List.for_all (fun (pat, exp) -> is_nonexpansive exp) pat_exp_list &
      is_nonexpansive body
  | Texp_function _ -> true
  | Texp_tuple el ->
      List.for_all is_nonexpansive el
  | Texp_construct(_, el) ->
      List.for_all is_nonexpansive el
  | Texp_record lbl_exp_list ->
      List.for_all
        (fun (lbl, exp) -> lbl.lbl_mut = Immutable & is_nonexpansive exp)
        lbl_exp_list
  | Texp_field(exp, lbl) -> is_nonexpansive exp
  | Texp_array [] -> true
  | _ -> false
@


<<function Typecore.unify_exp>>=
(* Typing of expressions *)

let unify_exp env exp expected_ty =
  try
    unify env exp.exp_type expected_ty
  with Unify trace ->
    raise(Error(exp.exp_loc, Expr_type_clash(trace)))
@

<<function Typecore.type_binding>>=
(* Typing of toplevel bindings *)

let type_binding env rec_flag spat_sexp_list =
  Typetexp.reset_type_variables();
  type_let env rec_flag spat_sexp_list
@

<<function Typecore.type_expression>>=
(* Typing of toplevel expressions *)

let type_expression env sexp =
  Typetexp.reset_type_variables();
  begin_def();
  let exp = type_exp env sexp in
  end_def();
  if is_nonexpansive exp then generalize exp.exp_type
  else make_nongen exp.exp_type;
  exp
@

<<function Typecore.type_expect_fun>>=
@

%-------------------------------------------------------------

<<./typing/typecore.ml>>=
<<copyright header>>

(* Typechecking for the core language *)

open Misc
open Asttypes
open Parsetree
open Types
open Typedtree
open Btype
open Ctype


<<type Typecore.error>>

<<exception Typecore.Error>>

<<constant Typecore.type_constant>>

<<function Typecore.unify_pat>>

<<constant Typecore.pattern_variables>>

<<function Typecore.enter_variable>>

<<function Typecore.type_pat>>

<<function Typecore.add_pattern_variables>>

<<function Typecore.type_pattern>>

<<function Typecore.type_pattern_list>>

<<function Typecore.iter_pattern>>

<<function Typecore.is_nonexpansive>>

<<function Typecore.type_format>>

<<function Typecore.unify_exp>>

<<function Typecore.type_exp>>

<<function Typecore.type_binding>>

<<function Typecore.type_expression>>

<<function Typecore.type_expect_fun>>


(* Error report *)

open Format
open Printtyp

<<constant Typecore.report_error>>
@


\subsection*{[[./typing/typemod.mli]]}


<<signature Typemod.check_nongen_schemes>>=
val check_nongen_schemes:
        Env.t -> Typedtree.structure -> unit
@



%-------------------------------------------------------------

<<./typing/typemod.mli>>=
<<copyright header>>

(* Type-checking of the module language *)

open Types

<<signature Typemod.type_structure>>
<<signature Typemod.transl_signature>>
<<signature Typemod.check_nongen_schemes>>

<<type Typemod.error>>

<<exception Typemod.Error>>

<<signature Typemod.report_error>>
@


\subsection*{[[./typing/typemod.ml]]}

<<function Typemod.extract_sig_open>>=
(* Extract a signature from a module type *)

let extract_sig_open env loc mty =
  match Mtype.scrape env mty with
    Tmty_signature sg -> sg
  | _ -> raise(Error(loc, Structure_expected mty))
@

<<function Typemod.type_module_path>>=
(* Lookup the type of a module path *)

let type_module_path env loc lid =
  try
    Env.lookup_module lid env
  with Not_found ->
    raise(Error(loc, Unbound_module lid))
@

<<function Typemod.check_unique_names>>=
let check_unique_names sg =
  let type_names = ref StringSet.empty
  and module_names = ref StringSet.empty
  and modtype_names = ref StringSet.empty in
  let check cl loc set_ref name =
    if StringSet.mem name !set_ref
    then raise(Error(loc, Repeated_name(cl, name)))
    else set_ref := StringSet.add name !set_ref in
  let check_item item =
    match item.pstr_desc with
      Pstr_eval exp -> ()
    | Pstr_value(rec_flag, exps) -> ()
    | Pstr_primitive(name, desc) -> ()
    | Pstr_type name_decl_list ->
        List.iter
          (fun (name, decl) -> check "type" item.pstr_loc type_names name)
          name_decl_list
    | Pstr_exception(name, decl) -> ()
    | Pstr_module(name, smod) ->
        check "module" item.pstr_loc module_names name
    | Pstr_open lid -> ()
  in
    List.iter check_item sg
@

<<function Typemod.check_nongen_scheme>>=
let check_nongen_scheme env = function
    Tstr_value(rec_flag, pat_exp_list) ->
      List.iter
        (fun (pat, exp) ->
          if not (Ctype.closed_schema exp.exp_type) then
            raise(Error(exp.exp_loc, Non_generalizable exp.exp_type)))
        pat_exp_list
  | Tstr_module(id, md) ->
      if not (closed_modtype md.mod_type) then
        raise(Error(md.mod_loc, Non_generalizable_module md.mod_type))
  | _ -> ()
@

<<function Typemod.check_nongen_schemes>>=
let check_nongen_schemes env str =
  List.iter (check_nongen_scheme env) str
@



%-------------------------------------------------------------

<<./typing/typemod.ml>>=
<<copyright header>>

(* Type-checking of the module language *)

open Misc
open Longident
open Path
open Parsetree
open Types
open Typedtree


<<type Typemod.error>>

<<exception Typemod.Error>>

<<function Typemod.extract_sig_open>>

<<function Typemod.type_module_path>>

(* Check and translate a module type expression *)

let rec transl_modtype env smty =
  match smty.pmty_desc with
    Pmty_ident lid ->
      failwith "TODO: lookup_modtype"
(*
      begin try
        let (path, info) = Env.lookup_modtype lid env in 
        Tmty_ident path
      with Not_found ->
        raise(Error(smty.pmty_loc, Unbound_modtype lid))
      end
*)
  | Pmty_signature ssg ->
      Tmty_signature(transl_signature env ssg)
      
and transl_signature env sg =
  Ctype.init_def(Ident.current_time());
  match sg with
    [] -> []
  | {psig_desc = Psig_value(name, sdesc)} :: srem ->
      let desc = Typedecl.transl_value_decl env sdesc in
      let (id, newenv) = Env.enter_value name desc env in
      let rem = transl_signature newenv srem in
      Tsig_value(id, desc) :: rem
  | {psig_desc = Psig_type sdecls} :: srem ->
      let (decls, newenv) = Typedecl.transl_type_decl env sdecls in
      let rem = transl_signature newenv srem in
      map_end (fun (id, info) -> Tsig_type(id, info)) decls rem
  | {psig_desc = Psig_exception(name, sarg)} :: srem ->
      let arg = Typedecl.transl_exception env sarg in
      let (id, newenv) = Env.enter_exception name arg env in
      let rem = transl_signature newenv srem in
      Tsig_exception(id, arg) :: rem
  | {psig_desc = Psig_module(name, smty)} :: srem ->
      let mty = transl_modtype env smty in
      let (id, newenv) = Env.enter_module name mty env in
      let rem = transl_signature newenv srem in
      Tsig_module(id, mty) :: rem
  | {psig_desc = Psig_open lid; psig_loc = loc} :: srem ->
      let (path, mty) = type_module_path env loc lid in
      let sg = extract_sig_open env loc mty in
      let newenv = Env.open_signature path sg env in
      transl_signature newenv srem

(* Check that all type and module identifiers in a structure have
   distinct names (so that access by named paths is unambiguous). *)

module StringSet = Set

<<function Typemod.check_unique_names>>

(* Check that all core type schemes in a structure are closed *)

let rec closed_modtype = function
    Tmty_ident p -> true
  | Tmty_signature sg -> List.for_all closed_signature_item sg

and closed_signature_item = function
    Tsig_value(id, desc) -> Ctype.closed_schema desc.val_type
  | Tsig_module(id, mty) -> closed_modtype mty
  | _ -> true

<<function Typemod.check_nongen_scheme>>

<<function Typemod.check_nongen_schemes>>

(* Type a module value expression *)

let rec type_module env smod =
  match smod.pmod_desc with
    Pmod_ident lid ->
      let (path, mty) = type_module_path env smod.pmod_loc lid in
      { mod_desc = Tmod_ident path;
        mod_type = Mtype.strengthen env mty path;
        mod_env = env;
        mod_loc = smod.pmod_loc }
  | Pmod_structure sstr ->
      let (str, sg, finalenv) = type_structure env sstr in
      { mod_desc = Tmod_structure str;
        mod_type = Tmty_signature sg;
        mod_env = env;
        mod_loc = smod.pmod_loc }
  | Pmod_constraint(sarg, smty) ->
      let arg = type_module env sarg in
      let mty = transl_modtype env smty in
      let coercion =
        try
          Includemod.modtypes env arg.mod_type mty
        with Includemod.Error msg ->
          raise(Error(sarg.pmod_loc, Not_included msg)) in
      { mod_desc = Tmod_constraint(arg, mty, coercion);
        mod_type = mty;
        mod_env = env;
        mod_loc = smod.pmod_loc }

and type_structure env sstr =
  check_unique_names sstr;
  type_struct env sstr

and type_struct env sstr =
  Ctype.init_def(Ident.current_time());
  match sstr with
    [] ->
      ([], [], env)
  | {pstr_desc = Pstr_eval sexpr} :: srem ->
      let expr = Typecore.type_expression env sexpr in
      let (str_rem, sig_rem, final_env) = type_struct env srem in
      (Tstr_eval expr :: str_rem, sig_rem, final_env)
  | {pstr_desc = Pstr_value(rec_flag, sdefs)} :: srem ->
      let (defs, newenv) =
        Typecore.type_binding env rec_flag sdefs in
      let (str_rem, sig_rem, final_env) = type_struct newenv srem in
      let bound_idents = let_bound_idents defs in
      let make_sig_value id =
        Tsig_value(id, Env.find_value (Pident id) newenv) in
      (Tstr_value(rec_flag, defs) :: str_rem,
       map_end make_sig_value bound_idents sig_rem,
       final_env)
  | {pstr_desc = Pstr_primitive(name, sdesc)} :: srem ->
      let desc = Typedecl.transl_value_decl env sdesc in
      let (id, newenv) = Env.enter_value name desc env in
      let (str_rem, sig_rem, final_env) = type_struct newenv srem in
      (Tstr_primitive(id, desc) :: str_rem,
       Tsig_value(id, desc) :: sig_rem,
       final_env)
  | {pstr_desc = Pstr_type sdecls} :: srem ->
      let (decls, newenv) = Typedecl.transl_type_decl env sdecls in
      let (str_rem, sig_rem, final_env) = type_struct newenv srem in
      (Tstr_type decls :: str_rem,
       map_end (fun (id, info) -> Tsig_type(id, info)) decls sig_rem,
       final_env)
  | {pstr_desc = Pstr_exception(name, sarg)} :: srem ->
      let arg = Typedecl.transl_exception env sarg in
      let (id, newenv) = Env.enter_exception name arg env in
      let (str_rem, sig_rem, final_env) = type_struct newenv srem in
      (Tstr_exception(id, arg) :: str_rem,
       Tsig_exception(id, arg) :: sig_rem,
       final_env)
  | {pstr_desc = Pstr_module(name, smodl)} :: srem ->
      let modl = type_module env smodl in
      let (id, newenv) = Env.enter_module name modl.mod_type env in
      let (str_rem, sig_rem, final_env) = type_struct newenv srem in
      (Tstr_module(id, modl) :: str_rem,
       Tsig_module(id, modl.mod_type) :: sig_rem,
       final_env)
  | {pstr_desc = Pstr_open lid; pstr_loc = loc} :: srem ->
      let (path, mty) = type_module_path env loc lid in
      let sg = extract_sig_open env loc mty in
      type_struct (Env.open_signature path sg env) srem

(* Error report *)

open Format
open Printtyp

<<constant Typemod.report_error>>
@


